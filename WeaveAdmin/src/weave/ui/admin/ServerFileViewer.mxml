<?xml version="1.0" encoding="utf-8"?>
<!--
/* ***** BEGIN LICENSE BLOCK *****
 *
 * This file is part of Weave.
 *
 * The Initial Developer of Weave is the Institute for Visualization
 * and Perception Research at the University of Massachusetts Lowell.
 * Portions created by the Initial Developer are Copyright (C) 2008-2015
 * the Initial Developer. All Rights Reserved.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this file,
 * You can obtain one at http://mozilla.org/MPL/2.0/.
 * 
 * ***** END LICENSE BLOCK ***** */
-->
<mx:Panel 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns="weave.ui.*"
	layout="absolute"
	title="Server File Browser"
	borderAlpha="1"
	borderColor="#939178"
	borderThicknessBottom="0" borderThicknessLeft="5" borderThicknessRight="5" borderThicknessTop="0"
	verticalScrollPolicy="off"
	horizontalScrollPolicy="off"
	height="480" width="640"
	roundedBottomCorners="true"
	creationComplete="creationComplete(event)">
	
	<mx:VBox x="0" y="0" width="100%" height="100%" backgroundAlpha="1" backgroundColor="#939178">
		<mx:ControlBar paddingTop="0" paddingBottom="0" width="100%">
			<mx:Spacer width="100%" />
			<mx:Label id="statusLabel" text="Loading..." />
			<mx:Spacer width="20" />
			<mx:Button id="refreshButton" label="Refresh" click="refresh(event);" />
		</mx:ControlBar>
		
		<TextInputWithPrompt id="searchTerm" width="100%" clear="search()" change="search()" prompt="{prompt}"/>
		<mx:DataGrid id="dataGrid" paddingBottom="0" paddingTop="0" doubleClickEnabled="true"
					 editable="false" itemClick="itemClickHandler(event)"
					 width="100%" height="100%">
			
			<mx:columns>
				<mx:DataGridColumn dataField="fileName" headerText="Name" sortable="true" />
				<mx:DataGridColumn dataField="lastModified" labelFunction="lastModifiedLabel" headerText="Last Modified" sortable="true" />
				<mx:DataGridColumn dataField="fileSize" labelFunction="sizeLabel" headerText="Size" sortable="true" />
			</mx:columns>
		</mx:DataGrid>
		
		<mx:ControlBar paddingTop="5" paddingBottom="5" width="100%">
			<mx:Spacer width="100%"/>
			<mx:Button id="selectButton" label="Select" />
			<mx:Button id="cancelButton" label="Close"  />
		</mx:ControlBar>
	</mx:VBox>
	
	<mx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			import mx.rpc.AsyncToken;
			import mx.rpc.events.ResultEvent;
			
			import weave.compiler.StandardLib;
			import weave.services.Admin;
			import weave.services.addAsyncResponder;
			import weave.services.beans.WeaveFileInfo;
			import weave.utils.FileUtils;
			
			public static const INPUT_CSV:String = "INPUT_CSV";
			public static const INPUT_SHP:String = "INPUT_SHP";
			
			[Bindable] public var prompt:String = "Enter search text";
			[Bindable] private var inputType:String = "";
			[Bindable] private var fileCount:uint = 0;
			
			public function get selectedFileInfo():WeaveFileInfo { return dataGrid.selectedItem as WeaveFileInfo; }
			
			private function clear():void
			{
				searchTerm.text = "";
				search();
			}
			
			private function search():void
			{
				var source:Array = [];
				
				if (inputType == INPUT_CSV)
					source = Admin.instance.uploadedCSVFiles;
				else if (inputType == INPUT_SHP)
					source = Admin.instance.uploadedShapeFiles;
				
				if (searchTerm.text.length > 0)
				{
					var result:Array = [];
					for each (var info:WeaveFileInfo in source)
						if (info.fileName.toLowerCase().search(searchTerm.text.toLowerCase()) >= 0)
							result.push( info );
					dataGrid.dataProvider = result;
					fileCount = result.length;
				}
				else
				{
					dataGrid.dataProvider = source;
					fileCount = source.length;
				}
				refreshButton.enabled = true;
				statusLabel.text = fileCount + " file(s)";
			}
			
			public static function createServerFileViewer(parent:DisplayObject, inputType:String):ServerFileViewer
			{
				var fileViewer:ServerFileViewer = PopUpManager.createPopUp(parent, ServerFileViewer, true) as ServerFileViewer;
				PopUpManager.centerPopUp(fileViewer);
				fileViewer.inputType = inputType;
				fileViewer.cancelButton.addEventListener(MouseEvent.CLICK, function(..._):void { PopUpManager.removePopUp(fileViewer); });
				fileViewer.refreshButton.addEventListener(MouseEvent.CLICK, fileViewer.refresh );
				
				return fileViewer;
			}
			
			private function creationComplete(event:FlexEvent):void
			{
				if( inputType == INPUT_CSV )
					Admin.service.getUploadedCSVFiles();
				else if( inputType == INPUT_SHP )
					Admin.service.getUploadedSHPFiles();
				
				if( inputType == "" )
				{
					callLater(creationComplete, [event]);
					return;
				}
				
				refreshButton.enabled = false;
				statusLabel.text = "Loading...";
				selectButton.enabled = false;
				refresh();
			}
			
			private function itemClickHandler(event:ListEvent):void
			{
				if (dataGrid.selectedItems.length > 0)
					selectButton.enabled = true;
				else
					selectButton.enabled = false;
			}
			
			private function refresh(e:MouseEvent = null):void
			{
				selectButton.enabled = false;
				refreshButton.enabled = false;
				statusLabel.text = "Loading...";
				
				var query:AsyncToken;
				if( inputType == INPUT_CSV )
					query = Admin.service.getUploadedCSVFiles();
				else if( inputType == INPUT_SHP )
					query = Admin.service.getUploadedSHPFiles();
				
				addAsyncResponder(query, handleUploadedFiles);
				function handleUploadedFiles(event:ResultEvent, token:Object = null):void
				{
					search();
				}
			}
			
			private function sizeLabel(info:WeaveFileInfo, column:DataGridColumn):String
			{
				return FileUtils.parse(info.fileSize as Number, 2);
			}
			private function lastModifiedLabel(info:WeaveFileInfo, column:DataGridColumn):String
			{
				return StandardLib.formatDate(info.lastModified, "MM/DD/YYYY L:NN A", false);
			}
		]]>
	</mx:Script>
</mx:Panel>
