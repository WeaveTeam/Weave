<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<mx:VBox horizontalScrollPolicy="off" verticalScrollPolicy="off" 
		 xmlns="weave.ui.admin.*" xmlns:mx="http://www.adobe.com/2006/mxml" 
		 width="100%"
		 creationComplete="getSchemas()"
		 xmlns:ui="weave.ui.*">
	<ui:Indent indent="{indent}" label="{Strings.sql_schema}">
		<ui:CustomComboBox id="schemaInput" enabled="{schemaInput.dataProvider.length > 0}" prompt="Select a schema..." toolTip="{Strings.tip_sql_schema_dropdown}" text="{activeSchema}" dataProvider="{schemas}" change="activeSchema = schemaInput.text;"/>
		<mx:Button id="schemasRefreshButton" label="Refresh" click="getSchemas()" toolTip="{Strings.tip_refresh_schemas}"/>
	</ui:Indent>
	<ui:Indent id="tableControls" indent="{indent}" label="{Strings.sql_table}">
		<ui:CustomComboBox id="_tableCombo" enabled="{_tableCombo.dataProvider.length > 0}" prompt="Select a table..." text="{activeTable}" dataProvider="{tables}" change="activeTable = _tableCombo.text;" toolTip="{Strings.tip_sql_table_dropdown}"/>
		<mx:Button id="tablesRefreshButton" label="Refresh" enabled="{schemaInput.selectedIndex >= 0 &amp;&amp; !refreshingTables}" click="getTables()" toolTip="{Strings.tip_refresh_tables}"/>
		<ui:TextInputWithPrompt id="_tableInput" editable="true" text="{activeTable}" prompt="Enter a table name" change="activeTable = _tableInput.text;"/>
		<mx:CheckBox label="Overwrite" id="tableOverwriteCheck" selected="false" visible="{tableOverwriteCheckVisible}" width="{tableOverwriteCheckVisible ? NaN : 0}" toolTip="{Strings.tip_overwrite_sql_table}"/>
	</ui:Indent>
	<mx:Metadata>
		[Exclude(name="_tableCombo", kind="property")]
		[Exclude(name="_tableInput", kind="property")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.collections.ICollectionView;
			import mx.events.CollectionEvent;
			import mx.events.ListEvent;
			import mx.rpc.events.ResultEvent;
			
			import weave.Strings;
			import weave.services.*;
			import weave.utils.ByteArrayUtils;
			
			// values returned by the server that depend on the active selected values
			[Bindable] public var schemas:Array = [];
			[Bindable] public var tables:Array = [];
			[Bindable] public var columns:Array = [];
			
			[Bindable] public var indent:int = 128;
			[Bindable] public var tableOverwriteCheckVisible:Boolean = true;
			[Bindable] public var showTableCombo:Boolean = true;

			private var _activeSchema:String = ""; // has corresponding public setter/getter
			private var _activeTable:String = ""; // has corresponding public setter/getter
			[Bindable] private var refreshingTables:Boolean = false;

			override protected function initializationComplete():void
			{
				super.initializationComplete();
				
				if (showTableCombo)
				{
					tableControls.removeChild(_tableInput);
				}
				else
				{
					tableControls.removeChild(_tableCombo);
					tableControls.removeChild(tablesRefreshButton);
				}
			}

			public function getSchemas():void
			{
				activeSchema = '';
				// clear current list, then request new list
				schemas = [];
				AdminInterface.instance.service.getSchemas(
					AdminInterface.instance.activeConnectionName,
					AdminInterface.instance.activePassword
				).addAsyncResponder(handler, handler);
				
				schemasRefreshButton.enabled = false;
				function handler(event:Event, token:Object = null):void
				{
					if (event is ResultEvent)
						schemas = (event as ResultEvent).result as Array;
					schemasRefreshButton.enabled = true;
				}
			}
			
			[Bindable] public function get activeSchema():String
			{
				return _activeSchema;
			}
			public function set activeSchema(value:String):void
			{
				if (_activeSchema != value)
				{
					_activeSchema = value;
					getTables();
				}
			}
			
			public function getTables():void
			{
				if (!showTableCombo)
					return;
				activeTable = '';
				// clear current list, then request new list
				tables = [];
				if (activeSchema != '')
				{
					AdminInterface.instance.service.getTables(
						AdminInterface.instance.activeConnectionName,
						AdminInterface.instance.activePassword,
						activeSchema
					).addAsyncResponder(handler, handler);
					
					refreshingTables = true;
					function handler(event:Event, token:Object = null):void
					{
						if (event is ResultEvent)
							tables = (event as ResultEvent).result as Array;
						refreshingTables = false;
					}
				}
				
			}
			
			[Bindable] public function get activeTable():String
			{
				return _activeTable;
			}
			public function set activeTable(value:String):void
			{
				if (_activeTable != value)
				{
					_activeTable = value;
					getColumns();
				}
			}
			
			public function getColumns():void
			{
				// clear current list, then request new list
				columns = [];
				if (activeSchema != '' && activeTable != '')
				{
					AdminInterface.instance.service.getColumns(
						AdminInterface.instance.activeConnectionName,
						AdminInterface.instance.activePassword,
						activeSchema,
						activeTable
					).addAsyncResponder(handleGetColumns);
				}
				
				function handleGetColumns(event:ResultEvent, token:Object = null):void
				{
					columns = event.result as Array;
				}
			}
		]]>
	</mx:Script>
</mx:VBox>
