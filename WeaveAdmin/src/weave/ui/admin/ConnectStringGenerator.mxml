<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<!---
	Check linkableObjectIsBusy() to see if this component is busy with an RPC call.
	@author adufilie
-->
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
				xmlns:ui="weave.ui.*"
				xmlns="weave.ui.admin.managers.*" implements="weave.api.core.ILinkableObject" 
				>
	<ui:Indent label="SQL Server type" group="{indentGroup}">
		<ui:CustomComboBox id="dbmsCombo" change="{generateDatabaseChange()}" dataProvider="{ConnectionInfo.dbmsList}" width="130"/>
	</ui:Indent>
	<ui:IndentTextInput label="SQL Server IP" id="ipInput" change="{generate()}" text="{hostIP}" inputWidth="130" group="{indentGroup}"/>
	<ui:IndentTextInput label="SQL Server Port" id="portInput" change="{generate()}" text="{ConnectionInfo.getDefaultPort(dbmsCombo.text)}" inputWidth="130" group="{indentGroup}"/>
	<ui:Indent id="sqlDatabaseName" label="SQL Database name" group="{indentGroup}">
		<mx:TextInput id="databaseInput" change="{generate()}" width="130"/>
		<mx:Label id="optionalText" text="(Optional)"/>
		<ui:HelpComponent id="optionalHelp">
			This field is optional.  You can specify the name of a default database to connect to.
			For SQL Server, this is an instance name.
			Similarly in PostGreSQL, databases are different from schemas.
			MySQL does not differentiate between the two.
		</ui:HelpComponent>
		<!--
			This field is required only for MS SQLServer connections.
			If you had a Server Name \'LocalMachine\\Express\', then this field should be entered as Express.
			If there is no instance name for your server, you may leave this blank.
		-->
	</ui:Indent>
	<ui:IndentTextInput label="SQL User Name" id="userInput" change="{generate()}" inputWidth="130" group="{indentGroup}"/>
	<ui:IndentTextInput label="SQL Password" id="passInput" change="{generate()}" displayAsPassword="false" inputWidth="130" group="{indentGroup}"/>
	<mx:Script>
		<![CDATA[
			import mx.rpc.AsyncToken;
			import mx.rpc.events.ResultEvent;
			import mx.utils.StringUtil;
			
			import weave.api.WeaveAPI;
			import weave.api.core.ICallbackCollection;
			import weave.api.getCallbackCollection;
			import weave.services.Admin;
			import weave.services.addAsyncResponder;
			import weave.services.beans.ConnectionInfo;
			import weave.utils.EventUtils;
			
			[Bindable] public var indentGroup:Object = this;
			
			[Bindable] public var connectString:String = '';
			
			private static const MYSQL:String = 'MySQL';
			private static const POSTGRESQL:String = 'PostGreSQL';
			private static const SQLSERVER:String = 'Microsoft SQL Server';
			private static const ORACLE:String = 'Oracle';
			
			private function get hostUrl():String
			{
				var result:Array = WeaveAPI.topLevelApplication.url.split("http://");
				result = (result.pop() as String).split("/");
				return result[0] as String;
			}
			
			private const hostIP:String = hostUrl.split(':')[0];
			
			public function resetFields(resetConnectString:Boolean = true):void
			{
				dbmsCombo.selectedIndex = 0;
				ipInput.text = hostIP;
				databaseInput.text = "";
				userInput.text = "";
				passInput.text = "";
				if (resetConnectString)
					connectString = "jdbc:mysql://localhost:3306/?user=&password=";
			}
			
			private function generateDatabaseChange():void
			{
				switch(dbmsCombo.text)
				{
					case MYSQL:
					{
						sqlDatabaseName.label = "SQL Database name";
						optionalHelp.visible = true;
						optionalText.visible = true;
						break;
					}
					case POSTGRESQL:
					{
						sqlDatabaseName.label = "SQL Database name";
						optionalHelp.visible = true;
						optionalText.visible = true;
						break;
					}
					case SQLSERVER:
					{
						sqlDatabaseName.label = "Instance Name";
						optionalHelp.visible = false;
						optionalText.visible = false;
						break;
					}
					case ORACLE:
					{
						sqlDatabaseName.label = "SQL Database name";
						optionalHelp.visible = true;
						optionalText.visible = true;
						break;
					}
				}
				WeaveAPI.SessionManager.assignBusyTask(_generate, this);
				_delayedGenerate();
				triggerCallbacks();
			}
			
			private function generate():void
			{
				WeaveAPI.SessionManager.assignBusyTask(_generate, this);
				_delayedGenerate();
				triggerCallbacks();
			}
			
			private var _delayedGenerate:Function = EventUtils.generateDelayedCallback(this, _generate, 500);
			
			private function triggerCallbacks():void
			{
				getCallbackCollection(this).triggerCallbacks();
			}
			private function _generate():void
			{
				WeaveAPI.SessionManager.unassignBusyTask(_generate);
				
				connectString = '';
				connectString = getConnectString(dbmsCombo.text, ipInput.text, portInput.text, databaseInput.text, userInput.text, passInput.text);
				triggerCallbacks();
			}
			
			/**
			 * @param dbms The name of a DBMS (MySQL, PostGreSQL, Microsoft SQL Server)
			 * @param ip The IP address of the DBMS.
			 * @param port The port the DBMS is on (optional, can be "" to use default).
			 * @param database The name of a database to connect to (can be "" for MySQL)
			 * @param user The username to use when connecting.
			 * @param pass The password associated with the username.
			 * @return A connect string that can be used in the getConnection() function.
			 */
			private function getConnectString(dbms:String, ip:String, port:String, database:String, user:String, pass:String):String
			{
				var host:String;
				if (port == null || port.length == 0)
					host = ip; // default port for specific dbms will be used
				else
					host = ip + ":" + port;
				
				var format:String = null;
				if (SQLSERVER == dbms)
				{
					dbms = "sqlserver"; // this will be put in the format string
					format = "jdbc:jtds:{0}://{1}/;instance={2};user={3};password={4}";
				}
				else if (ORACLE == dbms)
				{
					format = "jdbc:{0}:thin:{1}/{2}@{3}:{4}";
					//"jdbc:oracle:thin:<user>/<password>@<host>:<port>:<instance>"
				}
				else // MySQL or PostGreSQL
				{
					format = "jdbc:{0}://{1}/{2}?user={3}&password={4}";
				}
				
				// MySQL connect string uses % as an escape character, so we must use URLEncoder.
				// PostGreSQL does not support % as an escape character, and does not work with the & character.
				if (dbms == MYSQL)
				{
					try
					{
						database = encodeURIComponent(database);
						user = encodeURIComponent(user);
						pass = encodeURIComponent(pass);
					}
					catch (e:Error)
					{
						// this should never happen
						throw new Error(e);
					}
				}
				
				var result:String = "";
				if (dbms == ORACLE)
					result = StringUtil.substitute(format, dbms.toLowerCase(), user, pass, host, database);
				else
					result = StringUtil.substitute(format, dbms.toLowerCase(), host, database, user, pass);
				
				return result;
			}
		]]>
	</mx:Script>
</mx:VBox>
