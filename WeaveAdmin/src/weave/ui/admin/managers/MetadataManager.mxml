<?xml version="1.0" encoding="utf-8"?>
<mx:HDividedBox xmlns:mx="http://www.adobe.com/2006/mxml"
                xmlns:ui="weave.ui.*"
                xmlns:aci="weave.services.beans.*"
                xmlns="weave.ui.admin.managers.*"
                label="{lang('Data Hierarchies')}{metadataEditor.isChanged?'*':''}"
                creationComplete="handleCreated()">
    <mx:VBox height="100%" width="100%" enabled="{!metadataEditor.isChanged}">
		<ui:IndentTextInput label="{lang('Data Tables')}" labelFontWeight="bold" prompt="{lang('Search')}" id="tableSearch" width="100%"/>
        <ui:EntityTree id="tableTree" rootFilterType="{EntityType.TABLE}" nodeFilterFunction="{filterTables}"
			entityCache="{Admin.entityCache}"
            width="100%" height="100%" dragEnabled="true" dragMoveEnabled="{editTablesButton.selected}" dropEnabled="{editTablesButton.selected}"/>
        <mx:HBox width="100%">
			<mx:Spacer width="100%"/>
			<mx:Button toggle="true" label="Edit tables" id="editTablesButton" visible="false" includeInLayout="false"/>
            <mx:Button label="{lang('Delete')}" click="deleteTable()" enabled="{tableTree.selectedItem}"/>
			<mx:Button label="{lang('Refresh')}" click="Admin.entityCache.invalidateAll();"/>
        </mx:HBox>
    </mx:VBox>
    <mx:VBox id="hierarchyDiv" minWidth="{plusButton.width}" height="100%" width="100%"
			 resize="minusButton.includeInLayout = minusButton.visible = !(plusButton.includeInLayout = plusButton.visible = (hierarchyDiv.width == plusButton.height));"
			 enabled="{!metadataEditor.isChanged &amp;&amp; !editTablesButton.selected}">
        <mx:HBox width="100%" horizontalGap="0">
			<mx:HBox width="100%" minWidth="0" visible="{minusButton.visible}" includeInLayout="{minusButton.visible}" horizontalScrollPolicy="off">
				<mx:Label text="{lang('Custom Hierarchies')}" fontWeight="bold"/>
				<ui:HelpComponent>
					The Weave client does not read these hierarchies yet, but it will in a future version.
					Check for an update soon.
				</ui:HelpComponent>
				<mx:Spacer width="100%"/>
			</mx:HBox>
			<mx:Button id="minusButton" width="{minusButton.height}"
					   toolTip="{lang('Hide Custom Hierarchies')}"
					   click="hierarchyDiv.width = plusButton.height;"
					   icon="@Embed(source='/weave/resources/images/minus.png')"/>
			<mx:Button id="plusButton" width="{plusButton.height}"
					   toolTip="{lang('Show Custom Hierarchies')}"
					   click="hierarchyDiv.width = hierarchyDiv.parent.width / 3"
					   icon="@Embed(source='/weave/resources/images/plus.png')"/>
		</mx:HBox>
		<mx:VBox height="100%" width="100%" visible="{minusButton.visible}" includeInLayout="{minusButton.visible}">
	        <ui:EntityTree id="hierarchyTree" rootFilterType="{EntityType.HIERARCHY}"
	            width="100%" height="100%" entityCache="{Admin.entityCache}"
	            dragEnabled="{!editTablesButton.selected}" dragMoveEnabled="true" dropEnabled="true"/>
	        <mx:HBox width="100%">
		        <ui:TextInputWithPrompt id="categoryNameInput" prompt="{lang('New {0} name', categoryOrHierarchyLabel(hierarchyTree.selectedItems))}" width="100%" enter="addCategory()"/>
	            <mx:Button label="{lang('Add {0}', categoryOrHierarchyLabel(hierarchyTree.selectedItems))}" click="addCategory()" enabled="{Boolean(categoryNameInput.text)}"/>
	            <mx:Button label="{lang('Remove')}" click="removeHierarchy()" enabled="{hierarchyTree.selectedItem}"/>
	        </mx:HBox>
		</mx:VBox>
    </mx:VBox>
	<ui:MetadataEditor id="metadataEditor" height="100%" width="100%" targetItems="{selectedItems}"/>
<mx:Script>
<![CDATA[
	import mx.events.ListEvent;
	
	import weave.core.UIUtils;
	import weave.services.Admin;
	import weave.services.beans.Entity;
	import weave.services.beans.EntityType;
	import weave.ui.EntityNode;
	import weave.ui.EntityTree;
	import weave.utils.EventUtils;
	import weave.utils.PopUpUtils;
	
	private function categoryOrHierarchyLabel(hierarchyTreeSelectedItems:Array):String
	{
		return hierarchyTreeSelectedItems.length == 1 ? 'category' : 'hierarchy';
	}

    [Bindable] public var selectedItems:Array = [];
	
    public function handleCreated():void
    {
		debugId(this);
		
        tableTree.addEventListener(ListEvent.CHANGE, handleTreeChange);
        hierarchyTree.addEventListener(ListEvent.CHANGE, handleTreeChange);
		
		EventUtils.addDelayedEventCallback(tableSearch, Event.CHANGE, tableTree.refresh, 100);
    }
	
	private function handleTreeChange(event:ListEvent):void
	{
		var tree:EntityTree = event.currentTarget as EntityTree;
		if (Admin.instance.getFocusEntityId() >= 0)
		{
			var node:EntityNode = tree.scrollToAndSelectMatchingItem(isFocusedEntityNode) as EntityNode;
			if (node)
				tree.expandItem(node, true);
		}
		
		selectionsChanged(event);
	}
	
	private function isFocusedEntityNode(node:EntityNode):Boolean
	{
		if (node && node.id == Admin.instance.getFocusEntityId())
		{
			Admin.instance.clearFocusEntityId();
			return true;
		}
		
		return false;
	}

	private function filterTables(item:EntityNode):Boolean
	{
		if (!tableSearch.text)
			return true;
		return item.label.toLowerCase().search(tableSearch.text.toLowerCase()) >= 0;
	}
	
    public function deleteTable():void
    {
		var foundTable:Boolean = false;
		var foundColumn:Boolean = false;
		for each (var item:Object in tableTree.selectedItems)
		{
			var entity:Entity = (item as EntityNode).getEntity();
			if (entity.type == EntityType.TABLE)
				foundTable = true;
			else if (entity.type == EntityType.COLUMN)
				foundColumn = true;
		}
		var typeStr:String = lang("tables and columns");
		if (foundTable != foundColumn)
			typeStr = foundTable ? lang("tables") : lang("columns");
			
        PopUpUtils.confirm(
			this,
			lang("Delete {0}", typeStr),
			lang("Delete selected {0}?\nExisting visualizations using this data will stop working.", typeStr),
			deleteTableFunction,
			null,
			lang("Delete"),
			lang("Cancel")
		);
        function deleteTableFunction():void
        {
			for each (var item:EntityNode in tableTree.selectedItems)
			{
				Admin.entityCache.delete_entity(item.id);
	        }
			tableTree.selectedItem = null;
		}
    }
    public function addCategory():void
    {
		var name:String = categoryNameInput.text
        if (!name)
        {
            UIUtils.componentPulse(categoryNameInput);
            return;
        }
		
		var node:EntityNode;
		if (hierarchyTree.selectedItems.length == 1)
			node = hierarchyTree.selectedItem as EntityNode;
		else
			node = hierarchyTree.rootNode;
        
		var index:int = -1;
		// if user selected a column, use its parent node and insert at the column's position
		if (node.getEntity().type == EntityType.COLUMN)
		{
			var parent:EntityNode = hierarchyTree.getParentItem(node);
			index = parent.getEntity().childIds.indexOf(node.id);
			node = parent;
		}
		
		Admin.entityCache.add_category(name, node.id, index);
   	    categoryNameInput.text = "";
		hierarchyTree.expandItem(node, true); // expand so new child can be selected
    }
    public function selectionsChanged(event:ListEvent):void
    {
		var tree:EntityTree = event.currentTarget as EntityTree;
		var otherTree:EntityTree = tree === tableTree ? hierarchyTree : tableTree;
		
		// if both trees have something selected, prefer the tree that just selected something.
		if (tree.selectedItem && otherTree.selectedItem)
			otherTree.selectedItems = [];
		
		if (tree.selectedItem)
			this.selectedItems = tree.selectedItems.concat().reverse(); // use reverse order because shift+click produces reverse order
		else if (!otherTree.selectedItem)
			this.selectedItems = []; // neither tree has selection
    }
    public function removeHierarchy():void
    {
		for each (var item:EntityNode in hierarchyTree.selectedItems)
		{
			if (item.getEntity().type == EntityType.HIERARCHY)
			{
	            PopUpUtils.confirm(
					this,
					lang("Remove hierarchy items"),
					lang('You have selected some root hierarchies.  Remove all selected items?'),
					removeImmediately,
					null,
					lang("Remove"),
					lang("Cancel")
				);
				return;
			}
		}
		removeImmediately();
	}
	private function removeImmediately():void
	{
		for each (var item:EntityNode in hierarchyTree.selectedItems)
		{
	        var type:int = item.getEntity().type;
	        var parent:EntityNode = hierarchyTree.getParentItem(item);
	        if (parent)
	        {
				Admin.entityCache.remove_child(parent.id, item.id);
	        }
	        else if (type == EntityType.HIERARCHY)
	        {
				Admin.entityCache.delete_entity(item.id);
	        }
	        else
	        {
				// this occurs when root item is selected.. should never happen
				weaveTrace("Entity " + item.id + " has no parent and is not a hierarchy");
	        }
		}
		hierarchyTree.selectedItem = null;
		selectedItems = [];
    }
]]>
</mx:Script>
</mx:HDividedBox>
