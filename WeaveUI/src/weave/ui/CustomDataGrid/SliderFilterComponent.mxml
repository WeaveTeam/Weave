<?xml version="1.0" encoding="utf-8"?>
<!--
/*
Weave (Web-based Analysis and Visualization Environment)
Copyright (C) 2008-2011 University of Massachusetts Lowell

This file is a part of Weave.

Weave is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License, Version 3,
as published by the Free Software Foundation.

Weave is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<mx:Box  xmlns:mx="http://www.adobe.com/2006/mxml"  implements="weave.ui.CustomDataGrid.IFilterComponent"
		 verticalScrollPolicy="off" horizontalScrollPolicy="off"  creationPolicy="all" >
	
	<mx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			
			import weave.api.WeaveAPI;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IQualifiedKey;
			import weave.api.getCallbackCollection;
			import weave.api.registerLinkableChild;
			import weave.core.LinkableNumber;
			import weave.utils.EventUtils;
			
			public var column:WeaveCustomDataGridColumn;
			
			
			public function mapColumnToFilter(column:WeaveCustomDataGridColumn):void{				
				 this.column =column;				
			 }
			
			override protected function childrenCreated():void{
				super.childrenCreated();
				// this prevents null point error while updatingDisplayList(thumbs is Null)
				rangeSlider.validateProperties();
			}
			
			protected function rangeSlider_initializeHandler(event:FlexEvent):void
			{
				if(!column){
					return callLater(rangeSlider_initializeHandler,[event]);
				}
				dataMinimum = WeaveAPI.StatisticsCache.getMin(column.attrColumn);
				dataMaximum = WeaveAPI.StatisticsCache.getMax(column.attrColumn);
				rangeSlider.values = [dataMinimum, dataMaximum];
				EventUtils.addBindCallback(event.target, 'values', handleBindableValues);
				getCallbackCollection(this).addGroupedCallback(this, handleLinkableValues);				
			}
			private function handleBindableValues():void
			{
				minimum.value = rangeSlider.values[0];
				maximum.value = rangeSlider.values[1];
			}
			private function handleLinkableValues():void				
			{
				rangeSlider.values = [minimum.value, maximum.value];
			}
			
			
			//only get need to be allowed set needs to be disabled
			[Bindable]		
			public var dataMinimum:Number;
			[Bindable]
			public var dataMaximum:Number; 
			
			public const minimum:LinkableNumber = registerLinkableChild(this,new LinkableNumber());
			public const maximum:LinkableNumber = registerLinkableChild(this,new LinkableNumber());
			
			//range value nor getting stored in session need to find why?s
			public function get isActive():Boolean{
				var min:Number = minimum.value;
				var max:Number = maximum.value;
				return (min != dataMinimum || max != dataMaximum);
			}
			
			
			public function filterFunction(key:Object):Boolean	{
				var value:Number = column.attrColumn.getValueFromKey(key as IQualifiedKey, Number);
				var min:Number = minimum.value;
				var max:Number = maximum.value;
				if (!isNaN(value))
				{
					if (!isNaN(min) && value < min)return false;
					if (!isNaN(max) && value > max)return false;
				}
				return true;
			}
			
			
		]]>
	</mx:Script>
	
	<mx:HSlider id="rangeSlider" thumbCount="2" liveDragging="true" width="100%" height="100%" minWidth="0"
				minimum="{dataMinimum}" maximum="{dataMaximum}"
				labels="{[dataMinimum, dataMaximum]}"
				initialize="rangeSlider_initializeHandler(event)"
				showTrackHighlight="true" />
	
</mx:Box >
