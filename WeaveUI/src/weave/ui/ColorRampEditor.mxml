<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->

<!--
ColorRampEditor

@sanbalagan
-->
<mx:HBox xmlns:ui ="weave.ui.*"
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns="weave.ui.*"
	implements="weave.api.core.ILinkableObject"
	width="{NaN}" height="{NaN}"
	>
	<mx:VBox verticalGap="3">
		<mx:DataGrid id="colorRampGrid"
					 useRollOver="false"
					 minWidth="200" width="100%" height="100%"
					 fontWeight="normal"
					 paddingBottom="2" paddingTop="2" paddingLeft="5" paddingRight="5" 
					 horizontalScrollPolicy="off" verticalScrollPolicy="on"
					 allowMultipleSelection="false"
					 dataProvider="{ColorRamp.allColorRamps.colorRamp}"
					 change="handleColorRampSelected()">
			<mx:columns>												
				<mx:DataGridColumn dataField="@name"  headerText="Color scale"  itemRenderer="weave.ui.colormap.ColorRampPresetItemRenderer"/>
			</mx:columns>
		</mx:DataGrid>
		<mx:Button label="Reverse color scale"
					 toolTip="Click to reverse the colors in the color scale."
					 click="reverseColorRamp()"/>
	</mx:VBox>
	<mx:VBox verticalGap="1">
		<mx:Label text="Filter by:" fontWeight="bold"/>
		<mx:CheckBox id="colorblindSafeCheckbox"
					 label="Colorblind-safe"
					 toolTip="Click to only show choices that are colorblind safe."
					 click="handleFilterOptionSet(event)"/>
		<mx:CheckBox id="singleHueCheckbox"
					 label="Single hue"
					 toolTip="Click to only show choices that usea single hue (base color)."
					 click="handleFilterOptionSet(event)"/>
		<mx:CheckBox id="printFriendlyCheckbox"
					 label="Printer friendly"
					 toolTip="Click to only show choices that are printer friendly."
					 click="handleFilterOptionSet(event)"/>
		<mx:CheckBox id="photocopyableCheckbox"
					 label="Photocopy-able"
					 toolTip="Click to only show choices that can be photocopied."
					 click="handleFilterOptionSet(event)"/>
		<mx:HBox>
			<mx:Label text="Color scale style:" width="130"/>
			<ui:CustomComboBox id="typeCombo"   change="handleFilterOptionSet(event)" width="100%" color="black" rollOverColor="0xCCFEFF"/>
		</mx:HBox>
		
		<mx:HBox>
			<mx:Label text="Color scale category:" width="130"/>
			<ui:CustomComboBox id="categoriesCombo"   change="handleFilterOptionSet(event)" width="100%" color="black" rollOverColor="0xCCFEFF"/>
		</mx:HBox>
	</mx:VBox>
	<mx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.collections.XMLListCollection;
			import mx.core.Application;
			import mx.managers.PopUpManager;
			
			import weave.Weave;
			import weave.api.core.ILinkableDynamicObject;
			import weave.api.core.ILinkableObject;
			import weave.api.linkSessionState;
			import weave.data.AttributeColumns.BinnedColumn;
			import weave.data.AttributeColumns.ColorColumn;
			import weave.data.AttributeColumns.FilteredColumn;
			import weave.primitives.ColorRamp;
			
			private var _colorRamp:ColorRamp = null;
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				var colorRampList:XMLList = new XMLList(ColorRamp.allColorRamps.colorRamp);
				var sources:ArrayCollection     = new ArrayCollection(["All"]);
				var categories:ArrayCollection  = new ArrayCollection(["Any"]);
				var dataFormats:ArrayCollection = new ArrayCollection(["All"]);
				
				
				for (var i:int = 0; i < colorRampList.length(); i++)
				{
					var colorRamp:XML = colorRampList[i] as XML;
					
					/*if(colorRamp.@name == multiLayerColormap.getColormap().colorRamp.name)
					colormapGrid.selectedIndex = i;*/
					
					var sourceName:String = colorRamp.@source;
					if(colorRamp.hasOwnProperty("@source") && !sources.contains(sourceName) )
						sources.addItem(sourceName);
					
					var category:String = colorRamp.@category;
					if(colorRamp.hasOwnProperty("@category") && !categories.contains(category) )
						categories.addItem(category);
					
					var dataFormat:String = colorRamp.@dataFormat;
					if(colorRamp.hasOwnProperty("@dataFormat") && !dataFormats.contains(dataFormat) )
						dataFormats.addItem(dataFormat);
				}
				
				typeCombo.dataProvider = dataFormats;
				categoriesCombo.dataProvider = categories;
				
			}
			
			
			private function colorRampCallback():void
			{
				if (!parent)
					return callLater(colorRampCallback);
				// update gui
				var rampXML:XML;
				try
				{
					rampXML = XML(_colorRamp.value);
				}
				catch (e:Error)
				{
					return;
				}
				
				// update selected ramp by name
				var xmlList:XMLList = ColorRamp.allColorRamps.colorRamp.(@name == rampXML.@name);
				if (xmlList.length() > 0)
					colorRampGrid.selectedItem = xmlList[0];
				else
					colorRampGrid.selectedItem = null;
			}
			
			private function handleColorRampSelected():void
			{
				if (colorRampGrid.selectedItem is XML)
					_colorRamp.value = ColorRamp.getColorRampXMLByName(colorRampGrid.selectedItem.@name);
			}
			
			private function reverseColorRamp():void
			{
				_colorRamp.reverse();
			}
			public function set colorRamp(cRamp:ColorRamp):void
			{
				if (_colorRamp == cRamp)
					return;
				
				if (_colorRamp != null)
					_colorRamp.removeCallback(colorRampCallback);
				
				_colorRamp = cRamp;
				
				if (_colorRamp != null)
					_colorRamp.addGroupedCallback(this, colorRampCallback, true);
			}
			
			public function get colorRamp():ColorRamp
			{
				return _colorRamp;
			}
			
			private var previousSelectedItem:Object = null;
			private function handleFilterOptionSet(event:Event):void
			{	
				if(colorRampGrid.selectedItem != null)
					previousSelectedItem = colorRampGrid.selectedItem;
				
				
				(colorRampGrid.dataProvider as XMLListCollection).filterFunction = filterColormaps;
				(colorRampGrid.dataProvider as XMLListCollection).refresh();
				
				// disable the presetSelector if there are no colormaps that can be selected
				colorRampGrid.enabled = true;
				if( (colorRampGrid.dataProvider as XMLListCollection).length == 0)
					colorRampGrid.enabled = false;
				
				// if the selected item is null, set it to the previous one (should never be null)
				if(colorRampGrid.selectedItem == null)
					colorRampGrid.selectedItem = previousSelectedItem;
			}
			
			private function filterColormaps(item:Object):Boolean {
				var meetsCriteria:Boolean = true;
				
				if(colorblindSafeCheckbox.selected)
					meetsCriteria &&= (item.@colorblindSafe == true);
				if(printFriendlyCheckbox.selected)
					meetsCriteria &&= (item.@printFriendly == true);
				if(photocopyableCheckbox.selected)
					meetsCriteria &&= (item.@photocopyable == true);
				if(singleHueCheckbox.selected)
					meetsCriteria &&= (item.@singlehue == true);
				
				/*if(sourceCombo.selectedIndex != 0)	
				meetsCriteria &&= (item.@source == sourceCombo.selectedItem);*/
				
				if(typeCombo.selectedIndex != 0)	
					meetsCriteria &&= (item.@dataFormat == typeCombo.selectedItem);
				
				if(categoriesCombo.selectedIndex != 0)	
					meetsCriteria &&= (item.@category == categoriesCombo.selectedItem);
				
				return meetsCriteria;
				
			}
			
		]]>
	</mx:Script>
	
</mx:HBox>

