<?xml version="1.0" encoding="utf-8"?>
<!--
/*
Weave (Web-based Analysis and Visualization Environment)
Copyright (C) 2008-2011 University of Massachusetts Lowell

This file is a part of Weave.

Weave is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License, Version 3,
as published by the Free Software Foundation.

Weave is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<DraggablePanel xmlns="weave.ui.*" xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				title="{lang('Saved Subsets')}"
				width="25%" height="10%"
				xmlns:tools="weave.visualization.tools.*" 
				xmlns:editors="weave.editors.*">
	<layout>
		<s:VerticalLayout/>
	</layout>
	<CustomComboBox id="subsetSelectorBox" prompt="{lang('Select a subset...')}"
					width="100%" height="100%"
					labelFunction="subsetsListLabelFunction"
					valueCommit="itemSelected()"
					/>
	<fx:Script>
		<![CDATA[
			import weave.Weave;
			import weave.api.copySessionState;
			import weave.api.core.ILinkableObject;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.registerLinkableChild;
			import weave.core.LinkableBoolean;
			import weave.data.KeySets.KeyFilter;
			
			public const includeAllRecordsOption:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(false), updateSubsets);
			private var setToIndex:int = -1;
			
			override protected function constructor():void
			{
				super.constructor();
				
				minimizable.value = false;
				maximizable.value = false;
				pinned.value = true;
			}
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				Weave.savedSubsetsKeyFilters.addGroupedCallback(this, updateSubsets, true);
				getCallbackCollection(Weave.defaultSubsetKeyFilter).addGroupedCallback(this, handleGlobalSubsetChange);
			}
			private function handleGlobalSubsetChange():void
			{
				if( !Weave.defaultSubsetKeyFilter.included.keys.length ) // IF SHOWING ALL RECORDS
					subsetSelectorBox.selectedIndex = -1;
			}
			private function updateSubsets():void
			{
				var list:Array = Weave.savedSubsetsKeyFilters.getObjects();
				if (includeAllRecordsOption.value)
					list.unshift(false);
				subsetSelectorBox.dataProvider = list;
				if( setToIndex >= 0 )
				{
					subsetSelectorBox.selectedIndex = setToIndex;
					setToIndex = -1;
				}
			}
			public function selectItem(name:String):void
			{
				setToIndex = Weave.savedSubsetsKeyFilters.getNames().indexOf(name);
			}
			/**
			 * Function used solely to get the label for each list item
			 **/
			private function subsetsListLabelFunction(item:Object):String
			{
				if (!item)
					return lang("All records");
				
				// get the subset by its name
				var keyFilter:KeyFilter = item as KeyFilter;
				
				// the label will be the name of the item followed by the included/excluded records count
				var name:String = Weave.savedSubsetsKeyFilters.getName(keyFilter);
				
				// create a string for the number of included keys followed by " records"
				// if the excluded keys are used instead, the string will be excluded keys followed by " excluded records"
				if (keyFilter.includeMissingKeys.value && keyFilter.excluded.keys.length > 0)
					return lang('{0} ({1} excluded records)', name, keyFilter.excluded.keys.length);
				else
					return lang('{0} ({1} records)', name, keyFilter.included.keys.length);
			}
			
			protected function itemSelected():void
			{
				if (subsetSelectorBox.selectedIndex == -1) return;
				
				var subset:KeyFilter = subsetSelectorBox.selectedItem as KeyFilter;
				if (subset)
					copySessionState(subset, Weave.defaultSubsetKeyFilter);
				else
					Weave.defaultSubsetKeyFilter.replaceKeys(true, true); // all records
				// reset the selected item to what it was before we set the global subset
				subsetSelectorBox.selectedItem = subset;
			}
		]]>
	</fx:Script>
	<ControlPanel>
		<s:NavigatorContent label="{lang('Settings')}">
			<s:layout>
				<s:VerticalLayout/>
			</s:layout>
			<s:CheckBox label='{lang("Include \"All records\" option")}' creationComplete="linkBindableProperty(includeAllRecordsOption, event.target, 'selected')"/>
		</s:NavigatorContent>
		<editors:WindowSettingsEditor target="{this}"/>
	</ControlPanel>
</DraggablePanel>
