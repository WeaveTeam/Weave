<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<!--	Session State Editor
   	*  @author skolman
    *  @author kmonico
 	*  A text editor to change the Session State on the fly.
-->
<ui:DraggablePanel
    xmlns:ui ="weave.ui.*"
    xmlns:mx="http://www.adobe.com/2006/mxml"
    width="800" height="600"
    title="Session State Editor"
	backgroundColor="#EFF4BC" 
    xmlns:sessioning="weave.core.*">

    <mx:VBox horizontalScrollPolicy="off" verticalScrollPolicy="off" width="100%" height="100%"
			 paddingBottom="5" paddingTop="5" paddingRight="5" paddingLeft="5">
		<mx:HBox width="100%">
			<mx:Button label="Reload" toolTip="Reloads to previous save state" click="refresh()" paddingLeft="5" paddingRight="5"/>
			<mx:Button label="Apply" click="handleApplyClick()" paddingLeft="5" paddingRight="5"/>
			<mx:Button label="Save &amp; Close" click="saveAndClosePanel()" paddingLeft="5" paddingRight="5"/>
			<mx:Button label="Cancel" click="removePanel()" paddingLeft="5" paddingRight="5"/>
		</mx:HBox>
		<mx:HDividedBox width="100%" height="100%">
			<mx:VBox id="listVBox" width="100%" height="100%">
				<!--<mx:Label paddingTop="5" fontWeight="bold" text="Object Hierarchy"/>-->
				<!--<CustomComboBox maxWidth="{listVBox.width}" id="sessionObjectTypeComboBox" toolTip="select filter to list by session object type" change="handleSessionObjectTypeFilterSelect(event)"/>-->
 				<ui:SessionNavigator id="sessionNav" height="100%" horizontalScrollPolicy="auto" width="100%"/>
 				<mx:HBox width="100%" id="linkControls" horizontalScrollPolicy="off">
 					<mx:TextInput id="linkNameInput" width="100%"/>
 					<mx:Button label="Link to global object" click="linkSelectedObject();"/>
 					<mx:Button label="Unlink" click="linkNameInput.text = ''; linkSelectedObject();"/>
 				</mx:HBox>
			</mx:VBox>
			<mx:VBox width="100%" height="100%" horizontalScrollPolicy="off">
				<mx:HBox width="100%" horizontalAlign="right">
					<ui:TextInputWithPrompt id="search" prompt="Enter search text" enter="highlightSearchText()" change="searchTermChanged()" width="100%" minWidth="100"/>
					<mx:Button label="Search" click="highlightSearchText()" repeatDelay="1"/>
				</mx:HBox>
				<mx:Canvas id="editorBox" width="100%" height="100%">
					<mx:TextArea id="sessionStateTextArea" wordWrap="false" enabled="false" focusIn="handleTextAreaFocusIn()" fontAntiAliasType="normal" fontSize="11" 
								 fontFamily="Courier New" change="handleTextAreaChange()" width="100%" height="100%" visible="true"/>
					<mx:DataGrid id="sessionStateDataGrid" wordWrap="false" enabled="true" editable="true" focusIn="handleDataGridFocusIn()" fontAntiAliasType="normal" fontSize="11"
								 fontFamily="Courier New" itemEditEnd="handleDataGridChange(event)" width="100%" height="100%" verticalScrollPolicy="auto" 
								 horizontalScrollPolicy="auto" visible="true" variableRowHeight="false" rowHeight="24" >
						<!--<mx:columns>
							<mx:DataGridColumn width="200" dataField="Name" headerText="Name" editable="false"/>
							<mx:DataGridColumn dataField="Value" headerText="Value"/>
						</mx:columns>-->
					</mx:DataGrid>
				</mx:Canvas>
				<mx:Button id="toggleView" label="Toggle View" click="handleToggleViewClick()" toolTip="Click this button to switch the view of the session state."/>
			</mx:VBox>
		</mx:HDividedBox>
	</mx:VBox>
	<mx:Script>
		<![CDATA[
			import com.as3xls.xls.formula.Functions;
			
			import mx.collections.ArrayCollection;
			import mx.containers.VBox;
			import mx.controls.Alert;
			import mx.controls.Label;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.core.Application;
			import mx.core.mx_internal;
			import mx.effects.Resize;
			import mx.events.CloseEvent;
			import mx.events.DataGridEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			import mx.utils.ObjectUtil;
			
			import weave.Weave;
			import weave.WeaveProperties;
			import weave.api.WeaveAPI;
			import weave.api.core.ILinkableDynamicObject;
			import weave.api.core.ILinkableHashMap;
			import weave.api.core.ILinkableObject;
			import weave.api.core.ILinkableVariable;
			import weave.api.data.IQualifiedKey;
			import weave.api.getCallbackCollection;
			import weave.api.getLinkableObjectOwner;
			import weave.api.getSessionState;
			import weave.api.linkBindableProperty;
			import weave.api.setSessionState;
			import weave.core.DynamicState;
			import weave.core.LinkableHashMap;
			import weave.core.LinkableString;
			import weave.core.SessionManager;
			import weave.core.WeaveXMLDecoder;
			import weave.core.WeaveXMLEncoder;
			import weave.core.weave_internal;
			import weave.ui.colormap.*;
			import weave.utils.DebugUtils;
			import weave.utils.EventUtils;
			import weave.utils.NumberUtils;
			import weave.utils.PopUpUtils;
			import weave.utils.VectorUtils;
			use namespace weave_internal;

		    private var selectors:Array = null;

			override protected function constructor():void
			{
				super.constructor();
				panelWidth.value = "75%";
				panelHeight.value = "75%";
				panelX.value = "12.5%";
				panelY.value = "12.5%";
			}			
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();

				refreshTimer.addEventListener(TimerEvent.TIMER, handleRefreshTimer);
				refreshTimer.start();
				
				//handle keyboard event CTRL +F to Search Box
				this.addEventListener(KeyboardEvent.KEY_DOWN, handleCtrlFPressEvent)

		    	EventUtils.addBindCallback(sessionNav, "selectedItem", handleSessionNavSelect);
		    	
				sessionNav.addEventListener(KeyboardEvent.KEY_UP, confirmDeleteSelectedItem);
				
				if (_editor == TEXT_EDITOR)
				{
					editorBox.removeChild(sessionStateDataGrid);
					editorBox.addChild(sessionStateTextArea);
					sessionStateTextArea.visible = true;
					sessionStateTextArea.enabled = true;
				}
				else 
				{
					editorBox.removeChild(sessionStateTextArea);
					editorBox.addChild(sessionStateDataGrid);
					sessionStateDataGrid.visible = true;
					sessionStateDataGrid.enabled = true;
				}
				
				_nameColumn.editable = false;
				_nameColumn.width = 192;
				loadSelectedSessionObjectInView();
				
		    }
			
			override public function dispose():void
			{
				refreshTimer.stop();
				EventUtils.removeBindCallback(sessionNav, "selectedItem", handleSessionNavSelect);
				super.dispose();
			}
		    
			
		    private var _needRefresh:Boolean = false;
		    private const refreshTimer:Timer = new Timer(200, 0);
			private var autoRefresh:Boolean = false;
		    private var _textModified:Boolean = false;
			
			private var _currentSelectedObject:ILinkableObject = null;
			private var _currentSessionStateXML:XML = null;
			private var _currentSessionStateDataProvider:ArrayCollection = null; 
			private var _prevSelectedObject:ILinkableObject = null;
			
			private var _textStartSearchIndex:int = 0;
			private var _gridStartSearchIndex:int = 0; 
			
			private function handleRefreshTimer(event:TimerEvent):void
		    {
		    	if (autoRefresh && _needRefresh && !_textModified && visible && parent)
				{
					autoRefresh = false; // this prevents text from refreshing every time -- we only want it to refresh when text area gains focus.
		    		loadSelectedSessionObjectInView();
				}
		    }
		    private function handleSelectedObjectGroupCallback():void
		    {
		    	_needRefresh = true;
		    }
			
			private function handleTextAreaFocusIn():void
			{
				autoRefresh = true;
			}
			
		    private function handleTextAreaChange():void
		    {
		    	_textModified = true;
		    }
		    
			public function refresh():void
			{
				if (_textModified == false)
				{
					sessionNav.refresh();
					return;
				}
				
				_textModified = false;
				var functionSave:Function = function():void { saveSessionState(); sessionNav.refresh(); };
				var functionDontSave:Function = function():void { loadSelectedSessionObjectInView(); sessionNav.refresh(); };
				PopUpUtils.confirm(
					this,
					"Save",
					"Session state has been changed.  Save Changes?",
					functionSave,
					functionDontSave
				);
			}

			private function loadSelectedSessionObjectInView(sessObj:ILinkableObject = null):void
			{
				if (sessionStateDataGrid.visible == false && sessionStateTextArea.visible == false)
					return;
				
				_needRefresh = false;

				if (sessionNav.selectedItem == null && sessObj == null)
				{
					clearAndDisableView();
					return;
				}

				enableView();
				if(sessObj != null)
					_currentSelectedObject = sessObj;
				else
					_currentSelectedObject = sessionNav.selectedItem.object;

				loadSessionStateInView();
				_textModified = false;
			}

		    private function linkSelectedObject():void
		    {
				var dynamicObject:ILinkableDynamicObject = sessionNav.getSelectedSessionedObject() as ILinkableDynamicObject;
		    	if (dynamicObject)
		    	{
		    		dynamicObject.globalName = linkNameInput.text.length > 0 ? linkNameInput.text : null;
		    		refresh();
		    	}
		    }
			private function handleSessionNavSelect():void
			{
				var dynamicObject:ILinkableDynamicObject = sessionNav.getSelectedSessionedObject() as ILinkableDynamicObject;
				if (dynamicObject)
				{
					linkNameInput.text = dynamicObject.globalName;
					linkControls.enabled = true;
				}
				else
				{
					linkNameInput.text = '';
					linkControls.enabled = false;
				}
				
				if(_textModified)
					refresh();
				else
					loadSelectedSessionObjectInView();
			}
			
		    private function alertError(e:Error):void
		    {
		    	PopUpUtils.confirm(
		    			this,
		    			e.name,
		    			e.message+"\n\n"+"Show stack trace?",
		    			function():void { Alert.show(e.message+"\n\n"+e.getStackTrace(), e.name); }
		    		);
		    }
		    
		    private function handleApplyClick():void
		    {
		    	try {
			    	saveSessionState();
			    	refresh();
				} catch (e:Error) { alertError(e); }
		    }

			private function highlightSearchText():Boolean
			{
				var i:int;
				var searchTerm:String = search.text.toLowerCase();
				if (_editor == TEXT_EDITOR)
				{
					var text:String = sessionStateTextArea.text.toLowerCase();
					// search through the text
					for (i = 0; i < 2; i++) // 
					{
						_textStartSearchIndex = text.indexOf(searchTerm, _textStartSearchIndex + 1); // +1 so it won't find the same thing twice 
						if (_textStartSearchIndex >= 0)
						{
							sessionStateTextArea.setSelection(_textStartSearchIndex, _textStartSearchIndex+searchTerm.length);
							sessionStateTextArea.setFocus();
							return true;
						}
						//end of file reached. start search again.
					}
				}
				else if (_editor == DATA_GRID)
				{
					// this loop is to make sure we wrap around to find it
					for (var j:int = 0; j < 2; ++j)
					{
						var rows:Array = _currentSessionStateDataProvider.source;
						
						for (i = _gridStartSearchIndex + 1; i < rows.length; ++i)
						{
							var row:Object = rows[i];
							for each (var string:String in row)
							{
								_gridStartSearchIndex = string.toLowerCase().indexOf(searchTerm);
								if (_gridStartSearchIndex >= 0)
								{
									_gridStartSearchIndex = i;
									sessionStateDataGrid.selectedIndex = i;
									sessionStateDataGrid.validateNow();
									sessionStateDataGrid.scrollToIndex(i);
									return true;
								}
							}
						}
					}
				}
				
				Alert.show("Not found: "+searchTerm, "Search");
				return false;
			}
			
			private function searchTermChanged():void
			{
				_textStartSearchIndex = -1;
				_gridStartSearchIndex = -1;
			}
			
			private function handleCtrlFPressEvent(event:KeyboardEvent):void
			{
				if (event.ctrlKey && event.keyCode == 70)
				{
					search.setFocus();
				}
			}
			
			private function setLabelName(item:Object):String
			{
				var node:XML = XML(item);
				
				if(node.@name.toString() != "")
					return node.@name.toString();
				else
					return node.name().toString();
			}
			private function confirmDeleteSelectedItem(event:KeyboardEvent):void
			{
				if(event.keyCode == 46)
				{
					var parent:ILinkableObject = getLinkableObjectOwner(sessionNav.selectedItem.object);
					if (!parent)
						return;
					var hashMap:ILinkableHashMap = parent as ILinkableHashMap;
					var dynamicObject:ILinkableDynamicObject = parent as ILinkableDynamicObject;
					if (hashMap || dynamicObject)
						Alert.show("Delete Selected Item?","Confirm Delete",mx.controls.Alert.YES| mx.controls.Alert.NO,this,deleteSelectedItem)
				}
			}
			
			private function deleteSelectedItem(event:CloseEvent):void
			{
				if(event.detail == Alert.YES)
				{
					var parent:ILinkableObject = getLinkableObjectOwner(sessionNav.selectedItem.object);
					if (!parent)
						return;
					var hashMap:ILinkableHashMap = parent as ILinkableHashMap;
					var dynamicObject:ILinkableDynamicObject = parent as ILinkableDynamicObject;
					
					var oldObject:ILinkableObject = sessionNav.selectedItem.object;
					if (hashMap)
						hashMap.removeObject(hashMap.getName(oldObject));
					else if (dynamicObject)
						dynamicObject.removeObject();
					
					if ((WeaveAPI.SessionManager as SessionManager).objectWasDisposed(oldObject))
						refresh();
					else
						Alert.show("Object is locked and cannot be deleted.", "Delete Failed");
				}
			}

			private function handleDataGridFocusIn():void
			{
				autoRefresh = true;
			}
			
			private function handleDataGridChange(event:DataGridEvent):void
			{
				var row:int = int(event.rowIndex);
				var element:String = sessionStateDataGrid.dataProvider.getItemAt(row)["Name"];
				var oldValue:String = sessionStateDataGrid.dataProvider.getItemAt(row)["Value"];
				var newValue:String = sessionStateDataGrid.itemEditorInstance[sessionStateDataGrid.columns[event.columnIndex].editorDataField];
				newValue = newValue.replace(/(\r\n)|\r/gm, "\n");
				sessionStateDataGrid.itemEditorInstance[sessionStateDataGrid.columns[event.columnIndex].editorDataField] = newValue;

				if( oldValue != newValue ) 
					_textModified = true;
			}

			private function handleToggleViewClick():void
			{
				var oldValue:String = _editor;
				if (oldValue == TEXT_EDITOR)
				{
					//toggleView.label = "Text Editor View";
					_editor = DATA_GRID;
					editorBox.removeChild(sessionStateTextArea);
					editorBox.addChild(sessionStateDataGrid);
					
					sessionStateTextArea.enabled = false;
					sessionStateTextArea.editable = false;
					sessionStateTextArea.visible = false;

					sessionStateDataGrid.enabled = true;
					sessionStateDataGrid.editable = true;
					sessionStateDataGrid.visible = true;
					

				}
				else if (oldValue == DATA_GRID)
				{
					//toggleView.label = "Data Grid View";
					_editor = TEXT_EDITOR;
					editorBox.removeChild(sessionStateDataGrid);
					editorBox.addChild(sessionStateTextArea);
					
					sessionStateTextArea.enabled = true;
					sessionStateTextArea.editable = true;
					sessionStateTextArea.visible = true;

					sessionStateDataGrid.enabled = false;
					sessionStateDataGrid.editable = false;
					sessionStateDataGrid.visible = false;
				}
				loadSelectedSessionObjectInView();
			}
			
			private function clearAndDisableView():void
			{
				if (_editor == TEXT_EDITOR)
				{
					sessionStateTextArea.text = '';
					sessionStateTextArea.enabled = false;
				}
				else if (_editor == DATA_GRID)
				{
					sessionStateDataGrid.dataProvider = [ {Name: '', Value: ' '} ];
					sessionStateDataGrid.enabled = false;
				}
			}
			private function enableView():void
			{
				if (_editor == TEXT_EDITOR)
				{
					sessionStateTextArea.text = ' ';
					sessionStateTextArea.enabled = true;
					sessionStateTextArea.visible = true;
				}
				else if (_editor == DATA_GRID)
				{
					sessionStateDataGrid.dataProvider = [ {Name: ' ', Value: ' '} ];
					sessionStateDataGrid.enabled = true;
					sessionStateDataGrid.visible = true;
				}
			}
			
			private var _editor:String = DATA_GRID;
			private const _nameColumn:DataGridColumn = new DataGridColumn("Name");
			private const _valueColumn:DataGridColumn = new DataGridColumn("Value");
			public static const TEXT_EDITOR:String = WeaveProperties.TEXT_EDITOR;
			public static const DATA_GRID:String = WeaveProperties.DATA_GRID;
			private function loadSessionStateInView():void
			{
				var sessionState:Object = _currentSelectedObject ? getSessionState(_currentSelectedObject) : null;
				if (_prevSelectedObject != _currentSelectedObject)
				{
					if (_prevSelectedObject)
						getCallbackCollection(_prevSelectedObject).removeCallback(handleSelectedObjectGroupCallback);
					_prevSelectedObject = _currentSelectedObject;
					if (_currentSelectedObject)
						getCallbackCollection(_currentSelectedObject).addGroupedCallback(this, handleSelectedObjectGroupCallback);
					hsp = 0;
					vsp = 0;
				}

				if (_editor == TEXT_EDITOR)
				{
					var vsp:Number = sessionStateTextArea.verticalScrollPosition;
					var hsp:Number = sessionStateTextArea.horizontalScrollPosition;

					_currentSessionStateXML = WeaveXMLEncoder.encode(sessionState,"Tag");
					sessionStateTextArea.text = _currentSessionStateXML.children().toXMLString();
					sessionStateTextArea.enabled = true;
					sessionStateTextArea.validateNow();
					sessionStateTextArea.verticalScrollPosition = vsp;
					sessionStateTextArea.horizontalScrollPosition = hsp;
				}
				else if (_editor == DATA_GRID)
				{
					var i:int;
					
					// if the object is a single ILinkableVariable
					if (_currentSelectedObject is ILinkableVariable)
					{
						var value:Object = (_currentSelectedObject as ILinkableVariable).getSessionState();
						if (value === null || (value as String) === '') // strict equality check to avoid casting 0, false, or '' to values of 0 (equal to null)
							value = ' ';
						sessionStateDataGrid.columns = [ _valueColumn ];
						sessionStateDataGrid.dataProvider = [ { Value: value.toString() } ];

						sessionStateDataGrid.variableRowHeight = true;
						sessionStateDataGrid.wordWrap = false;
						
						return;
					} 

					var tempValue:Object;
					var dataGridProvider:ArrayCollection = new ArrayCollection();
					var childObject:ILinkableObject;
					var ignoreList:Dictionary = new Dictionary(true);
					var names:Array;
					var sessionedObjects:Array;
					// check for LinkableHashMap (Array of DynamicState objects)
					if (_currentSelectedObject is ILinkableHashMap)
					{
						names = (_currentSelectedObject as ILinkableHashMap).getNames();
						sessionedObjects = (_currentSelectedObject as ILinkableHashMap).getObjects();
					
						for (i = 0; i < names.length; ++i)
						{
							childObject = sessionedObjects[i] as ILinkableObject;
							if (childObject is ILinkableVariable && (WeaveAPI.SessionManager as SessionManager).isChildInSessionState(_currentSelectedObject, childObject))
							{
								if (ignoreList[childObject] != undefined)
									continue;
								ignoreList[childObject] = true;
								
								tempValue = getSessionState(childObject);
								if (tempValue === null || (tempValue as String) === '')
									tempValue = ' ';
								dataGridProvider.addItem( {Name: names[i], Value: tempValue.toString()} );
							}
						}
					}
					else // an object mapping
					{
						if (_currentSelectedObject is ILinkableDynamicObject)
						{
							names = (_currentSelectedObject as ILinkableDynamicObject).globalName ? [] : ["internalObject"];
						}
						else if (_currentSelectedObject)
						{
							names = (WeaveAPI.SessionManager as SessionManager).getLinkablePropertyNames(_currentSelectedObject);
						}
						for (i = 0; i < names.length; i++)
						{
							childObject = _currentSelectedObject[names[i]];
							if (!childObject)
								continue;
							if (childObject is ILinkableVariable &&(WeaveAPI.SessionManager as SessionManager).isChildInSessionState(_currentSelectedObject, childObject))
							{
								// don't include duplicate siblings
								if (ignoreList[childObject] != undefined)
									continue;
								ignoreList[childObject] = true;
								
								tempValue = getSessionState(childObject);
								dataGridProvider.addItem( {Name: names[i], Value: tempValue } );
							}
						}
					}

					_currentSessionStateDataProvider = dataGridProvider;
					sessionStateDataGrid.columns = [ _nameColumn, _valueColumn ];
					sessionStateDataGrid.dataProvider = dataGridProvider;
					sessionStateDataGrid.enabled = true;
					sessionStateDataGrid.editable = true;
					sessionStateDataGrid.variableRowHeight = false;
					sessionStateDataGrid.wordWrap = false;
					sessionStateDataGrid.validateNow();
				}
			}
			
			private function saveSessionState():void
		    {
				_textModified = false;
				
				if (_currentSelectedObject != null)
				{
					if (_editor == TEXT_EDITOR && _currentSessionStateXML != null)
					{
						_currentSessionStateXML.setChildren(XMLList(sessionStateTextArea.text));
						
						var decodedStateObject:Object = WeaveXMLDecoder.decode(_currentSessionStateXML);
						
						setSessionState(_currentSelectedObject, decodedStateObject, true);
					}
					else if (_editor == DATA_GRID && _currentSessionStateDataProvider != null)
					{
						var newSessionState:Object;
						var i:int, j:int;

						_currentSessionStateDataProvider = sessionStateDataGrid.dataProvider as ArrayCollection;
						
						// special case of just one object to set
						if (_currentSelectedObject is ILinkableVariable && _currentSessionStateDataProvider.length == 1)
						{
							newSessionState = _currentSessionStateDataProvider[0].Value;
							setSessionState(_currentSelectedObject, newSessionState, true);
							return;
						}
						
						var oldSessionState:Object = _currentSelectedObject ? getSessionState(_currentSelectedObject) : null;
						// special case of DynamicState objects
						if (_currentSelectedObject is ILinkableHashMap) 
						{
							newSessionState = (oldSessionState as Array).concat(); // make a copy and copy over the changes
							for (j = 0; j < _currentSessionStateDataProvider.length; ++j)
							{
								var lookupName:String = _currentSessionStateDataProvider[j].Name;
								for (i = 0; i < newSessionState.length; ++i)
								{
									if ((newSessionState[i] as DynamicState).objectName == lookupName)
									{
										(newSessionState[i] as DynamicState).sessionState = WeaveXMLDecoder.decode(<tag>{_currentSessionStateDataProvider[j].value}</tag>);
										break;
									}
								}
							}

							setSessionState(_currentSelectedObject, newSessionState, false);
							return;
						}

						// general case of just an object mapping
						newSessionState = new Object();
						for (i = 0; i < _currentSessionStateDataProvider.length; ++i)
						{
							newSessionState[_currentSessionStateDataProvider[i].Name] = _currentSessionStateDataProvider[i].Value;
						}
						
						setSessionState(_currentSelectedObject, newSessionState, false); // do not remove any missing things
					}
				}
		    }

			private function saveAndClosePanel():void
		    {
		    	try {
					saveSessionState();
					super.removePanel();
				} catch (e:Error) { alertError(e); }
		    }
			
			override public function removePanel():void
			{
				if (_textModified)
				{
					PopUpUtils.confirm(
							this,
							"Save",
							"Session state has been changed.  Save Changes?",
							saveAndClosePanel,
							super.removePanel
						);
				}
				else
					super.removePanel();
			}
			
			private static const defaultEditor:SessionStateEditor = new SessionStateEditor();
			
			public static function openDefaultEditor(rootObject:ILinkableObject = null):void
			{
				if (!defaultEditor.parent)
					PopUpManager.addPopUp(defaultEditor, Application.application as Application);
				
				if (rootObject == null)
					rootObject = Weave.root;
				
				defaultEditor.sessionNav.rootObject = rootObject;
				//defaultEditor.refresh();
				defaultEditor.restorePanel();
			}
			
		]]>
	</mx:Script>
</ui:DraggablePanel>