<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->

<!--
LinkableObjectList

@skolman
-->

<mx:List xmlns="weave.ui.*" xmlns:mx="http://www.adobe.com/2006/mxml" 
		 dragEnabled="true"
		 dropEnabled="true"
		 dragMoveEnabled="true"
		 allowMultipleSelection="true"
		 showDataTips="false"
		 width="100%" 
		 labelFunction="setLabel"
		 backgroundAlpha="0.5" borderStyle="inset"
		 >
	
	
	<mx:Script>
		<![CDATA[
			import flash.utils.describeType;
			import flash.utils.getQualifiedClassName;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ICollectionView;
			import mx.controls.Alert;
			import mx.controls.Image;
			import mx.controls.List;
			import mx.controls.Text;
			import mx.controls.TextInput;
			import mx.core.DragSource;
			import mx.core.IUIComponent;
			import mx.core.SpriteAsset;
			import mx.core.UIComponent;
			import mx.events.DragEvent;
			import mx.managers.DragManager;
			import mx.utils.ObjectUtil;
			import mx.utils.StringUtil;
			
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.core.ILinkableDynamicObject;
			import weave.api.core.ILinkableHashMap;
			import weave.api.core.ILinkableObject;
			import weave.api.data.AttributeColumnMetadata;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IDataSource;
			import weave.api.data.IDynamicKeyFilter;
			import weave.api.getCallbackCollection;
			import weave.core.LinkableHashMap;
			import weave.core.SessionManager;
			import weave.core.weave_internal;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.ui.DraggablePanel;
			import weave.utils.ColumnUtils;
			
			// This is a pointer to an existing object map.  Do not create a new private one. 
			private var _hashMap:ILinkableHashMap = null; // <- do not create a new object map
			
			
			
			[Inspectable] public function set hashMap(value:ILinkableHashMap):void
			{
				if (_hashMap == value)
					return;
				// remove callback from previous object map
				if (_hashMap != null)
					getCallbackCollection(_hashMap).removeCallback(updateDataProvider);
					
				if (value != null)
					dynamicObject = null;

				// add callback to new object map	
				_hashMap = value;
				if (_hashMap != null)
					getCallbackCollection(_hashMap).addGroupedCallback(this, updateDataProvider);
				
				updateDataProvider();
			}
			public function get hashMap():ILinkableHashMap
			{
				if (_hashMap && (WeaveAPI.SessionManager as SessionManager).objectWasDisposed(_hashMap))
				{
					hashMap = null;
				}
				
				return _hashMap;
			}
			
			private var _dynamicObject:ILinkableDynamicObject = null;
			public function get dynamicObject():ILinkableDynamicObject
			{
				if (_dynamicObject && (WeaveAPI.SessionManager as SessionManager).objectWasDisposed(_dynamicObject))
				{
					dynamicObject = null;
				}
				
				return _dynamicObject;
			}
			[Inspectable] public function set dynamicObject(value:ILinkableDynamicObject):void
			{
				if (_dynamicObject == value)
					return;
				// remove callback from previous object map
				if (_dynamicObject != null)
					getCallbackCollection(_dynamicObject).addGroupedCallback(this, updateDataProvider);
				
				if (value != null)
					hashMap = null;
				
				// add callback to new object map	
				_dynamicObject = value;
				if (_dynamicObject != null)
					getCallbackCollection(_dynamicObject).addGroupedCallback(this, updateDataProvider);
				
				updateDataProvider();
				
				this.rowCount = 1;
				//this.maxHeight = 10;
			}
			
			private function updateDataProvider():void
			{
				if (dynamicObject)
				{
					dataProvider = dynamicObject.internalObject;
				}
				else if (hashMap)
				{
					dataProvider = hashMap.getObjects();
				}

				var view:ICollectionView = dataProvider as ICollectionView;
				if (view)
					view.refresh();
			}
			
			public function removeSelectedItem():void
			{
				if (hashMap && selectedIndex >= 0)
				{
					var names:Array = [];
					for (var i:int = 0; i < selectedIndices.length; i++)
					{
						var selectedIndex:int = selectedIndices[i];
						
						names.push(hashMap.getName(dataProvider[selectedIndex] as ILinkableObject) );
					}	
					
					for each(var name:String in names)
					{
						hashMap.removeObject(name);
					}
				}
				else if (dynamicObject)
				{
					dynamicObject.removeObject();
				}
			}
			
			// called when something is being dragged on top of this list
			override protected function dragOverHandler(event:DragEvent):void
			{
				super.dragOverHandler(event);
				
				DragManager.showFeedback(DragManager.MOVE);
			}
			
			// called when something is dropped into this list
			override protected function dragDropHandler(event:DragEvent):void
			{			
				//need to add re-order functionality				
				//if(event.dragInitiator == this)
				//super.dragDropHandler(event);
				
				//hides the drop visual lines
				event.currentTarget.hideDropFeedback(event);
				mx_internal::resetDragScrolling(); // if we don't do this, list will scroll when mouse moves even when not dragging something
				
				if(event.dragInitiator == this)
				{
					event.action = DragManager.MOVE;
					super.dragDropHandler(event);
					
					updateHashMapNameOrder();
				}
				else
				{
					var object:ILinkableObject;
					var items:Array = event.dragSource.dataForFormat("items") as Array;
					if (hashMap)
					{
						var prevNames:Array = hashMap.getNames();
						var newNames:Array = [];
						var dropIndex:int = calculateDropIndex(event);
						
						// copy each item in the list, in order
						for (var i:int = 0; i < items.length; i++)
						{
							object = items[i] as ILinkableObject;
							if (hashMap.getName(object) == null)
							{
								var newObject:ILinkableObject = hashMap.weave_internal::requestObjectCopy(null, object);
								newNames.push(hashMap.getName(newObject));
							}
						}
						
						// insert new names inside prev names list and save the new name order
						var args:Array = newNames;
						newNames.unshift(dropIndex, 0);
						prevNames.splice.apply(null, args);
						hashMap.setNameOrder(prevNames);
					}
					else if (dynamicObject && items.length > 0)
					{
						// only copy the first item in the list
						dynamicObject.weave_internal::requestLocalObjectCopy(items[0]);
					}
				}
			}
			
			// called when something is dragged on top of this list
			override protected function dragEnterHandler(event:DragEvent):void
			{
				if (event.dragSource.hasFormat("items"))
				{
					var items:Array = event.dragSource.dataForFormat("items") as Array;
					if (items[0] is ILinkableObject)
						DragManager.acceptDragDrop(event.currentTarget as IUIComponent);
				}
			}
			
			[Inspectable] public var defaultDragAction:String = DragManager.COPY;
			
			// called when something in this list is dragged and dropped somewhere else
			override protected function dragCompleteHandler(event:DragEvent):void
			{
				if (event.shiftKey)
					event.action = DragManager.MOVE;
				else if (event.ctrlKey)
					event.action = DragManager.COPY;
				else
					event.action = defaultDragAction;
				
				super.dragCompleteHandler(event);
				
				removeObjectsMissingFromDataProvider();
			}
			
			// generates a label for an item in the list
			public function setLabel(item:Object):String
			{
				var label:String = "";
				
				if(hashMap)
					label = hashMap.getName(item as ILinkableObject);
				if(dynamicObject)
					label = dynamicObject.globalName || getQualifiedClassName(item).split("::")[1];
				
				if (item is IAttributeColumn)
				{
					var col:IAttributeColumn = item as IAttributeColumn;
					var title:String = ColumnUtils.getTitle(col);
					var keyType:String = ColumnUtils.getKeyType(col);
					var dataType:String = ColumnUtils.getDataType(col);
					if (!dataType)
						return StringUtil.substitute("{0} (Key Type: {1})", title, keyType);
					return StringUtil.substitute("{0} ({1} -> {2})", title, keyType, dataType);
				}
				else
				{
					return label;
				}
			}
			
			// generates a tooltip for an item in the list
			private function setToolTip(item:Object):String
			{
				return setLabel(item);
			}
			
			// this function updates the sessioned name order based on what is in the data provider
			private function updateHashMapNameOrder():void
			{
				validateNow();
				
				if (hashMap)
				{
					// update object map name order based on what is in the data provider
					var newNameOrder:Array = [];
					for (var i:int = 0; i < dataProvider.length; i++)
					{
						var object:ILinkableObject = dataProvider[i] as ILinkableObject;
						if (object)
							newNameOrder[i] = hashMap.getName(object);
					}
					hashMap.setNameOrder(newNameOrder);
				}
			}
			// this function removes anything from the session state not appearing in the data provider
			private function removeObjectsMissingFromDataProvider():void
			{
				if (hashMap)
				{
					var objects:Array = hashMap.getObjects();
					for each (var object:ILinkableObject in objects)
					{
						if(!(this.dataProvider as ArrayCollection).contains(object))
							hashMap.removeObject(hashMap.getName(object));
					}
				}
				else if(dynamicObject)
				{
					if(!(this.dataProvider as ArrayCollection).contains(dynamicObject.internalObject))
						dynamicObject.removeObject();
				}
			}
			
//			private var _deleteShape:SpriteAsset = null;
//			override protected function createChildren():void
//			{
//				super.createChildren();
//				
//				_deleteShape = new SpriteAsset();
//				_deleteShape.addEventListener(MouseEvent.CLICK, minPanelSizeHandler);
//				super.addChild(_deleteShape);
//			}
//			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
//			{
//				super.updateDisplayList(unscaledWidth, unscaledHeight);
//				
//				_deleteShape.graphics.clear();
//				_deleteShape.graphics.drawRect(0,0, 50, 50);
//			}
		]]>
	</mx:Script>
</mx:List>
