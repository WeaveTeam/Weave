<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->


<!--
		This class creates a taskbar that sits at the bottom of a page to provide objects to access different options.  For example, tools could be
		minimized to go into this taskbar like in windows or a mac.  
-->
<s:SkinnableContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
 xmlns:s="library://ns.adobe.com/flex/spark" 
 xmlns:mx="library://ns.adobe.com/flex/mx"
		   implements="weave.api.core.ILinkableObject"
		   width="100%" height="0">
	<s:layout>
		<s:BasicLayout clipAndEnableScrolling="true"/>
	</s:layout>
	<s:BorderContainer id="hbox"
			 visible="false"
			 cornerRadius="5" 
			 filters="{[glow]}"
			 x="{(width - hbox.width) / 2}"
			 y="{-hbox.height}"
			 >
		<s:backgroundFill>
			<s:SolidColor color="0x808080" alpha="0.8"/>
		</s:backgroundFill>
		<s:borderStroke>
			<s:SolidColorStroke color="0x000000" weight="1"/>
 		</s:borderStroke>
		<s:layout>
			<s:HorizontalLayout verticalAlign="middle"  paddingLeft="10" paddingRight="10" paddingTop="2" paddingBottom="2"/>
		</s:layout>
	</s:BorderContainer>
	
	<fx:Script>
		<![CDATA[
			import mx.core.IVisualElementContainer;
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;
			
			import spark.filters.GlowFilter;
			
			import weave.Weave;
			import weave.api.newLinkableChild;
			import weave.api.reportError;
			import weave.api.core.ILinkableObject;
			import weave.core.LinkableBoolean;
			import weave.core.LinkableNumber;
			import weave.data.AttributeColumns.AbstractAttributeColumn;
			import weave.ui.MinimizedComponent;
			import weave.utils.SparkUtils;

			public const taskbarShrinkGrow:LinkableBoolean = newLinkableChild(this, LinkableBoolean);
			public const taskbarLargeIconSize:LinkableNumber = newLinkableChild(this, LinkableNumber, updateIconSizes);
			public const taskbarSmallIconSize:LinkableNumber = newLinkableChild(this, LinkableNumber, shrinkComponents);
			
			[Bindable]
			private var glow:GlowFilter = new GlowFilter(0xFFFFFF, 0.5, 3, 3, 5);
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				addEventListener(MouseEvent.ROLL_OVER, growComponents);
				addEventListener(MouseEvent.ROLL_OUT,  shrinkComponents);
				
				taskbarShrinkGrow.value = true;
				taskbarSmallIconSize.value = 10;
				taskbarLargeIconSize.value = 50;
				
				Weave.root.childListCallbacks.addImmediateCallback(this, handleLinkableChildListChanged);
			}
			
			// add a callback for when components that these minimized components represented are removed
			private function handleLinkableChildListChanged():void
			{
				var removed:ILinkableObject = Weave.root.childListCallbacks.lastObjectRemoved;
				if (!removed)
					return;
				
				var toolChildren:Array = SparkUtils.getAllElement(hbox);
				// go through each minimizedComponent in the taskbar
				for each (var minimizedComponent:MinimizedComponent in toolChildren)
				{
					// go through each component in the minimized component group (this will have to be modified later in case we start to group
					// tools together
					for each (var component:UIComponent in minimizedComponent.componentGroup)
					{
						// if the last object removed that caused this callback is in this component group, 
						// then remove the minimized component from the taskbar so you cant restore something that doesn't exist anymore
						if (component == removed)
						{
							hbox.removeElement(minimizedComponent);
							continue;
						}
					}
				}
			}
			
			private function updateIconSizes():void
			{
				var toolChildren:Array = SparkUtils.getAllElement(hbox);
				for each (var component:MinimizedComponent in toolChildren)
				{
					component.scaleX = 1;
					component.scaleY = 1;
					component.validateNow();
					component.updateMinimizedIcon(taskbarLargeIconSize.value, taskbarLargeIconSize.value);
				}
				
				shrinkComponents();
			}
				
			public function shrinkComponents(event:MouseEvent = null):void
			{
				if (taskbarShrinkGrow.value == false)
					return;
					
				removeEventListener(MouseEvent.ROLL_OUT,  shrinkComponents);				
				
				// limit the smallest size to 2 pixels, otherwise grow/shrink doesn't work
				var scale:Number = Math.max(2, taskbarSmallIconSize.value) / taskbarLargeIconSize.value;
				var toolChildren:Array = SparkUtils.getAllElement(hbox);
				for each (var component:MinimizedComponent in toolChildren)
				{
					component.scaleX = scale;
					component.scaleY = scale;
				}
				
				addEventListener(MouseEvent.ROLL_OUT,  shrinkComponents);
			}	
			public function growComponents(event:MouseEvent = null):void
			{
				if (taskbarShrinkGrow.value == false)
					return;
				var toolChildren:Array = SparkUtils.getAllElement(hbox);
				for each (var component:MinimizedComponent in toolChildren)
				{
					component.scaleX = 1;
					component.scaleY = 1;
				}
			}
			
			public function addMinimizedComponent(component:UIComponent, restoreFunction:Function):MinimizedComponent
			{
				var clickListener:Function = function(e:MouseEvent):void
				{
					removeMinimizedComponent(minimizedComponent);
					restoreFunction();
				};
				var minimizedComponent:MinimizedComponent = new MinimizedComponent(
					[component],
					taskbarLargeIconSize.value,
					taskbarLargeIconSize.value,
					clickListener
				);
				
				hbox.addElement(minimizedComponent);
				hbox.validateNow();
				validateNow();
				hbox.visible = hbox.numElements > 0;
				
				if (taskbarShrinkGrow.value)
					shrinkComponents();
				
				return minimizedComponent;
			}
			
			public function removeMinimizedComponent(minimizedComponent:MinimizedComponent):void
			{
				try
				{
					if (minimizedComponent.parent){
						if(minimizedComponent.parent is IVisualElementContainer){
							(minimizedComponent.parent as IVisualElementContainer).removeElement(minimizedComponent);
						}
						else{
							minimizedComponent.parent.removeChild(minimizedComponent);
						}
					}
					hbox.visible = hbox.numElements > 0;
				}
				catch (error:Error)
				{
					reportError(error);
				}
			}
			public function cleanMinimizedComponentImage(minimizedComponent:MinimizedComponent):void
			{
				try
				{
					minimizedComponent.graphics.clear();
				}
				catch (error:Error)
				{
					reportError(error);
				}
			}
			
			public function addMinimizedComponentGroup(components:Array, restoreFunctions:Array):MinimizedComponent
			{	
				var minimizedComponent:MinimizedComponent = new MinimizedComponent(
					components, 
					taskbarLargeIconSize.value, 
					taskbarLargeIconSize.value, 
					function(e:MouseEvent):void
					{
						removeMinimizedComponent(minimizedComponent);
						// restore all components
						for (var i:int = 0; i < components.length; i++)
							restoreFunctions[i]();
					}
				);
						
				hbox.addElement(minimizedComponent);
				hbox.validateNow();
				validateNow();
				hbox.visible = hbox.numChildren > 0;
				
				if (taskbarShrinkGrow.value)
					shrinkComponents();
								
				return minimizedComponent;
			}
			
			public function restoreAllComponents():void
			{
				var toolChildren:Array = SparkUtils.getAllElement(hbox);
				for each (var component:MinimizedComponent in toolChildren)
					component.clickListener(null);
			}
			
			private static var _instance:VisTaskbar = null;
			public static function get instance():VisTaskbar
			{
				if (_instance == null)
				{
					_instance = new VisTaskbar();
					_instance.initialize();
				}
				return _instance;
			}
		]]>
	</fx:Script>
</s:SkinnableContainer>
