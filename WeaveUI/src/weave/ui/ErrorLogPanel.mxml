<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<DraggablePanel xmlns:mx="http://www.adobe.com/2006/mxml"
		xmlns="weave.ui.*" 
		borderColor="#C5C3A8"
		title="Error Log"
		>
	<Console id="console" context="{WeaveAPI.ErrorManager}" padding="0"/>
<!--
	<mx:List id="list" variableRowHeight="true"  width="100%" height="100%">
		<mx:itemRenderer>
			<mx:Component>
			<mx:Box dataChange="handleDataChange();">
				<mx:TextArea id="textArea" width="100%"
					editable="false"
					verticalScrollPolicy="off"
					/>
			<mx:Script>
				<![CDATA[
					private function handleDataChange():void
					{
						textArea.text = (data as Error).name + ': ' + (data as Error).message;
					}
				]]>
			</mx:Script>
			</mx:Box>
			</mx:Component>
		</mx:itemRenderer>
	</mx:List>
-->
	<mx:Script>
		<![CDATA[
			import mx.core.Application;
			import mx.managers.PopUpManager;
			import mx.rpc.Fault;
			
			import weave.api.WeaveAPI;
			import weave.api.getCallbackCollection;
			import weave.utils.DebugUtils;
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				
				pinnable.value = false;
				getCallbackCollection(WeaveAPI.ErrorManager).addGroupedCallback(this, handleError, true);
			}
			
			private var errorIndex:int = 0;
			private function handleError():void
			{
				while (errorIndex < WeaveAPI.ErrorManager.errors.length)
				{
					if (errorIndex > 0)
						console.log.text += '\r\r';
					
					var error:Error = WeaveAPI.ErrorManager.errors[errorIndex++];
					if (error is Fault)
					{
						var f:Fault = error as Fault;
						var errorString:String = '';
						if (f.faultDetail && [IOErrorEvent.IO_ERROR, SecurityErrorEvent.SECURITY_ERROR].indexOf(f.faultCode) >= 0)
						{
							errorString = f.faultDetail;
						}
						else
						{
							errorString = f.faultCode;
							if (f.faultString)
								errorString += ': ' + f.faultString;
							if (f.faultDetail)
								errorString += ': ' + f.faultDetail;
							if (errorString == 'Error')
								errorString = "Communication error";
						}
						
						console.log.text += errorString;
					}
					else if (Capabilities.isDebugger)
					{
						// get partial stack trace
						console.log.text += error.message + '\rStack trace: ' + DebugUtils.getCompactStackTrace(error).join('; ');
					}
					else
					{
						console.log.text += error.toString();
					}
				}
				console.log.validateProperties();
				console.log.verticalScrollPosition = console.log.maxVerticalScrollPosition;
			}
			
			private static var _instance:ErrorLogPanel;
			public static function getInstance():ErrorLogPanel
			{
				if (!_instance)
					_instance = new ErrorLogPanel();
				return _instance;
			}
			public static function openErrorLog():void
			{
				var instance:ErrorLogPanel = getInstance();
				if (!instance.parent)
				{
					var app:Application = Application.application as Application;
					var desiredHeight:Number = 150;
					var percentHeight:Number = Math.round(desiredHeight / app.stage.stageHeight * 100);
					
					instance.panelY.value = (100 - percentHeight) + "%";
					instance.panelHeight.value = percentHeight + "%";
					
					instance.panelX.value = "0%";
					instance.panelWidth.value = "100%";
					
					PopUpManager.addPopUp(instance, Application.application as Application);
				}
				instance.restorePanel();
			}
		]]>
	</mx:Script>
</DraggablePanel>
