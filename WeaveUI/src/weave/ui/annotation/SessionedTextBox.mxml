<?xml version="1.0" encoding="utf-8"?>
<!--
/*
Weave (Web-based Analysis and Visualization Environment)
Copyright (C) 2008-2011 University of Massachusetts Lowell

This file is a part of Weave.

Weave is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License, Version 3,
as published by the Free Software Foundation.

Weave is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<ui:DraggablePanel xmlns="weave.ui.annotation.*"
				   xmlns:fx="http://ns.adobe.com/mxml/2009" 
				   xmlns:s="library://ns.adobe.com/flex/spark" 
				   xmlns:mx="library://ns.adobe.com/flex/mx"
				   xmlns:ui="weave.ui.*"
				   width="25%" height="25%" 
				   layout="{new BasicLayout()}" backgroundAlpha="0"  >
	
	<fx:Style>
		
 .weave-panel-style{
			
			borderThickness: 0;
		}
	</fx:Style>
	
	<mx:TextArea id="textArea" width="100%" height="100%" minWidth="5" minHeight="5"
				 borderStyle="solid" editable="false" wordWrap="true"/>
	<ui:ControlPanel added="if (event.target == controlPanel) updateTextArea()"
					 width="360" height="260"
					 removed="if (event.target == controlPanel) callLater(updateTextArea)"
					 sessionPanelCoordsAsPercentages="false" title="{lang('Annotation Editor')}">
		<s:VGroup id="toolbar" width="100%" height="100%" gap="7"
				  paddingBottom="2" paddingLeft="2" paddingRight="2"
				  paddingTop="2" >
			
			<s:HGroup>
				<s:CheckBox id="borderCheckBox" label="{lang('Show window borders')}"/>
				<s:CheckBox id="moveResizeCheckBox" label="{lang('Allow moving and resizing')}"/>
			</s:HGroup>
			
			<s:HGroup width="100%"  paddingBottom="2" paddingLeft="2"
					  paddingRight="2" paddingTop="2" >
				<ui:FontControl id="fontControls" />
			</s:HGroup>
			
			<mx:Grid>
				<mx:GridRow>
					<mx:GridItem>
						<s:Label text="{lang('Paragraph style:')}"/>
					</mx:GridItem>
					<mx:GridItem>
						<ui:CustomComboBox id="paragraphCombo" change="applyParagraphFormat(event);"
										   dataProvider="{[lang('Left align'),lang('Right align'),lang('Center'),lang('Justify'),lang('Bullet')]}"
										   selectedIndex="0"/>
					</mx:GridItem>
				</mx:GridRow>
				<mx:GridRow>
					<mx:GridItem>
						<s:Label id="roundingLabel" text="{lang('Corner rounding:')}"/>
					</mx:GridItem>
					<mx:GridItem>
						<mx:HSlider id="cornerRadiusSlider" width="100%" liveDragging="true"
									maximum="30" minimum="0" snapInterval="1"
									toolTip="{lang('Change the editing text area\'s corner rounding.')}"
									value="0"/>
					</mx:GridItem>
				</mx:GridRow>
				<mx:GridRow>
					<mx:GridItem>
						<s:Label id="paddingLabel" text="{lang('Padding around text:')}"/>
					</mx:GridItem>
					<mx:GridItem>
						<mx:HSlider id="paddingSlider" width="100%" liveDragging="true" maximum="25"
									minimum="0" snapInterval="1"
									toolTip="{lang('Set the space between the text and the text box.')}"
									value="0"/>
					</mx:GridItem>
				</mx:GridRow>
				<mx:GridRow>
					<mx:GridItem>
						<s:Label id="backgroundLabel" text="{lang('Background properties:')}"/>
					</mx:GridItem>
					<mx:GridItem>
						<s:HGroup width="100%">
							<mx:ColorPicker id="backgroundColorPicker" selectedColor="white"
											toolTip="{lang('Change the editing text area\'s background color.')}"/>
							<mx:HSlider id="backgroundAlphaSlider" width="100%" liveDragging="true"
										maximum="1" minimum="0"
										toolTip="{lang('Change the editing text area\'s background transparency.  A value of 1 (slider to the right) is fully opaque, 0 (slider to the left) is fully transparent.')}"
										value="1.0"/>
						</s:HGroup>
					</mx:GridItem>
				</mx:GridRow>
				<mx:GridRow>
					<mx:GridItem>
						<s:Label id="borderLabel" text="{lang('Border properties:')}"/>
					</mx:GridItem>
					<mx:GridItem>
						<s:HGroup width="100%">
							<mx:ColorPicker id="borderColorPicker"
											enabled="{borderThicknessSlider.value &gt; 0}"
											toolTip="{lang('Change the editing text area\'s border color.')}"/>
							
							<mx:HSlider id="borderThicknessSlider"
										value="1" minimum="0" maximum="25" snapInterval="1" 
										toolTip = "{lang('Change the editing text area\'s border thickness.')}"
										width="100%" liveDragging="true"/>
						</s:HGroup>
					</mx:GridItem>
				</mx:GridRow>
			</mx:Grid>
		</s:VGroup>
	</ui:ControlPanel>
	<fx:Script>
		<![CDATA[
			import mx.binding.utils.BindingUtils;
			import mx.core.IUITextField;
			import mx.core.mx_internal;
			import mx.events.ColorPickerEvent;
			import mx.events.ListEvent;
			import mx.events.StateChangeEvent;
			
			import spark.layouts.BasicLayout;
			
			import weave.Weave;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.newDisposableChild;
			import weave.api.registerLinkableChild;
			import weave.api.reportError;
			import weave.api.ui.ILinkableContainer;
			import weave.core.LinkableFunction;
			import weave.core.LinkableNumber;
			import weave.core.LinkableString;
			import weave.ui.CustomContextMenuManager;
			
			public const htmlText:LinkableString        = registerLinkableChild(this, new LinkableString(''), handleLinkableHtmlText, true);
			public const backgroundAlpha:LinkableNumber = registerLinkableChild(this, new LinkableNumber(1, isFinite), applyStyle, true);
			public const backgroundColor:LinkableNumber = registerLinkableChild(this, new LinkableNumber(0xFFFFFF, isFinite), applyStyle, true);
			public const cornerRadius:LinkableNumber    = registerLinkableChild(this, new LinkableNumber(0, isFinite), applyStyle, true);
			public const borderColor:LinkableNumber     = registerLinkableChild(this, new LinkableNumber(0x000000, isFinite), applyStyle, true);
			public const borderThickness:LinkableNumber = registerLinkableChild(this, new LinkableNumber(1.0, isFinite), applyStyle, true);
			public const padding:LinkableNumber         = registerLinkableChild(this, new LinkableNumber(10, isFinite), applyStyle, true);
			
			private const htmlTextFunction:LinkableFunction = newDisposableChild(this, LinkableFunction);
			
			override protected function constructor():void
			{
				super.constructor();
				
				minimizable.value = false;
				maximizable.value = false;
				enableBorders.addImmediateCallback(this, updateBorders);
			}
			
			override protected function childrenCreated():void
			{
				fontControls.initialize();
				super.childrenCreated();
				
				linkBindableProperty(enableBorders, borderCheckBox, 'selected');
				linkBindableProperty(enableMoveResize, moveResizeCheckBox, 'selected');
				
				linkBindableProperty(backgroundAlpha, backgroundAlphaSlider, "value");
				linkBindableProperty(backgroundColor, backgroundColorPicker, "selectedColor");
				linkBindableProperty(cornerRadius,    cornerRadiusSlider, "value" );
				linkBindableProperty(borderColor,     borderColorPicker, "selectedColor");
				linkBindableProperty(borderThickness, borderThicknessSlider, "value");
				linkBindableProperty(padding,         paddingSlider, "value");
				BindingUtils.bindSetter(handleTextAreaChange, textArea, 'htmlText');
				getCallbackCollection(Weave.root).addGroupedCallback(this, handleWeaveRootChange);
				
				getTextField().alwaysShowSelection = true;
				textArea.setStyle("focusAlpha", 0);
				getTextField().embedFonts = true;
				textArea.setStyle("embedFonts", true);
				
				fontControls.fontFamilyCombo.selectedLabel = getTextField().defaultTextFormat.font;
				fontControls.fontSizeCombo.selectedItem = getTextField().defaultTextFormat.size;
				fontControls.colorPicker.addEventListener(ColorPickerEvent.CHANGE, applyTextFormat);
				fontControls.fontFamilyCombo.addEventListener(ListEvent.CHANGE, applyTextFormat);
				fontControls.fontSizeCombo.addEventListener(ListEvent.CHANGE, applyTextFormat);
				fontControls.boldButton.addEventListener(Event.CHANGE, applyTextFormat);
				fontControls.italicButton.addEventListener(Event.CHANGE, applyTextFormat);
				fontControls.underlineButton.addEventListener(Event.CHANGE, applyTextFormat);
				
				applyStyle();
			}
			
			private function handleTextAreaChange(value:String):void
			{
				// we only want to save the session state if the control panel is open (the user is editing the text).
				if (controlPanel.parent)
					htmlText.value = value;
			}
			
			private function handleLinkableHtmlText():void
			{
				if (!parent)
				{
					callLater(handleLinkableHtmlText);
					return;
				}
				
				// use internal text field because the real _tempTextField loses formatting.
				var _tempTextField:IUITextField = textArea.mx_internal::getTextField();
				
				if (!_tempTextField)
				{
					reportError("Unexpected: textArea.mx_internal::getTextField() is null");
					return;
				}
				
				var selBegin:int = _tempTextField.selectionBeginIndex;
				var selEnd:int = _tempTextField.selectionEndIndex;
				
				_tempTextField.htmlText = htmlText.value; // use the textField temporarily for getTextFormat,setTextFormat
				
				// make sure the text between each pair of { and } brackets only has a single format.
				var begin:int;
				var end:int = 0;
				while (true)
				{
					// find a pair of { and } brackets
					begin = _tempTextField.text.indexOf("{", end);
					if (begin < 0)
						break;
					end = _tempTextField.text.indexOf("}", begin);
					if (end < 0)
						break;
					
					// make sure the text between begin & end only has a single format
					//				 	var f:TextFormat = _tempTextField.getTextFormat(begin, end + 1);
					//					_tempTextField.setTextFormat(f, begin, end + 1);
					_tempTextField.setSelection(begin, end + 1);
					_tempTextField.replaceSelectedText(_tempTextField.text.substring(begin, end + 1));
					
					//					// replace the text from { to }
					//					var script:String = _tempTextField.text.substring(begin + 1, end);
					//					htmlTextFunction.value = script;
					//					_tempTextField.replaceText(begin, end + 1, String(htmlTextFunction.apply()));
				}
				_tempTextField.setSelection(selBegin, selEnd);
				
				var ht:String = _tempTextField.htmlText;
				ht = ht.split('&apos;').join("'");
				ht = ht.split('&quot;').join('"');
				htmlTextFunction.value = '`' + ht.split('`').join('\\`') + '`';
				
				//This is a hack to keep re-applying the currently selected text format.
				applyTextFormat(new Event("LIST"));
				
				updateTextArea();
			}
			
			/**
			 * This function gets called when anything in Weave changes so that inline code within brackets can be re-evaluated.
			 */
			private function handleWeaveRootChange():void
			{
				// while the control panel is closed, update the text area in case inline code needs to be re-evaluated
				if (!controlPanel.parent)
					updateTextArea();
			}
			
			private function updateTextArea():void
			{
				if (!parent)
				{
					callLater(updateTextArea);
					return;
				}
				
				// if control panel is closed, evaluate inline code
				if (!controlPanel.parent)
				{
					textArea.editable = false;
					try
					{
						textArea.htmlText = htmlTextFunction.apply();
					}
					catch (e:Error)
					{
						reportError(e);
						textArea.htmlText = htmlText.value; // show literally what the user has typed
					}
				}
				else
				{
					textArea.editable = true;
					textArea.htmlText = htmlText.value; // show literally what the user has typed
				}
			}
			
			private function applyStyle():void
			{
				if (!parent)
					return;
				textArea.setStyle("backgroundAlpha", backgroundAlpha.value);
				textArea.setStyle("backgroundColor", backgroundColor.value);
				textArea.setStyle("cornerRadius", cornerRadius.value);
				textArea.setStyle("paddingLeft", padding.value);
				textArea.setStyle("paddingRight", padding.value);
				textArea.setStyle("borderColor", borderColor.value);
				textArea.setStyle("borderThickness", borderThickness.value);
			}
			
			/**
			 * This function is called when the user presses the tool button.
			 */
			override public function toggleControlPanel():void
			{
				super.toggleControlPanel();
			}
			
			private function getTextField():TextField
			{
				return textArea.mx_internal::getTextField();
			}
			
			private function applyParagraphFormat(e:Event):void
			{
				if (false)
				{
					//TODO: fix this buggy code
					
					// Apply the paragraph styles to the whole paragraph instead of just the selected text
					var beginIndex:int = getTextField().selectionBeginIndex;
					var endIndex:int = getTextField().selectionEndIndex;
					
					beginIndex = getTextField().getFirstCharInParagraph(beginIndex) - 1;
					beginIndex = Math.max(0, beginIndex);
					endIndex = getTextField().getFirstCharInParagraph(endIndex);
					if (endIndex >= 0)
						endIndex += getTextField().getParagraphLength(endIndex) - 1;
					else
						endIndex = beginIndex + getTextField().getParagraphLength(beginIndex);
					applyTextFormat(e, beginIndex, endIndex);
				}
				else
				{
					applyTextFormat(e, 0, getTextField().length);
				}
			}
			
			private function applyTextFormat(e:Event, beginIndex:int = -1, endIndex:int = -1):void
			{
				// save selection & scroll position
				var selectionBeginIndex:int = getTextField().selectionBeginIndex;
				var selectionEndIndex:int = getTextField().selectionEndIndex;
				var vsp:int = textArea.verticalScrollPosition;
				if (beginIndex < 0)
					beginIndex = selectionBeginIndex;
				if (endIndex < 0)
					endIndex = selectionEndIndex;
				
				// get text format from gui
				var f:TextFormat = new TextFormat();
				f.font = fontControls.fontFamilyCombo.selectedLabel;
				f.size = fontControls.fontSizeCombo.selectedItem;
				f.color = fontControls.colorPicker.selectedColor;
				f.bold = fontControls.boldButton.selected;
				f.italic = fontControls.italicButton.selected;
				f.underline = fontControls.underlineButton.selected;
				if (e.target == paragraphCombo)
				{
					if (paragraphCombo.selectedIndex == 4)
					{
						f.bullet = true;
					}
					else
					{
						f.bullet = false;
						f.align = ['left','right','center','justify'][paragraphCombo.selectedIndex];
					}
				}
				
				// set text format using temporary text field
				if( beginIndex == getTextField().caretIndex && endIndex == getTextField().caretIndex )
				{
					getTextField().defaultTextFormat = f;
				}
				else
				{
					getTextField().setTextFormat(f, beginIndex, endIndex);
				}
				/* 				if (beginIndex >= 0 && beginIndex < getTextField().length)
				{
				// compute new htmlText using temporary text field
				_tempTextField.htmlText = textArea.htmlText;
				// extra newline may get removed from htmlText, so make sure endIndex <= length
				endIndex = Math.min(endIndex, _tempTextField.length);
				if (beginIndex < endIndex)
				_tempTextField.setTextFormat(f, beginIndex, endIndex);
				htmlText.value = _tempTextField.htmlText;
				} */
				
				textArea.setFocus();
				textArea.validateNow();
				
				// restore selection & scroll position
				selectionEndIndex = Math.min(selectionEndIndex, getTextField().length);
				getTextField().setSelection(selectionBeginIndex, selectionEndIndex);
				textArea.verticalScrollPosition = vsp;
			}
			
			private static const _tempTextField:TextField = new TextField(); // temporary reusable object
			
			
			
			/*************************************************
			 *                static section                 *
			 *************************************************/
			
			
			
			/**
			 * This function is called whenever the context menu is opened.
			 * The function will change the caption displayed depending upon where the context menu was opened.
			 */
			private static var _textAreaEditContextMenuItem:ContextMenuItem 	= null;
			private static var _textAreaAddRemoveContextMenuItem:ContextMenuItem = null;
			private static const ADD_CAPTION:String           = lang("Add Annotation");
			private static const REMOVE_CAPTION:String        = lang("Remove Annotation");
			private static const EDIT_TEXT_CAPTION:String     = lang("Edit Annotation");
			private static const fontSizeArray:Array = [8,9,10,11,12,14,16,18,20,22,24,26,28,36,48,72];
			private static const fontFamilyArray:Array = ['_sans','_serif','_typewriter','Arial','Courier','Courier New', 'Geneva', 'Georgia', 'Helvetica', 'Times New Roman', 'Times', 'Verdana'];
			
			private static function handleContextMenuOpened(e:ContextMenuEvent):void
			{	
				var textArea:SessionedTextBox = getTextAreaFromTarget(e.mouseTarget);	
				
				if( getLinkableContainer(e.mouseTarget) )
				{
					_textAreaAddRemoveContextMenuItem.enabled = true;
				}
				else
				{
					_textAreaAddRemoveContextMenuItem.enabled = false;
				}
				
				if ( textArea == null)
				{ 
					_textAreaAddRemoveContextMenuItem.caption = ADD_CAPTION;
					_textAreaEditContextMenuItem.enabled = false;
				}
				else
				{
					_textAreaAddRemoveContextMenuItem.caption = REMOVE_CAPTION;
					_textAreaEditContextMenuItem.enabled = true;
				}
			}
			
			public static function createContextMenuItems(destination:DisplayObject):Boolean
			{
				if(!destination.hasOwnProperty("contextMenu") )
					return false;
				
				
				// Add a listener to this destination context menu for when it is opened
				var contextMenu:ContextMenu = destination["contextMenu"] as ContextMenu;
				contextMenu.addEventListener(ContextMenuEvent.MENU_SELECT, handleContextMenuOpened);
				
				// Create a context menu item for printing of a single tool with title and logo
				_textAreaAddRemoveContextMenuItem   = CustomContextMenuManager.createAndAddMenuItemToDestination(ADD_CAPTION,destination,handleAddRemove,"2.1 textBoxMenuItem");
				_textAreaEditContextMenuItem        = CustomContextMenuManager.createAndAddMenuItemToDestination(EDIT_TEXT_CAPTION,destination,handleEdit,"2.1 textBoxMenuItem");
				
				return true;
			}
			
			
			/**
			 * This function is called by ChangeWindows.
			 * This will add a Text Box to whatever parent has been indicated.
			 * If the container passed is not an ILinkableContainer the Text Box is automatically placed on the visdesktop.
			 */
			private static function addTextAreaAndLabelToLocation(container:*, x:Number=0, y:Number=0):void
			{	
				var linkableContainer:ILinkableContainer = container as ILinkableContainer;
				
				if ( linkableContainer )
				{
					var name:String = linkableContainer.getLinkableChildren().generateUniqueName("Label");
					var newTextArea:SessionedTextBox = linkableContainer.getLinkableChildren().requestObject(name, SessionedTextBox,false);
					newTextArea.toggleControlPanel();
				}
			}
			
			/**
			 * This function is called when Remove is selected in the Context Menu.
			 * It checks for a TextArea, and if found will attempt to remove the TextArea and associated TextBox. 
			 */
			private static function removeWindows(e:ContextMenuEvent):void
			{
				var parent:ILinkableContainer = getLinkableContainer(getTextAreaFromTarget(e.mouseTarget));
				
				if ( getTextAreaFromTarget(e.mouseTarget) != null ) {				
					if ( parent is ILinkableContainer ) {
						var textArea:SessionedTextBox = getTextAreaFromTarget(e.mouseTarget);
						
						var name:String = parent.getLinkableChildren().getName(textArea);
						parent.getLinkableChildren().removeObject(name);
					}
				}
			}
			/**
			 * This function is passed a target and checks to see if the target was a TextBox.
			 * Either a TextBox or null will be returned.
			 */
			private static function getTextAreaFromTarget(target:*):SessionedTextBox
			{
				var targetComponent:* = target;
				
				while(targetComponent)
				{
					if(targetComponent is SessionedTextBox)
						return targetComponent as SessionedTextBox;
					targetComponent = targetComponent.parent;
				}
				
				return targetComponent;
			}
			
			/**
			 * This function is passed a target and checks to see if the target is an ILinkableContainer.
			 * Either a ILinkableContainer or null will be returned.
			 */
			private static function getLinkableContainer(target:*):*
			{
				var targetComponent:* = target;
				
				while(targetComponent)
				{
					if(targetComponent is ILinkableContainer)
						return targetComponent as ILinkableContainer;
					
					targetComponent = targetComponent.parent;
				}
				
				return targetComponent;
			}
			
			/**
			 * This function handles what occurs when an option in the context menu involving the Text Box tool is selected.
			 * It is linked to the Text Box tool options by the createContextMenuItems function.
			 */
			private static function handleAddRemove(e:ContextMenuEvent):void
			{
				var parent:ILinkableContainer = getLinkableContainer(e.mouseTarget);
				var textArea:SessionedTextBox = getTextAreaFromTarget(e.mouseTarget);
				
				// if the user did not right click on a TextBox or the editor for one
				if ( textArea == null) 
				{
					// add text area to this container
					addTextAreaAndLabelToLocation(parent, e.mouseTarget.mouseX, e.mouseTarget.mouseY);
				}
				else
				{
					textArea.removePanel();
				}
			}
			
			private static function handleEdit(e:ContextMenuEvent):void
			{
				var textArea:SessionedTextBox = getTextAreaFromTarget(e.mouseTarget);
				if (textArea)
				{
					textArea.sendWindowToForeground();
					textArea.toggleControlPanel();
				}
			}
		]]>
	</fx:Script>
</ui:DraggablePanel>
