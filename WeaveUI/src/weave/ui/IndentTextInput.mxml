<?xml version="1.0" encoding="utf-8"?>
<!--
/* ***** BEGIN LICENSE BLOCK *****
 *
 * This file is part of Weave.
 *
 * The Initial Developer of Weave is the Institute for Visualization
 * and Perception Research at the University of Massachusetts Lowell.
 * Portions created by the Initial Developer are Copyright (C) 2008-2015
 * the Initial Developer. All Rights Reserved.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this file,
 * You can obtain one at http://mozilla.org/MPL/2.0/.
 * 
 * ***** END LICENSE BLOCK ***** */
-->
<Indent
    xmlns:mx="http://www.adobe.com/2006/mxml"
    xmlns="weave.ui.*"
    >
    <TextInputWithPrompt id="input" prompt="{prompt}" text="{text}" change="handleInputChange()" displayAsPassword="{displayAsPassword}" width="100%"/>
	
	<mx:Metadata>
		[Event(name="change", type="flash.events.Event")]
	</mx:Metadata>
    <mx:Script>
    	<![CDATA[
			[Bindable] public var prompt:String = "";
    		[Bindable] public var text:String = "";
    		[Bindable] public var displayAsPassword:Boolean = false;
			
			private function handleInputChange():void
			{
				text = input.text;
				dispatchEvent(new Event(Event.CHANGE));
			}

			[PercentProxy("inputPercentWidth")]
			public function set inputWidth(value:Number):void
			{
				setInputProperty('width', value);
			}
			public function set inputPercentWidth(value:Number):void
			{
				setInputProperty('percentWidth', value);
			}

			// this function will delay setting values until input is created.
			private function setInputProperty(propertyName:String, value:Object):void
			{
				if (input)
					input[propertyName] = value;
				else
					_pendingInputProperties[propertyName] = value;
			}
			private var _pendingInputProperties:Object = {};
			
			// this function will set values that were delayed by setInputProperty
			override protected function createChildren():void
			{
				super.createChildren();
				for (var prop:String in _pendingInputProperties)
					input[prop] = _pendingInputProperties[prop];
			}
    	]]>
    </mx:Script>
</Indent>
