<?xml version="1.0" encoding="utf-8"?>
<!--
/* ***** BEGIN LICENSE BLOCK *****
 *
 * This file is part of Weave.
 *
 * The Initial Developer of Weave is the Institute for Visualization
 * and Perception Research at the University of Massachusetts Lowell.
 * Portions created by the Initial Developer are Copyright (C) 2008-2015
 * the Initial Developer. All Rights Reserved.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this file,
 * You can obtain one at http://mozilla.org/MPL/2.0/.
 * 
 * ***** END LICENSE BLOCK ***** */
-->
<mx:TitleWindow
    xmlns="weave.ui.*"
    xmlns:mx="http://www.adobe.com/2006/mxml"
    showCloseButton="false"
    close="dispose()"
    borderAlpha="0.95"
    borderThickness="2"
	autoLayout="true"
    horizontalScrollPolicy="off" verticalScrollPolicy="off"
	titleStyleName="weave-panel-title-style"
	styleName="weave-panel-style"
	width="400"
	height="300"
	initialize="UIUtils.pad(this, 10);"
	keyDown="if (event.keyCode == Keyboard.ESCAPE) dispose();"
	>
	<WeaveTree id="tree" width="100%" height="100%"
			   doubleClickEnabled="true"
			   doubleClick="if (tree.selectedItem) commitSelection()"/>
	<mx:HBox id="alertButtons" width="100%">
		<mx:Spacer width="100%"/>
		<mx:Button label="{lang('Use selected hierarchy')}" click="commitSelection()" enabled="{tree.selectedItem}"/>
		<mx:Button label="{lang('Cancel')}" click="dispose()" minWidth="64"/>
	</mx:HBox>
	
    <mx:Script>
	<![CDATA[
		import mx.managers.PopUpManager;
		
		import weave.api.data.EntityType;
		import weave.api.disposeObject;
		import weave.api.registerLinkableChild;
		import weave.compiler.StandardLib;
		import weave.core.UIUtils;
		import weave.data.hierarchy.EntityNode;
		import weave.services.EntityCache;
		import weave.services.WeaveDataServlet;
		
		override protected function createChildren():void
		{
			super.createChildren();
			service = registerLinkableChild(tree, new WeaveDataServlet(url));
			cache = registerLinkableChild(tree, new EntityCache(service));
			tree.rootNode = new EntityNode(cache, EntityType.HIERARCHY);
		}
		
		override public function move(x:Number, y:Number):void
		{
			x = StandardLib.constrain(x, 0, parent.width - this.width);
			y = StandardLib.constrain(y, 0, parent.height - this.height);
			
			super.move(x, y);
		}
		
		public var url:String;
		private var service:WeaveDataServlet;
		private var cache:EntityCache;
		public var selectionHandler:Function = null;
		
		public function commitSelection():void
		{
			if (selectionHandler != null)
				selectionHandler((tree.selectedItem as EntityNode).id);
			dispose();
		}
		public function dispose():void
		{
			PopUpManager.removePopUp(this);
			if (lastInstance == this)
				lastInstance = null;
			disposeObject(tree);
			selectionHandler = null;
			service = null;
			cache = null;
		}
		
		public static var lastInstance:Object;
		
		/**
		 * Pops up an instance of WeaveDataSourceHierarchySelector and calls selectionHandler(id:int) when the user commits to an ID.
		 */
		public static function show(url:String, selectionHandler:Function):WeaveDataSourceHierarchySelector
		{
			var instance:WeaveDataSourceHierarchySelector = new WeaveDataSourceHierarchySelector();
			instance.title = lang('Select a hierarchy');
			instance.url = url;
			instance.selectionHandler = selectionHandler;
			
			PopUpManager.addPopUp(instance, WeaveAPI.topLevelApplication as DisplayObject, true);
			PopUpManager.centerPopUp(instance);
			
			lastInstance = instance;
			return instance;
		}
	]]>
	</mx:Script>
</mx:TitleWindow> 
