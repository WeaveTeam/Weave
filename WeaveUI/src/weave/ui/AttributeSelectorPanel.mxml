<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->

<ControlPanel
	xmlns="weave.ui.*"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	title="Attribute Selector"
	verticalScrollPolicy="off" horizontalScrollPolicy="off"
	backgroundColor="#EFF4BC" xmlns:editors="weave.editors.*"
	>
	<mx:VBox width="100%" height="100%" id="outerVBox" verticalGap="5">
		<mx:HBox id="paramSelector" verticalAlign="middle">
			<mx:Label text="Parameter to modify:"/>
			<CustomToggleButtonBar
				id="dimensionTypeLinks"
				width="100%"
				useHandCursor="true"
				dataProvider="{selectableAttributeNames}"
				itemClick="linkToObject(selectableAttributes[event.index]);"/>
		</mx:HBox>
		<mx:VDividedBox width="100%" height="100%" id="verticalDividedBox">
			<editors:DynamicColumnEditor id="editor" width="100%" height="100%"/>
			<mx:HBox id="addColumnControls" width="100%">
				<mx:Spacer width="100%"/>
				<mx:Button label="Add selected" click="addSelected()" enabled="{editor.explorer.fileView.selectedItem}"/>
			</mx:HBox>
			<ColumnListComponent id="columnList" width="100%" height="100%"/>
		</mx:VDividedBox>
	</mx:VBox>
	<mx:Script>
		<![CDATA[
			import mx.controls.Label;
			
			import weave.api.copySessionState;
			import weave.api.core.ILinkableHashMap;
			import weave.api.core.ILinkableObject;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IColumnWrapper;
			import weave.api.detectLinkableObjectChange;
			import weave.api.getCallbackCollection;
			import weave.api.getLinkableOwner;
			import weave.api.registerLinkableChild;
			import weave.api.ui.IObjectWithSelectableAttributes;
			import weave.compiler.StandardLib;
			import weave.core.LinkableWatcher;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.ReferencedColumn;
			import weave.utils.ColumnUtils;
			
			override protected function constructor():void
			{
				super.constructor();
				
				minimizable.value = true;
				maximizable.value = true;
				
				closeable.value = true;
				closeable.lock();
			}
			
			override public function reposition():void
			{
				panelX.value = '' + int(22 + Math.random() * 6) + "%";
				panelY.value = '' + int(17 + Math.random() * 6) + "%";
				panelWidth.value = "50%";
				panelHeight.value = "60%";
			}
			
			override protected function createChildren():void
			{
				if (createdChildren)
					return;
				super.createChildren();

				registerLinkableChild(this, editor);
				registerLinkableChild(this, columnList);
				columnList.titleHBox.addChild(addColumnControls);
				getCallbackCollection(this).addImmediateCallback(this, invalidateDisplayList);
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				columnList.selectorControls.parent.removeChild(columnList.selectorControls);
			}
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				if (detectLinkableObjectChange(updateDisplayList, toolWatcher))
					handleTool();
				
				super.updateDisplayList(unscaledWidth, unscaledHeight);
			}
			
			override public function dispose():void
			{
				super.dispose();
			}
			
			private function addSelected():void
			{
				var columns:Array = editor.addMultipleColumns(columnList.hashMap, true);
				if (columns.length)
					columnList.beginEditVariableName(columns[0]);
			}
			
			private var selectableAttributes:Array = [];
			[Bindable] private var selectableAttributeNames:Array = [];
			
			private const toolWatcher:LinkableWatcher = registerLinkableChild(this, new LinkableWatcher(IObjectWithSelectableAttributes), handleToolImmediately);
			
			private function handleToolImmediately():void
			{
				if (!toolWatcher.target)
					title = DEFAULT_TITLE;
			}
			
			private function handleTool():void
			{
				if (!parent)
					toolWatcher.target = null;
				
				var tool:IObjectWithSelectableAttributes = toolWatcher.target as IObjectWithSelectableAttributes;
				if (tool)
				{
					var attrs:Array = tool.getSelectableAttributes();
					var names:Array = tool.getSelectableAttributeNames();
					
					if (StandardLib.arrayCompare(attrs, selectableAttributes))
						selectableAttributes = attrs;
					
					if (StandardLib.arrayCompare(names, selectableAttributeNames))
						selectableAttributeNames = names;
					
					var index:int = attrs.indexOf(_linkedObject);
					if (index < 0)
						linkToObject(attrs[0]);
					dimensionTypeLinks.selectedIndex = Math.max(0, index);
					
					title = ControlPanel.getControlPanelTitle(tool, TITLE_FORMAT);
				}
				else
				{
					selectableAttributes = [];
					selectableAttributeNames = [];
				}
				
				paramSelector.visible = paramSelector.includeInLayout = !!tool;
				columnList.visible = columnList.includeInLayout = !!columnList.hashMap;
			}
			
			private static const DEFAULT_TITLE:String = "Attribute selector";
			private static const TITLE_FORMAT:String = DEFAULT_TITLE + " for {0}";
			
			/**
			 * The last object that was passed in to linkToObject()
			 */
			private var _linkedObject:ILinkableObject = null;
			public function linkToObject(columnOrHashMap:ILinkableObject, objectTitle:String = null):void
			{
				_linkedObject = columnOrHashMap;
				
				editor.setTarget(ColumnUtils.hack_findInternalDynamicColumn(columnOrHashMap as IColumnWrapper));
				columnList.hashMap = columnOrHashMap as ILinkableHashMap;
				
				editor.explorer.fileView.allowMultipleSelection = !!columnList.hashMap;
				columnList.visible = columnList.includeInLayout = !!columnList.hashMap;

				var tool:IObjectWithSelectableAttributes = getVisTool(columnOrHashMap);
				toolWatcher.target = tool;
				// make sure we call handleTool() later
				getCallbackCollection(toolWatcher).triggerCallbacks();
				if (tool)
				{
					targets = tool.getSelectableAttributes();
				}
				else
				{
					targets = columnOrHashMap ? [columnOrHashMap] : [];
					
					if (objectTitle)
						title = lang(TITLE_FORMAT, objectTitle);
					else
						title = lang(DEFAULT_TITLE);
				}
				
				if (columnList.hashMap)
				{
					// copy the session state from the first ReferencedColumn we find.
					var columns:Array = columnList.hashMap.getObjects(IAttributeColumn);
					for each (var column:IAttributeColumn in columns)
					{
						if (column is DynamicColumn)
							column = (column as DynamicColumn).getInternalColumn();
						if (column is ReferencedColumn)
						{
							copySessionState(column, editor.referencedColumn);
							break;
						}
					}
					
					columnList.label = objectTitle ? objectTitle : lang('Selected attributes');
				}
			}
			
			override public function removePanel():void
			{
				editor.setTarget(null);
				super.removePanel();
			}
			
			///////////////////////////////////////////////////////////////////////
			
			public static function open(columnOrHashMap:ILinkableObject = null, title:String = null, showVariableNames:Boolean = false):AttributeSelectorPanel
			{
				var instance:AttributeSelectorPanel = DraggablePanel.openStaticInstance(AttributeSelectorPanel);
				
				instance.linkToObject(columnOrHashMap, title);
				instance.columnList.showVariableNames = showVariableNames;
				
				instance.reposition();
				instance.copyCoordinatesFromSessionedProperties();
				return instance;
			}
			public static function getVisTool(selectableAttribute:ILinkableObject, evenIfNotInSelectableAttributes:Boolean = false):IObjectWithSelectableAttributes
			{
				var owner:ILinkableObject = getLinkableOwner(selectableAttribute);
				while (owner && !(owner is IObjectWithSelectableAttributes))
					owner = getLinkableOwner(owner);
				
				var tool:IObjectWithSelectableAttributes = owner as IObjectWithSelectableAttributes;

				if (evenIfNotInSelectableAttributes)
					return tool;
				
				if (tool && tool.getSelectableAttributes().indexOf(selectableAttribute) >= 0)
					return tool;
				
				return null;
			}
		]]>
	</mx:Script>
</ControlPanel>
