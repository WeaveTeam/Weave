<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->

<ControlPanel
	xmlns="weave.ui.*"
	xmlns:mx="http://www.adobe.com/2006/mxml"
	title="Attribute Selector"
	verticalScrollPolicy="off" horizontalScrollPolicy="off"
	backgroundColor="#EFF4BC" xmlns:editors="weave.editors.*"
	>
	<mx:VBox width="100%" height="100%" id="outerVBox" verticalGap="5">
		<mx:HBox id="paramSelector" verticalAlign="middle">
			<mx:Label text="Parameter to modify:"/>
			<CustomToggleButtonBar
				id="dimensionTypeLinks"
				width="100%"
				useHandCursor="true"
				dataProvider="{selectableAttributeNames}"
				itemClick="linkToObject(selectableAttributes[event.index]);"/>
		</mx:HBox>
		<mx:VDividedBox width="100%" height="100%" id="verticalDividedBox">
			<editors:DynamicColumnEditor id="editor" width="100%" height="100%"/>
			<mx:HBox id="addColumnControls" width="100%">
				<mx:Spacer width="100%"/>
				<mx:Button label="Add selected" click="addSelected()" enabled="{editor.explorer.fileView.selectedItem}"/>
			</mx:HBox>
			<ColumnListComponent id="columnList" width="100%" height="100%"/>
		</mx:VDividedBox>
	</mx:VBox>
	<mx:Script>
		<![CDATA[
			import mx.collections.ListCollectionView;
			import mx.controls.Label;
			import mx.managers.PopUpManager;
			
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.copySessionState;
			import weave.api.core.ILinkableHashMap;
			import weave.api.core.ILinkableObject;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IColumnWrapper;
			import weave.api.getCallbackCollection;
			import weave.api.getLinkableOwner;
			import weave.api.registerLinkableChild;
			import weave.api.ui.IVisToolWithSelectableAttributes;
			import weave.compiler.StandardLib;
			import weave.core.LinkableWatcher;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.ReferencedColumn;
			import weave.data.ColumnReferences.HierarchyColumnReference;
			import weave.utils.ColumnUtils;
			import weave.utils.HierarchyUtils;
			
			override protected function constructor():void
			{
				super.constructor();
				
				panelWidth.value = "50%";
				panelHeight.value = "60%";
				panelX.value = "25%";
				panelY.value = "15%";
				minimizable.value = true;
				maximizable.value = true;
				
				closeable.value = true;
				closeable.lock();
			}
			
			override protected function createChildren():void
			{
				if (createdChildren)
					return;
				super.createChildren();

				registerLinkableChild(this, editor);
				registerLinkableChild(this, columnList);
				columnList.titleHBox.addChild(addColumnControls);
			}
			
			override public function dispose():void
			{
				super.dispose();
			}
			
			private function addSelected():void
			{
				var columns:Array = editor.addMultipleColumns(columnList.hashMap, true);
				if (columns.length)
					columnList.beginEditVariableName(columns[0]);
			}
			
			private var selectableAttributes:Array = [];
			[Bindable] private var selectableAttributeNames:Array = [];
			
			private const toolWatcher:LinkableWatcher = registerLinkableChild(this, new LinkableWatcher(IVisToolWithSelectableAttributes), handleTool, true);
			
			private function handleTool():void
			{
				if (!parent)
					toolWatcher.target = null;
				
				var tool:IVisToolWithSelectableAttributes = toolWatcher.target as IVisToolWithSelectableAttributes;
				if (tool)
				{
					var selectedIndex:int = dimensionTypeLinks.selectedIndex;
					var attrs:Array = tool.getSelectableAttributes();
					var names:Array = tool.getSelectableAttributeNames();
					
					// do nothing if nothing changed
					if (!StandardLib.arrayCompare(attrs, selectableAttributes)
						&& !StandardLib.arrayCompare(names, selectableAttributeNames))
						return;
						
					selectableAttributes = attrs;
					selectableAttributeNames = names;
					title = ControlPanel.getControlPanelTitle(tool, TITLE_FORMAT);
					if (selectedIndex >= 0)
						linkToObject(selectableAttributes[selectedIndex]);
				}
				else
				{
					selectableAttributes = [];
					selectableAttributeNames = [];
				}
				showOrHideParamSelector(tool != null);
				
				if (!columnList.hashMap && verticalDividedBox == columnList.parent)
					verticalDividedBox.removeChild(columnList);
			}
			
			private function showOrHideParamSelector(show:Boolean):void
			{
				if (show && !paramSelector.parent)
					outerVBox.addChildAt(paramSelector,0);
				if (!show && paramSelector.parent)
					outerVBox.removeChild(paramSelector);
			}
			
			private static const DEFAULT_TITLE:String = "Attribute selector";
			private static const TITLE_FORMAT:String = DEFAULT_TITLE + " for {0}";
			
			public function linkToObject(columnOrHashMap:ILinkableObject, objectTitle:String = null):void
			{
				editor.setTarget(ColumnUtils.hack_findInternalDynamicColumn(columnOrHashMap as IColumnWrapper));

				var tool:IVisToolWithSelectableAttributes = getVisTool(columnOrHashMap);
				toolWatcher.target = tool;
				handleTool();
				if (tool)
				{
					targets = tool.getSelectableAttributes();
					
					dimensionTypeLinks.selectedIndex = tool.getSelectableAttributes().indexOf(columnOrHashMap);
				}
				else
				{
					targets = columnOrHashMap ? [columnOrHashMap] : [];
					
					if (objectTitle)
						title = lang(TITLE_FORMAT, objectTitle);
					else
						title = lang(DEFAULT_TITLE);
				}
				
				editor.explorer.fileView.allowMultipleSelection = columnOrHashMap is ILinkableHashMap;
				if (columnOrHashMap is ILinkableHashMap)
				{
					// copy the session state from the first ReferencedColumn we find.
					var columns:Array = (columnOrHashMap as ILinkableHashMap).getObjects(IAttributeColumn);
					for each (var column:IAttributeColumn in columns)
					{
						if (column is DynamicColumn)
							column = (column as DynamicColumn).getInternalColumn();
						if (column is ReferencedColumn)
						{
							copySessionState(column, editor.referencedColumn);
							break;
						}
					}
					
					var child:DisplayObject = columnList.selectorControls;
					if (child.parent)
						child.parent.removeChild(child);
					columnList.hashMap = columnOrHashMap as ILinkableHashMap;
					columnList.label = objectTitle ? objectTitle : lang('Selected attributes');
					verticalDividedBox.addChild(columnList);
				}
				else if (verticalDividedBox == columnList.parent)
				{
					verticalDividedBox.removeChild(columnList);
				}
				
				_instance.editor.clearSearch();
			}
			
			///////////////////////////////////////////////////////////////////////
			
			public static function get instance():AttributeSelectorPanel
			{
				if (!_instance)
					_instance = new AttributeSelectorPanel();
				return _instance;
			}
			public static var _instance:AttributeSelectorPanel;
			public static function open(columnOrHashMap:ILinkableObject = null, title:String = null, showVariableNames:Boolean = false):void
			{
				if (!instance.parent)
					PopUpManager.addPopUp(_instance, WeaveAPI.topLevelApplication as DisplayObject);
				
				_instance.linkToObject(columnOrHashMap, title);
				_instance.columnList.showVariableNames = showVariableNames;
				
				if (!_instance.parent)
					PopUpManager.addPopUp(_instance, WeaveAPI.topLevelApplication as DisplayObject);
				_instance.restorePanel();
			}
			public static function getVisTool(selectableAttribute:ILinkableObject, evenIfNotInSelectableAttributes:Boolean = false):IVisToolWithSelectableAttributes
			{
				var owner:ILinkableObject = getLinkableOwner(selectableAttribute);
				while (owner && !(owner is IVisToolWithSelectableAttributes))
					owner = getLinkableOwner(owner);
				
				var tool:IVisToolWithSelectableAttributes = owner as IVisToolWithSelectableAttributes;

				if (evenIfNotInSelectableAttributes)
					return tool;
				
				if (tool && tool.getSelectableAttributes().indexOf(selectableAttribute) >= 0)
					return tool;
				
				return null;
			}
		]]>
	</mx:Script>
</ControlPanel>
