<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<mx:VBox 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns="weave.editors.*"
	verticalScrollPolicy="off"
	horizontalScrollPolicy="off"
	implements="weave.api.ui.IDataSourceEditor"
	creationPolicy="all" xmlns:ui="weave.ui.*"
	>
	<mx:Label id="editorTitle" fontWeight="bold" text="{lang((_wfsDataSource ? 'Edit' : 'Add') + ' WFS Data Source')}"/>
	<ui:Indent label="{lang('Source Name *')}">
		<mx:TextInput id="sourceName" width="100%"/>
	</ui:Indent>
	<ui:Indent label="{lang('Source URL *')}">
		<ui:TextInputWithPrompt id="url" width="100%" prompt="http://www.example.com/geoserver/wfs"/>
	</ui:Indent>
	<mx:CheckBox label="Swap X and Y coordinate order" id="swapXY"/>
	<mx:Spacer height="20"/>
	<mx:Label text="{lang('* = required')}"/>
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
			
			import weave.api.core.ILinkableObject;
			import weave.api.data.IDataSource;
			import weave.compiler.StandardLib;
			import weave.data.DataSources.WFSDataSource;
			
			override protected function createChildren():void
			{
				super.createChildren();
				
				sourceName.text = WeaveAPI.globalHashMap.generateUniqueName("WFSDataSource");
			}
			
			[Bindable] private var _wfsDataSource:WFSDataSource = null;
			public function setTarget(object:ILinkableObject):void
			{
				_wfsDataSource = object as WFSDataSource;
				
				if (!parent)
				{
					callLater(setTarget, arguments);
					return;
				}
				
				if (_wfsDataSource)
				{
					sourceName.text = WeaveAPI.globalHashMap.getName(_wfsDataSource);
					url.text = _wfsDataSource.url.value;
					swapXY.selected = _wfsDataSource.swapXY.value;
					sourceName.editable = false;
					sourceName.toolTip = lang("Source name is not editable once added to the session. If needed, delete the source and add it again with the new name.");
				}
			}
			
			public function hasPendingChanges():Boolean
			{
				return StandardLib.asString(_wfsDataSource.url.value) != StandardLib.asString(url.text)
					|| _wfsDataSource.swapXY.value != swapXY.selected;
			}
			
			public function applyChanges():void
			{
				_wfsDataSource.url.value = url.text;
				_wfsDataSource.swapXY.value = swapXY.selected;
			}
			
			public function createDataSource():IDataSource
			{
				if (!sourceName.text || !url.text)
				{
					Alert.show(lang("Please fill all required fields"), lang("Error"));
					return null;
				}
				if (WeaveAPI.globalHashMap.getObject(sourceName.text))
				{
					Alert.show(lang("Source Name already being used. Please give a different name"), lang("Error"));
					return null;
				}
				_wfsDataSource = WeaveAPI.globalHashMap.requestObject(sourceName.text, WFSDataSource, false);
				if (!_wfsDataSource)
				{
					Alert.show(lang("Unexpected problem. Unable to add the data source."));
					return null;
				}
				applyChanges();
				return _wfsDataSource;
			}
		]]>
	</mx:Script>
</mx:VBox>