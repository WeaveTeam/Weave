<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
	     xmlns:mx="http://www.adobe.com/2006/mxml"
	     xmlns:ui="weave.ui.*"
	     layout="absolute" xmlns:userControls="weave.ui.userControls.*">
	<mx:Script>
		<![CDATA[
			import mx.events.ListEvent;
			
			import weave.api.WeaveAPI;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.registerLinkableChild;
			import weave.api.reportError;
			import weave.api.ui.IVisTool;
			import weave.api.unlinkSessionState;
			import weave.core.LinkableBoolean;
			import weave.core.LinkableString;
			import weave.data.AttributeColumns.BinnedColumn;
			import weave.data.AttributeColumns.ColorColumn;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.FilteredColumn;
			import weave.data.AttributeColumns.ReferencedColumn;
			import weave.ui.AttributeSelectorPanel;
			import weave.utils.ColumnUtils;
			import weave.utils.LinkableTextFormat;
			import weave.utils.ProbeTextUtils;
			import weave.visualization.plotters.ColorBinLegendPlotter;

			WeaveAPI.registerImplementation(IVisTool, ColorBinLegendTool, "Color Legend");
			
			private var _plotter:ColorBinLegendPlotter = null;
			public const useUnobtrusiveToolTips:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(false), boxUnchecked);
			public const toolTipLocation:LinkableString = registerLinkableChild(this, new LinkableString(ProbeTextUtils.CENTER_WINDOW));
			
			override protected function constructor():void
			{
				super.constructor();
				// lock dynamic objects into place
				_plotter = initializePlotters(ColorBinLegendPlotter, false);

				plotLayer.useTextBitmapFilters.value = true;
				
				visualization.enableAutoZoomToExtent.value = true;
				visualization.enableZoomAndPan.value = false;
				
				visualization.topMarginClickCallbacks.addGroupedCallback(this, handleMarginLabelClick);
				
				getCallbackCollection(LinkableTextFormat.defaultTextFormat).addGroupedCallback(this, updateMarginLabel, true);
				_plotter.dynamicColorColumn.addGroupedCallback(this, updateMarginLabel, true);

				panelWidth.value = "20%";
				panelHeight.value = "50%";
				
				visualization.marginBottom.value = "5";
				visualization.marginTop.value    = DEFAULT_MARGIN_TOP.toString();
				visualization.marginLeft.value   = "5";
				visualization.marginRight.value  = "5";
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
				dataColumnComponent.dynamicObject = _plotter.dynamicColorColumn;
				_plotter.dynamicColorColumn.addImmediateCallback(this, colorColumnCallback, true);
				visualization.marginTopNumber.addImmediateCallback(this, handleMarginChange);
				linkBindableProperty(visualization.marginTopNumber, marginLabel, "height");
				
				linkBindableProperty(_plotter.maxColumns, legendControls.numColumns, 'value');
				linkBindableProperty(_plotter.ascendingOrder, legendControls.ascendingOrderCheck, 'selected');
				linkBindableProperty(_plotter.itemLabelFunction, legendControls.txtLegendValuesFunction, 'text', 500);
				linkBindableProperty(_plotter.legendTitleFunction, legendControls.txtLegendAttrNameFunc, 'text', 500);
				linkBindableProperty(useUnobtrusiveToolTips, legendControls.aboveToolTips, 'selected');
				legendControls.toolTipSide.addEventListener(ListEvent.CHANGE, updateToolTipSide);
				legendControls.chkShowLegendAttrName.addEventListener(Event.CHANGE, toggleShowAttributeName);
				
				_plotter.legendTitleFunction.addGroupedCallback(this, updateMarginLabel, true);
			}
			
			private function boxUnchecked():void
			{
				if( legendControls.aboveToolTips.selected == false )
					toolTipLocation.value = ProbeTextUtils.CENTER_WINDOW;
				else
				{	
					if( legendControls.toolTipSide.selectedIndex == 0 )
						toolTipLocation.value = ProbeTextUtils.ABOVE_WINDOW;
					else if( legendControls.toolTipSide.selectedIndex == 1 )
						toolTipLocation.value = ProbeTextUtils.LEFT_WINDOW;
					else if( legendControls.toolTipSide.selectedIndex == 2 )
						toolTipLocation.value = ProbeTextUtils.RIGHT_WINDOW;	
					else if( legendControls.toolTipSide.selectedIndex == 3 )
						toolTipLocation.value = ProbeTextUtils.BELOW_WINDOW;
				}
			}
			
			private function updateToolTipSide(event:ListEvent):void
			{
				if( legendControls.toolTipSide.selectedIndex == 0 )
					toolTipLocation.value = ProbeTextUtils.ABOVE_WINDOW;
				else if( legendControls.toolTipSide.selectedIndex == 1 )
					toolTipLocation.value = ProbeTextUtils.LEFT_WINDOW;
				else if( legendControls.toolTipSide.selectedIndex == 2 )
					toolTipLocation.value = ProbeTextUtils.RIGHT_WINDOW;	
				else if( legendControls.toolTipSide.selectedIndex == 3 )
					toolTipLocation.value = ProbeTextUtils.BELOW_WINDOW;
			}
			
			private var _prevBinnedColumn:BinnedColumn;
			private function colorColumnCallback():void
			{
				if (!parent)
					callLater(colorColumnCallback);
				
				if (_prevBinnedColumn != internalBinnedColumn)
				{
					if (_prevBinnedColumn)
						unlinkSessionState(_prevBinnedColumn, binEditor.binnedColumn);

					_prevBinnedColumn = internalBinnedColumn;

					if (_prevBinnedColumn)
						linkSessionState(_prevBinnedColumn, binEditor.binnedColumn);
				}
				
				if (_plotter.getInternalColorColumn() == null)
				{
					cRampEditor.colorRamp = null;
					reportError("ColorBinLegendPlotter.getInternalColorColumn() is null");
				}
				else
				{
					cRampEditor.colorRamp = _plotter.getInternalColorColumn().ramp;
				}
			}
			
			private function get internalBinnedColumn():BinnedColumn
			{
				var cc:ColorColumn = _plotter.getInternalColorColumn();
				if (cc)
					return cc.internalColumn as BinnedColumn
				return null;
			}

			private function get internalDataColumn():DynamicColumn
			{
				try
				{
					return (internalBinnedColumn.internalColumn as FilteredColumn).internalDynamicColumn;
				}
				catch (e:Error) { } // do nothing if there is no data column
				return null;
			}

			override public function getSelectableAttributeNames():Array
			{
				return ["Color data"];
			}
			override public function getSelectableAttributes():Array
			{
				return [_plotter.dynamicColorColumn];
			}
			
			private function handleMarginLabelClick():void
			{
				AttributeSelectorPanel.openToolSelector(this,_plotter.dynamicColorColumn);
			}
			
			private function updateMarginLabel():void
			{
				if (!parent)
					return callLater(updateMarginLabel);
				
				visualization.additionalProbeColumns = WeaveAPI.SessionManager.getLinkableDescendants(_plotter.dynamicColorColumn, ReferencedColumn);
				
				visualization.topMarginColumn = _plotter.dynamicColorColumn;
				var colTitle:String = ColumnUtils.getTitle(_plotter.dynamicColorColumn);
				try
				{
					marginLabel.text = _plotter.legendTitleFunction.apply(null, [colTitle]);   
				}
				catch (e:Error)
				{
					marginLabel.text = colTitle;
				}
				
				LinkableTextFormat.defaultTextFormat.copyToStyle(marginLabel);
			}
			
			override protected function get defaultPanelTitle():String
			{
				return "Color Legend";
			}
			
			private static const DEFAULT_MARGIN_TOP:int = 25;
			private var _prevMarginTop:String = String(DEFAULT_MARGIN_TOP); // this stores the previous nonzero top margin setting
			private function handleMarginChange():void
			{
				if (visualization.marginTopNumber.value != 0)
				{
					_prevMarginTop = visualization.marginTop.value;
					legendControls.chkShowLegendAttrName.selected = true;
				}
				else
					legendControls.chkShowLegendAttrName.selected = false;
			}
			private function toggleShowAttributeName(e:Event):void
			{
				visualization.marginTop.value = legendControls.chkShowLegendAttrName.selected ? _prevMarginTop : '0';
			}
		]]>
	</mx:Script>
	
	<mx:TextArea id="marginLabel"
				 visible="{marginLabel.height > 0}"
				 mouseChildren="false" mouseEnabled="false"
				 x="0" y="0" paddingLeft="5" paddingTop="5" paddingRight="5" width="100%"
				 horizontalScrollPolicy="off" verticalScrollPolicy="off"
				 wordWrap="true" selectable="false" editable="false"
				 borderThickness="0" backgroundAlpha="0"/>
	<ui:ControlPanel>
		<mx:VBox label="Binning">
			<ui:DynamicColumnComponent visTool="{this}" id="dataColumnComponent" label="Color data" showLinkControl="false"/>
			<ui:BinningDefinitionEditor id="binEditor" width="100%"/>
		</mx:VBox>
		<mx:VBox label="Appearance">
			<ui:LegendControls id="legendControls"/>
			<mx:HBox>
				<mx:Label text="Max bubble size"/>
				<mx:HSlider liveDragging="true" minimum="10" maximum="60" tickInterval="10" snapInterval="5" creationComplete="linkBindableProperty(_plotter.shapeSize, event.target, 'value')"/>
			</mx:HBox>
			<ui:ColorRampEditor id="cRampEditor"/>
		</mx:VBox>
	</ui:ControlPanel>

</SimpleVisTool>
