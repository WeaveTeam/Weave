<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
	     xmlns:mx="http://www.adobe.com/2006/mxml"
	     xmlns:ui="weave.ui.*" 
		 xmlns:userControls="weave.ui.userControls.*"
		 layout="absolute">
	<mx:Script>
		<![CDATA[
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.registerLinkableChild;
			import weave.api.ui.IVisTool;
			import weave.core.LinkableBoolean;
			import weave.core.LinkableVariable;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.FilteredColumn;
			import weave.ui.AttributeSelectorPanel;
			import weave.utils.ColumnUtils;
			import weave.utils.ProbeTextUtils;
			import weave.visualization.plotters.PieChartPlotter;
			import weave.visualization.plotters.styles.SolidFillStyle;

			WeaveAPI.registerImplementation(IVisTool, PieChartTool, "Pie Chart");
			
			public var _plotter:PieChartPlotter = null;
			
			override protected function constructor():void
			{
				super.constructor();
				// lock dynamic objects into place
				_plotter = initializePlotters(PieChartPlotter, false);
				
				visualization.plotManager.enableAutoZoomToExtent.value = true;
				visualization.plotManager.enableFixedAspectRatio.value = true;
				visualization.enableZoomAndPan.value = false;

				getCallbackCollection(visualization.plotManager.zoomBounds).addImmediateCallback(this, invalidateAxes);
				getCallbackCollection(_plotter).addImmediateCallback(this, invalidateAxes);
				invalidateAxes();
				
				initColumnDefaults(_plotter.data);
				// see if there is a matching column in probe headers
				var headers:Array = ProbeTextUtils.probeHeaderColumns.getObjects();
				for (var i:int = 0; i < headers.length; i++)
				{
					if (ColumnUtils.getKeyType(headers[i]) == ColumnUtils.getKeyType(_plotter.data))
					{
						_plotter.label.requestLocalObjectCopy(headers[i]);
						break;
					}
				}
				handleSelectAllAttrInProbe(true);//default to include all selectable attributes in tool

				_plotter.data.addImmediateCallback(this, updateProbeColumns, true);
				
				visualization.plotManager.marginTop.value = "65";
				visualization.plotManager.marginBottom.value = "65";
				visualization.plotManager.marginLeft.value = "85";
				visualization.plotManager.marginRight.value = "85";
			}
			
			public const attributesToIncludeInProbe:LinkableVariable = registerLinkableChild(this,new LinkableVariable(Array),updateProbeColumns,true);
			[Deprecated] public function set includeToolAttributesInProbe(value:Boolean):void
			{
				handleSelectAllAttrInProbe(value);
			}
			
			private function handleSelectAllAttrInProbe(setAll:Boolean):void
			{
				var attributes:Array = getSelectableAttributeNames();
				var valuesToSet:Array = [];
				if(setAll)
				{
					for each (var name:String in attributes)
					{
						valuesToSet.push(true);
					}
				}
				else
				{
					for each (var name2:String in attributes)
					{
						valuesToSet.push(false);
					}
				}
				attributesToIncludeInProbe.setSessionState(valuesToSet);
			}
			
			private function updateProbeColumns():void
			{
				/* first check to see if select all should be disabled or not */
				var attrToIncludeCheckList:Array = attributesToIncludeInProbe.getSessionState() as Array;
				
				if(attributesChecklist)
				{
					if(attrToIncludeCheckList.indexOf(false) > -1)
					{
						probeColumnsCheckBox.selected = false;
					}
					else
					{
						probeColumnsCheckBox.selected = true;
					}
				}
				
				
				var attributes:Array = getSelectableAttributes();
				var columns:Array = [];
				
				var datColIndex:int = attributes.indexOf(_plotter.data);
				if (_plotter.data.getInternalColumn() && attrToIncludeCheckList[datColIndex])
					columns.push(_plotter.data.getInternalColumn());
				
				var colorColIndex:int = attributes.indexOf(colorColumn);
				if (colorColumn.getInternalColumn() && attrToIncludeCheckList[colorColIndex])	
					columns.push(colorColumn);
				
				var labelColIndex:int = attributes.indexOf(_plotter.label);
				if (_plotter.label.getInternalColumn() && attrToIncludeCheckList[labelColIndex])	
					columns.push(_plotter.label.getInternalColumn());
				
				
				if(columns.length>0)
				{
					visualization.additionalProbeColumns = columns;
				}
				else
				{
					visualization.additionalProbeColumns = null;
				}
				
				if(attributesChecklist)
				{
					updateAttributesChecklist();
				}
				
				//else ProbeTextUtils.probedColumns.copyObject('probedColumns',oldProbeColumns);
			}
			
			private function updateAttributesChecklist():void
			{
				var attributes:Array = getSelectableAttributeNames();
				var attrCheckList:Array = attributesToIncludeInProbe.getSessionState() as Array;
				var temp:Array = [];
				for (var i:int = 0; i < attributes.length; i++)
				{
					var obj:Object = new Object();
					obj.label = attributes[i];
					obj.selected = attrCheckList[i];
					temp.push(obj);
				}
				attributesChecklist.dataProvider = temp;
			}
			
			public function get colorColumn():DynamicColumn
			{
				return AttributeSelectorPanel.hack_findInternalDynamicColumn((_plotter.fillStyle.internalObject as SolidFillStyle).color);
			}
			
			override public function getSelectableAttributeNames():Array
			{
				return [lang("Wedge Size"),lang("Wedge Color"),lang("Label")];
			}
			override public function getSelectableAttributes():Array
			{
				return [_plotter.data,colorColumn,_plotter.label];
			}
			
			private function invalidateAxes():void
			{
				handlePanelTitleChange();
			}

			override protected function get defaultPanelTitle():String
			{
				return lang("Pie Chart of {0}", ColumnUtils.getTitle(_plotter.data));
			}
		]]>
	</mx:Script>
	<ui:ControlPanel>
		<mx:VBox id="attributesPanel" label="{lang('Attributes for Pie Chart')}">
			<ui:DynamicColumnComponent visTool="{this}" label="{lang('Wedge size')}" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = _plotter.data"/>
			<ui:DynamicColumnComponent visTool="{this}" label="{lang('Wedge color')}" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = colorColumn"/>
			<ui:DynamicColumnComponent visTool="{this}" id="labelColumInput" label="{lang('Label Column')}" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = _plotter.label"/>
			<mx:HBox width="100%">
				<mx:Label text="{lang('Label angle')}" fontWeight="bold"/>
				<mx:HSlider
						snapInterval="0.25" minimum="0" maximum="1" liveDragging="true" tickInterval="0.25"
						dataTipFormatFunction="{function(v:Number):String { return lang('{0}%', Math.round(100 * v)); }}"
						creationComplete="linkBindableProperty(_plotter.labelAngleRatio, event.target, 'value')"/>
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="{lang('Inner radius')}" fontWeight="bold"/>
				<mx:HSlider
						snapInterval="0.05" minimum="0" maximum="1" liveDragging="true" tickInterval="0.25"
						creationComplete="linkBindableProperty(_plotter.innerRadius, event.target, 'value')"/>
			</mx:HBox>
		</mx:VBox>
		<mx:VBox label="{lang('Probe')}">
			<mx:CheckBox id="probeColumnsCheckBox" label="{lang('Include tool attributes in probe tooltip')}"
						 toolTip="{lang('Checking this box makes all of the following attributes visible in this tool\'s probe tooltip')}"
						 change="handleSelectAllAttrInProbe(probeColumnsCheckBox.selected)"/>
			<mx:List width="100%" borderThickness="0" id="attributesChecklist" minHeight="0" height="100%">
				<mx:itemRenderer>
					<mx:Component>
						<mx:CheckBox change="handleSelectionChange()">
							
							<mx:Script>
								<![CDATA[
									override public function set data(d:Object):void
									{
										super.data = d;
										selected = d && d.selected;
										label = (d && d.label) as String;
									}

									private function handleSelectionChange():void
									{
										data.selected = this.selected;
										var index:int = outerDocument.getSelectableAttributeNames().indexOf(data.label);
										var currentState:Array = outerDocument.attributesToIncludeInProbe.getSessionState() as Array;
										currentState[index]= this.selected;
										outerDocument.attributesToIncludeInProbe.setSessionState(currentState);
									}
								]]>
							</mx:Script>
						</mx:CheckBox>
					</mx:Component>
				</mx:itemRenderer>
			</mx:List>
		</mx:VBox>
	</ui:ControlPanel>
</SimpleVisTool>
