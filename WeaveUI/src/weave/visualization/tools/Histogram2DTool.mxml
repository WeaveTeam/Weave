<?xml version="1.0" encoding="utf-8"?>
<!--
/*
Weave (Web-based Analysis and Visualization Environment)
Copyright (C) 2008-2011 University of Massachusetts Lowell

This file is a part of Weave.

Weave is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License, Version 3,
as published by the Free Software Foundation.

Weave is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
			   xmlns:mx="http://www.adobe.com/2006/mxml"
			   xmlns:ui="weave.ui.*" 
			   xmlns:userControls="weave.ui.userControls.*"
			   xmlns:settings="weave.ui.settings.*"
			   layout="absolute">
	<mx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			
			import weave.Weave;
			import weave.api.core.ILinkableDynamicObject;
			import weave.api.data.IBinningDefinition;
			import weave.api.data.IKeySet;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.newLinkableChild;
			import weave.api.registerLinkableChild;
			import weave.api.unlinkSessionState;
			import weave.core.LinkableBoolean;
			import weave.core.SessionManager;
			import weave.data.AttributeColumns.BinnedColumn;
			import weave.data.AttributeColumns.ColorColumn;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.FilteredColumn;
			import weave.data.AttributeColumns.StringLookup;
			import weave.data.BinningDefinitions.EqualIntervalBinningDefinition;
			import weave.data.BinningDefinitions.SimpleBinningDefinition;
			import weave.primitives.ColorRamp;
			import weave.ui.AttributeSelectorPanel;
			import weave.ui.DraggablePanel;
			import weave.utils.BitmapText;
			import weave.utils.ColumnUtils;
			import weave.visualization.plotters.Histogram2DPlotter;
			import weave.visualization.plotters.HistogramPlotter;
			import weave.visualization.plotters.ProbeLinePlotter;
			
			public var plotter:Histogram2DPlotter = null;
			
			public function get histogramPlotter():Histogram2DPlotter
			{
				return plotter;
			}
			override protected function constructor():void
			{
				super.constructor();
				
				// lock dynamic objects into place
				plotter = initDefaultPlotter(Histogram2DPlotter) as Histogram2DPlotter;
				visualization.enableProbeLine(false, true);
				
				xAxisEnabled = true;
				yAxisEnabled = true;
				
				xAxisLayer.axisPlotter.forceTickCount.value = true;
				visualization.enableZoomAndPan.value = false;
				
				visualization.enableProbeLine(true, true);
				
				visualization.marginBottom.value = 80;
				visualization.marginTop.value    = 10;
				visualization.marginLeft.value   = 80;
				visualization.marginRight.value  = 10;
				
				visualization.enableXAxisProbing = true;
				visualization.enableYAxisProbing = true;
				
				visualization.marginLeft.addGroupedCallback(this, updateAxisLabels);
				visualization.marginBottom.addGroupedCallback(this, updateAxisLabels);
				getCallbackCollection(visualization.zoomBounds).addGroupedCallback(this, updateAxisLabels);
				getCallbackCollection(plotter).addGroupedCallback(this, updateAxisLabels, true);
				
				visualization.bottomMarginClickCallbacks.addImmediateCallback(this, AttributeSelectorPanel.openToolSelector, [this, plotter.xColumn])
				visualization.leftMarginClickCallbacks.addImmediateCallback(this, AttributeSelectorPanel.openToolSelector, [this, plotter.yColumn])
						
				visualization.enableAutoZoomXToNiceNumbers.value = false;
				visualization.enableAutoZoomYToNiceNumbers.value = false;
				
				initColumnDefaults(plotter.xColumn, plotter.yColumn);
				
				toolTitle.addGroupedCallback(this, updateTitle);
			}
			
			override protected function childrenCreated():void
			{
				cRampEditor.colorRamp = plotter.binColors;
				handleBinColRadioButtonChange();
				xColumnComponent.dynamicObject = plotter.xColumn;
				yColumnComponent.dynamicObject = plotter.yColumn;
			}
			
			override public function getSelectableAttributeNames():Array
			{
				return ["X","Y"];
			}
			override public function getSelectableAttributes():Array
			{
				return [plotter.xColumn,plotter.yColumn];
			}
			
			public const includeToolAttributesInProbe:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(true), updateProbeColumns);
			
			
			private function updateProbeColumns():void
			{
				visualization.additionalProbeColumns = [];
				if (includeToolAttributesInProbe.value)
				{
					if (plotter.xBinnedColumn.internalColumn && plotter.yBinnedColumn.internalColumn)
					{
						visualization.additionalProbeColumns.push(plotter.xBinnedColumn.internalColumn);
						visualization.additionalProbeColumns.push(plotter.xBinnedColumn.internalColumn);
					}
				}
			}
			
			private var _prevBinnedColumn:BinnedColumn = null;
			private function handleBinColRadioButtonChange():void
			{
				var selectedCol:String = binColGroup.selectedValue as String;
				
				//it is null when the tool loads for the first time and so it defaults to the x column
				if(selectedCol == "xBinCol" || selectedCol == null)
				{
					xBinColRadioButton.setStyle('fontWeight','bold');
					yBinColRadioButton.setStyle('fontWeight','normal');
					
					if (_prevBinnedColumn)
						unlinkSessionState(_prevBinnedColumn, binEditor.binnedColumn);
					
					if (plotter.xBinnedColumn)
						linkSessionState(plotter.xBinnedColumn, binEditor.binnedColumn);
					
					_prevBinnedColumn = plotter.xBinnedColumn;
				}else if(selectedCol == "yBinCol"){
					yBinColRadioButton.setStyle('fontWeight','bold');
					xBinColRadioButton.setStyle('fontWeight','normal');
					
					if (_prevBinnedColumn)
						unlinkSessionState(_prevBinnedColumn, binEditor.binnedColumn);
					
					if (plotter.yBinnedColumn)
						linkSessionState(plotter.yBinnedColumn, binEditor.binnedColumn);
					
					_prevBinnedColumn = plotter.yBinnedColumn;
				}
			}
			
			
			
			private function updateTitle():void
			{
				if (toolTitle.value == null)
					title = "2D Histogram of " + ColumnUtils.getTitle(plotter.xColumn) + " vs " + ColumnUtils.getTitle(plotter.yColumn) ;
			}
			
			private function updateAxisLabels():void
			{
				updateTitle();
				visualization.setXAxisColumn(plotter.xColumn);
				visualization.setYAxisColumn(plotter.yColumn);
				
				var xBinCol:BinnedColumn = plotter.xBinnedColumn;
				
				xAxisLayer.axisPlotter.tickCountRequested.value = xBinCol.numberOfBins;
				xAxisLayer.axisPlotter.tickMinValue.value = 0;
				xAxisLayer.axisPlotter.tickMaxValue.value = xBinCol.numberOfBins - 1;	
				xAxisLayer.axisPlotter.setLabelFunction(xBinCol.deriveStringFromNumber);
				
				var yBinCol:BinnedColumn = plotter.yBinnedColumn;
				
				yAxisLayer.axisPlotter.tickCountRequested.value = yBinCol.numberOfBins;
				yAxisLayer.axisPlotter.tickMinValue.value = 0;
				yAxisLayer.axisPlotter.tickMaxValue.value = yBinCol.numberOfBins - 1;	
				yAxisLayer.axisPlotter.setLabelFunction(yBinCol.deriveStringFromNumber);
				
				
				xAxisLayer.axisPlotter.setSideAxisName(ColumnUtils.getTitle(plotter.xColumn), 0, 0, visualization.marginBottom.value - 3, BitmapText.VERTICAL_ALIGN_BOTTOM);
				yAxisLayer.axisPlotter.setSideAxisName(ColumnUtils.getTitle(plotter.yColumn), -90, -visualization.marginLeft.value, 0, BitmapText.VERTICAL_ALIGN_TOP);
			}
		]]>
	</mx:Script>
	
	<ui:ControlPanel>
		<mx:VBox label="Visualization Controls">
			<mx:CheckBox id="probeColumnsCheckBox" label="Include tool attributes in probe tooltip"
						 toolTip="Checking this box makes the tool's binned attribute visible in this tool's probe tooltip"
						 initialize="linkBindableProperty(includeToolAttributesInProbe,probeColumnsCheckBox,'selected')"/>
			<ui:DynamicColumnComponent visTool="{this}" id="xColumnComponent" label="X Column"/>
			<ui:DynamicColumnComponent visTool="{this}" id="yColumnComponent" label="Y Column"/>
		</mx:VBox>
		<mx:VBox label="Binning Controls">
				<mx:HBox>
					<mx:Label text="Select Column : "/>
					<mx:RadioButtonGroup id="binColGroup" change="handleBinColRadioButtonChange()"/>
					<mx:RadioButton groupName="binColGroup" label="X Column" value="xBinCol" id="xBinColRadioButton" selected="true"/>
					<mx:RadioButton groupName="binColGroup" label="Y Column" value="yBinCol" id="yBinColRadioButton"/>
				</mx:HBox>
			<ui:BinningDefinitionEditor id="binEditor"/>
		</mx:VBox>
		<mx:VBox label="Advanced">
			<ui:ColorRampEditor id="cRampEditor"/>
			<settings:GridLineSettings tool="{this}"/>
		</mx:VBox>
	</ui:ControlPanel>
</SimpleVisTool>
