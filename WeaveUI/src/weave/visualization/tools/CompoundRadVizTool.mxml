<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
	     xmlns:mx="http://www.adobe.com/2006/mxml"
	     xmlns:ui="weave.ui.*" 
	     layout="absolute"
		 dragEnter="handleDragEnter(event)"
		 dragDrop="handleDragDrop(event)"
		 resize="handleResize(event)" xmlns:userControls="weave.ui.userControls.*">		 
	<mx:Script>
		<![CDATA[
			import flash.utils.getQualifiedClassName;
			
			import mx.events.DragEvent;
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;
			import mx.managers.DragManager;
			
			import weave.Weave;
			import weave.api.data.IAttributeColumn;
			import weave.api.disposeObjects;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.newLinkableChild;
			import weave.api.ui.IPlotLayer;
			import weave.core.LinkableHashMap;
			import weave.core.LinkableNumber;
			import weave.core.LinkableString;
			import weave.core.SessionManager;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.FilteredColumn;
			import weave.primitives.Bounds2D;
			import weave.primitives.ColorRamp;
			import weave.ui.DynamicColumnComponent;
			import weave.utils.ColumnUtils;
			import weave.utils.ProbeTextUtils;
			import weave.visualization.plotters.AnchorDisplayObject;
			import weave.visualization.plotters.AnchorPoint;
			import weave.visualization.plotters.CompoundRadVizPlotter;
			import weave.visualization.plotters.styles.SolidFillStyle;

			
			public var _plotter:CompoundRadVizPlotter = null;
			override protected function constructor():void
			{
				super.constructor();
				// lock dynamic objects into place
				_plotter = initDefaultPlotter(CompoundRadVizPlotter) as CompoundRadVizPlotter;
				getCallbackCollection(_plotter).addGroupedCallback(this, updateTitle);
				
				visualization.enableAutoZoomToExtent.value = true;
				visualization.enableFixedAspectRatio.value = true;
				visualization.enableZoomAndPan.value = false;

				var count:int = getDefaultColumnsOfMostCommonKeyType().length;
				var columnsToInit:Array = [];
				for (var i:int = 0; i < count; i++)
				{
					columnsToInit.push(_plotter.columns.requestObject(null, DynamicColumn, false));
				}
				
				if(columnsToInit.length > 0)
					(initColumnDefaults as Function).apply(null, columnsToInit);
				_plotter.setAnchorLocations();
			}

			override protected function childrenCreated():void
			{
				super.childrenCreated();

				(size as DynamicColumnComponent).dynamicObject = _plotter.radiusColumn ;
				(colorComponent as DynamicColumnComponent).dynamicObject = colorColumn;
				columnList.hashMap = _plotter.columns;

				linkBindableProperty(_plotter.enableJitter,jitterckbx,'selected');
				linkBindableProperty(_plotter.radiusConstant,sizeSlider,'value');				
				linkBindableProperty(_plotter.alphaColumn.defaultValue, alphaSlider, "value");
				linkBindableProperty(_plotter.jitterLevel, jitterSlider, "value" );
				
				_plotter.anchors.addGroupedCallback(this, drawAnchors );
				
				// TEMPORARY SOLUTION: draws anchors after the tool appears on screen so screenBounds are not undefined
				addEventListener(Event.RENDER, handleRenderEvent);
			}
						
			private function handleRenderEvent(event:Event):void
			{								
				getScreenBounds(screenBounds);				
				
				if(!(screenBounds.isEmpty() || screenBounds.isUndefined()))
				{
					_plotter.columns.addGroupedCallback(this, _plotter.setAnchorLocations);
 					drawAnchors();
					removeEventListener(Event.RENDER, handleRenderEvent);
				}
			}
			
			public function get colorColumn():DynamicColumn
			{
				var fc:FilteredColumn = Weave.root.getObject(Weave.DEFAULT_COLOR_DATA_COLUMN) as FilteredColumn;
				return fc.internalDynamicColumn;
			}
			
			override public function getSelectableAttributeNames():Array
			{
				return ["Size","Color","Anchor Dimensions"];
			}
			
			override public function getSelectableAttributes():Array
			{
				return [_plotter.radiusColumn,colorColumn,_plotter.columns];
			}
			
			private function updateTitle():void
			{
				if (toolTitle.value == null)
				{				
					// try to get unique attribute names separated by commas					
					var newTitle:String = "Compound RadViz of ";
					
					var uniqueColumns:Dictionary = new Dictionary();
					
					for each(var column:IAttributeColumn in _plotter.columns.getObjects(IAttributeColumn) )
					{
						uniqueColumns[ColumnUtils.getTitle(column)] = column;
					}
					
					for (var uniqueTitle:String in uniqueColumns)
					{
						newTitle += uniqueTitle + ", ";
					}
					
					if(newTitle.charAt(newTitle.length-1) == ",")
						newTitle = newTitle.substr(0, newTitle.length-2);
					
					title = newTitle;
				}
			}
			
			private const colorRampName:LinkableString = newLinkableChild(this, LinkableString,handleColorRampName);
			
			private function handleColorRampName():void
			{
				if (stage == null)
					return callLater(handleColorRampName);
				_plotter.colorMap.value = ColorRamp.getColorRampXMLByName(colorRampName.value);				
			}
			
			private function alphaSliderFormatFunction(item:Number):String 
			{
				return int((item * 100)) + "% Opaque" + "\n" + int(100 - (item * 100)) + "% Transparent";
			}
			
			private function handleReseedJitterButtonClick(event:MouseEvent = null):void
			{
				_plotter.setNewRandomJitterColumn();
				reseedJitterButton.selected = false; 
			}
			
			private function handleAnchorReorderButtonClick(event:MouseEvent = null):void
			{
				switch( algorithmComboBox.selectedIndex )
				{
					case 0: 
						_plotter.applyRandomAnchorLayout(); break;
					case 1:
						_plotter.applyNearestNeighborAlgorithm(); break;
					case 2:
						_plotter.applyGreedyLayoutAlgorithm(); break;
					default:
						break;
				}
				anchorReorderButton.selected = false; 
			}
						
			private var tempBounds:Bounds2D = new Bounds2D();
			private var screenBounds:Bounds2D = new Bounds2D();			
			private var tempPoint:Point = new Point();
			
			private const anchorShapes:Vector.<AnchorDisplayObject> = new Vector.<AnchorDisplayObject>();
			
			private function drawAnchors():void
			{				
				visualization.dataBounds.copyTo(tempBounds);
				getScreenBounds(screenBounds);
				if( tempBounds.isEmpty() || tempBounds.isUndefined() ||Â screenBounds.isEmpty() || screenBounds.isUndefined()) return;
				
				var anchors:Array = _plotter.anchors.getObjects(AnchorPoint);
				if( !(anchors.length)) return;				
				
				var anchorShape:AnchorDisplayObject;
				while(anchorShapes.length)
				{
					anchorShape = anchorShapes.pop();					
					disposeObjects(anchorShape);
				}
				
				for each( var anchor:AnchorPoint in anchors)
				{
					anchorShape = new AnchorDisplayObject();
					anchorShape.anchor = anchor;
					anchor.x.addGroupedCallback(this, anchorShape.drawAnchor);
					anchor.y.addGroupedCallback(this, anchorShape.drawAnchor);
					anchorShapes.push(anchorShape);
										
					getScreenBounds(anchorShape.screenBounds);
					
					linkSessionState(visualization.dataBounds, anchorShape.parentDataBounds);
					
					if(!this.contains(anchorShape)) this.addChild(anchorShape);										
				}				
			}			
						
			public function getScreenBounds(outputBounds:Bounds2D):void
			{
				for each( var plotlayer:IPlotLayer in visualization.layers.getObjects(IPlotLayer))
				{
					plotlayer.getScreenBounds(outputBounds);
					break;
				}
			}
			
			private function handleDragEnter(event:DragEvent):void 
			{
                if (event.dragSource.hasFormat("canv"))
                {
                    DragManager.acceptDragDrop(CompoundRadVizTool(event.currentTarget));
                }
            }
			
			private function handleDragDrop(event:DragEvent):void 
			{
				var anchor:AnchorDisplayObject = AnchorDisplayObject(event.dragInitiator);
				tempPoint = visualization.globalToLocal(new Point(event.stageX, event.stageY));
								
				visualization.dataBounds.copyTo(tempBounds);
				getScreenBounds(anchor.screenBounds);
				
				anchor.screenBounds.projectPointTo(tempPoint, tempBounds);
				
                anchor.anchor.x.value = tempPoint.x;
                anchor.anchor.y.value = tempPoint.y;                
            }
			
			private function handleResize(event:ResizeEvent):void
			{
				for each( var anchor:AnchorDisplayObject in anchorShapes)
				{
					getScreenBounds(anchor.screenBounds);
					anchor.drawAnchor();
				}
			}
			
		]]>
	</mx:Script>
	<ui:ControlPanel>
		<mx:VBox id="VizControlsPanel" label="Visualization Controls">
			<mx:VBox width="100%" 
					 id="controls">
				<mx:HBox width="100%" 
						 borderStyle="solid" 
						 backgroundColor="0xFFFFFF" 
						 backgroundAlpha="0.7">
					<mx:Label text="Point Opacity:" 
							  color="0x000000" fontSize="12" 
							  toolTip="Use this control to see how transparent points are.  Moving left increases transparency (decreased opacity), moving to the right decreases transparency (increased opacity) up to fully opaque (no transparency)."/>
					
					<mx:HSlider id="alphaSlider"
								width="100%"
								alpha="1.0"
								snapInterval="0.1"
								tickInterval="0.1" labels="{['10%', '30%', '50%', '70%', '90%', '100%']}"
								dataTipFormatFunction="alphaSliderFormatFunction"
								minimum="0.1" maximum="1.0" value="1.0"
								liveDragging="true"/>
				</mx:HBox>
				<mx:Spacer height="10"/>
				<mx:HBox width="100%" 
						 borderStyle="solid" 
						 backgroundColor="0xFFFFFF" 
						 backgroundAlpha="0.7">
					<mx:Label text="Point Radius:" 
							  color="0x000000" fontSize="12" 
							  toolTip="Use this control to adjust point radius"/>
					
					<mx:HSlider id="sizeSlider"
								width="100%"
								alpha="1.0"
								tickInterval="1" 
								minimum="1" maximum="10" 
								liveDragging="true"/>
				</mx:HBox>
			</mx:VBox>
			<mx:Spacer height="10"/>
			<mx:VBox
				borderStyle="solid" 
				backgroundColor="0xFFFFFF" 
				backgroundAlpha="0.7"
				id="controls2">
				<mx:HBox>
					<mx:Label text="Apply Jitter:" 
							  color="0x000000" fontSize="12" 
							  toolTip="Use this control to change record coordinates by a random number"/>					
					<mx:CheckBox id="jitterckbx"/>					
					<mx:HBox backgroundColor="0xFFFFFF" 
							 cornerRadius="5" 
							 borderStyle="solid" borderThickness="1" borderColor="0x222222"
							 >
						<mx:LinkButton id="reseedJitterButton" click="handleReseedJitterButtonClick(event)" 
									   toolTip="Use this control to reseed jitter with new random values"
									   label="Randomize Jitter" rollOverColor="0x80dbff" selectionColor="0xaaff00"/>
					</mx:HBox>
				</mx:HBox>
				<mx:HBox>
					<mx:Label text="Jitter Level:" 
							  color="0x000000" fontSize="12" 
							  enabled="{jitterckbx.selected}"
							  toolTip="Use slider to adjust the jitter level"/>					
					<mx:HSlider id="jitterSlider"
								enabled="{jitterckbx.selected}"
								width="100%"
								alpha="1.0"
								snapInterval="1"
								tickInterval="1"
								labels="{['1','2','3','4','5','6','7','8','9','10']}"
								showDataTip="false"
								minimum="-19" maximum="-10"
								liveDragging="true"/>
				</mx:HBox>
			</mx:VBox>
			<mx:Spacer height="10"/>
			<mx:VBox borderStyle="solid" 
					 backgroundColor="0xFFFFFF" 
					 backgroundAlpha="0.7"
					 id="controls4">
				<mx:HBox>
					<mx:Label text="Dimensional Anchor Reordering Algorithm:" 
							  color="0x000000" fontSize="12" 
							  toolTip="Use this control to reorder the dimensional anchors"/>					
				</mx:HBox>

					<mx:ComboBox id="algorithmComboBox" labelField="label">
						<mx:ArrayCollection>
							<mx:Object label="Random Swapping" />
							<mx:Object label="Nearest Neighbor" />
							<mx:Object label="Greedy Layout"/>
						</mx:ArrayCollection>
					</mx:ComboBox>
				<mx:HBox enabled="{algorithmComboBox.selectedIndex == 0}">
					<mx:Label text="Number of Iterations"/>
					<mx:TextInput id="iterationsTextInput" enabled="{algorithmComboBox.selectedIndex == 0}"
								  initialize="linkBindableProperty(_plotter.iterations, iterationsTextInput, 'text')"/>
				</mx:HBox>
					<mx:HBox backgroundColor="0xFFFFFF" 
							 cornerRadius="5" 
							 borderStyle="solid" borderThickness="1" borderColor="0x222222">
						<mx:LinkButton id="anchorReorderButton" click="handleAnchorReorderButtonClick(event)" 
									   label="Click to Apply" rollOverColor="0x80dbff" selectionColor="0xaaff00" />
					</mx:HBox>

			</mx:VBox>
		</mx:VBox>
		
		<mx:VBox id="attributesPanel" label="Attributes for RadViz">
			<ui:DynamicColumnComponent visTool="{this}" id="colorComponent" label="Color" />
			<ui:DynamicColumnComponent visTool="{this}" id="size" label="Size" />
			<ui:ColumnListComponent visTool="{this}" id="columnList" label="Anchor Dimensions"/>
		</mx:VBox>
		<mx:VBox id="AdvancedPanel" label="Advanced">
			<mx:CheckBox id="enableWedgeColoringCkbx"
						 label="Enable Wedge Coloring"
						 toolTip="Enable different wedge colors for the pi-chart glyphs"
						 creationComplete="linkBindableProperty(_plotter.enableWedgeColoring, enableWedgeColoringCkbx, 'selected')"/>
			<mx:Label text="Color scale used in pie-chart glyphs:" enabled="{enableWedgeColoringCkbx.selected}"/>
			<mx:DataGrid id="colormapGrid"
						 enabled="{enableWedgeColoringCkbx.selected}"
						 useRollOver="false"
						 minWidth="200" width="100%" height="100%"
						 color="black"
						 rollOverColor="0xCCFEFF"
						 fontWeight="normal"
						 paddingBottom="2" paddingTop="2" paddingLeft="5" paddingRight="5" 
						 horizontalScrollPolicy="off" verticalScrollPolicy="on"
						 allowMultipleSelection="false"  
						 dataProvider="{new XMLList(ColorRamp.allColorRamps.colorRamp)}"
						 change="colorRampName.value = colormapGrid.selectedItem.@name;"
						 headerHeight="22">
				<mx:columns>												
					<mx:DataGridColumn dataField="@name"  headerText="Color Scale"  itemRenderer="weave.ui.colormap.ColorRampPresetItemRenderer"/>
				</mx:columns>
			</mx:DataGrid>
		</mx:VBox>
	</ui:ControlPanel>
</SimpleVisTool>
