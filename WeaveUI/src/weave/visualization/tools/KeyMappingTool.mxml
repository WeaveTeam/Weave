<?xml version="1.0" encoding="utf-8"?>
<!--
/* ***** BEGIN LICENSE BLOCK *****
 *
 * This file is part of Weave.
 *
 * The Initial Developer of Weave is the Institute for Visualization
 * and Perception Research at the University of Massachusetts Lowell.
 * Portions created by the Initial Developer are Copyright (C) 2008-2015
 * the Initial Developer. All Rights Reserved.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this file,
 * You can obtain one at http://mozilla.org/MPL/2.0/.
 * 
 * ***** END LICENSE BLOCK ***** */
-->
<ui:DraggablePanel xmlns="weave.visualization.tools.*"
                   xmlns:mx="http://www.adobe.com/2006/mxml"
                   xmlns:ui="weave.ui.*" 
                   xmlns:CustomDataGrid="weave.ui.CustomDataGrid.*"
                   layout="absolute" implements="weave.api.ui.IVisTool, weave.api.ui.ISelectableAttributes"
                   title="{lang('Key Mapping Tool')}" xmlns:editors="weave.editors.*">
                   <mx:Script>
<![CDATA[
	import weave.Weave;
	import weave.api.data.ColumnMetadata;
	import weave.api.data.IQualifiedKey;
	import weave.api.linkBindableProperty;
	import weave.api.newLinkableChild;
	import weave.api.ui.IVisTool;
	import weave.core.LinkableBoolean;
	import weave.core.LinkableString;
	import weave.data.AttributeColumns.DynamicColumn;
	import weave.data.KeySets.KeySet;
	import weave.utils.ColumnUtils;


public const sourceKeyType:LinkableString = newLinkableChild(this, LinkableString, handleColumnsChange, true);
public const sourceLocalNameColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleColumnsChange, true);

public const targetKeyType:LinkableString = newLinkableChild(this, LinkableString, handleColumnsChange, true);
public const targetLocalNameColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleColumnsChange, true);

public const isBidirectional:LinkableBoolean = newLinkableChild(this, LinkableBoolean, handleColumnsChange, true);
public const limitRecursion:LinkableBoolean = newLinkableChild(this, LinkableBoolean, handleColumnsChange, true);

public const enableSelection:LinkableBoolean = newLinkableChild(this, LinkableBoolean, handleColumnsChange, true);
public const enableProbing:LinkableBoolean = newLinkableChild(this, LinkableBoolean, handleColumnsChange, true);


private const _probe:KeySet = Weave.defaultProbeKeySet;
private const _selection:KeySet = Weave.defaultSelectionKeySet;
private var keyMapping:Dictionary = new Dictionary();
private var reverseKeyMapping:Dictionary = new Dictionary();

private var currentlyMappedKeys:KeySet = new KeySet(); /* To ensure we only do one step */
private var previouslyMappedKeys:KeySet = new KeySet();

WeaveAPI.ClassRegistry.registerImplementation(IVisTool, KeyMappingTool, "Foreign Key Mapping Tool");

override protected function inConstructor():void
{
    super.inConstructor();
}
private function mapKeys(inputKeys:KeySet, mapping:Dictionary):KeySet
{
    
    var outputKeys:KeySet = new KeySet();
    var inputKeyArray:Array = inputKeys.keys;
    for (var i:int = inputKeyArray.length -1; i >= 0; i--)
    {
        var inKey:IQualifiedKey = inputKeyArray[i] as IQualifiedKey;
        
        if (limitRecursion.value && previouslyMappedKeys.containsKey(inKey)) continue;

        var replacements:Array = mapping[inKey] as Array;
        
        if (replacements)
        {
            outputKeys.addKeys(replacements);
        }
    }

    if (limitRecursion.value) currentlyMappedKeys.addKeys(outputKeys.keys);

    return outputKeys;
}

private function performMapping(inputSet:KeySet):void
{
    if (limitRecursion.value)
    {
        previouslyMappedKeys.replaceKeys(currentlyMappedKeys.keys);
        currentlyMappedKeys.clearKeys();
    }

    inputSet.addKeys(mapKeys(inputSet, keyMapping).keys);

    if (isBidirectional.value)
    {
        inputSet.addKeys(mapKeys(inputSet, reverseKeyMapping).keys);
    }

}
private function probeChanged():void
{
    if (enableProbing.value) performMapping(_probe);
}
private function selectionChanged():void
{
    if (enableSelection.value) performMapping(_selection);
}

private function handleColumnsChange():void
{

    keyMapping = new Dictionary();
    reverseKeyMapping = new Dictionary();
	
	var sourceKey:IQualifiedKey;
	var targetKey:IQualifiedKey;

    if (sourceLocalNameColumn.getInternalColumn() &&
        targetLocalNameColumn.getInternalColumn()) 
	{
		var columns:Array = ColumnUtils.joinColumns([sourceLocalNameColumn, targetLocalNameColumn], String);
		
		for (var i:int = columns[0].length - 1; i >= 0; i--)
		{
			var sourceKeyLocalName:String = columns[1][i];
			var targetKeyLocalName:String = columns[2][i];
			
			sourceKey = WeaveAPI.QKeyManager.getQKey(sourceKeyType.value, sourceKeyLocalName);
			targetKey = WeaveAPI.QKeyManager.getQKey(targetKeyType.value, targetKeyLocalName);
			
			if (keyMapping[sourceKey] === undefined)
			{
				keyMapping[sourceKey] = new Array();
			}
			
			if (reverseKeyMapping[targetKey] === undefined)
			{
				reverseKeyMapping[targetKey] = new Array();
			}
			
			keyMapping[sourceKey].push(targetKey);
			reverseKeyMapping[targetKey].push(sourceKey);
		}
    }
	else if (targetLocalNameColumn.getInternalColumn())
	{
		/* Assume that this is a proper array column that contains its own keytype information as "DataType" */
		var keyType:String = targetKeyType.value || targetLocalNameColumn.getMetadata(ColumnMetadata.DATA_TYPE) || ColumnMetadata.STRING;
		for each (sourceKey in targetLocalNameColumn.keys)
		{
			var targetKeys:Array = targetLocalNameColumn.getValueFromKey(sourceKey, Array);
			
			 
			for each (targetKeyLocalName in targetKeys)
			{
				targetKey = WeaveAPI.QKeyManager.getQKey(keyType, targetKeyLocalName);
				if (keyMapping[sourceKey] === undefined)
				{
					keyMapping[sourceKey] = [];
				}
				if (reverseKeyMapping[targetKey] === undefined)
				{
					reverseKeyMapping[targetKey] = [];
				}
				keyMapping[sourceKey].push(targetKey);
				reverseKeyMapping[targetKey].push(sourceKey);
			}
		}
	}
	else
	{
		return;
	}

    probeChanged();
    selectionChanged();

    return;
}
override protected function childrenCreated():void
{
    super.childrenCreated();

    _probe.addGroupedCallback(this, probeChanged);
    _selection.addGroupedCallback(this, selectionChanged);

    sourceLocalNameColumnComponent.dynamicObject = sourceLocalNameColumn;
    targetLocalNameColumnComponent.dynamicObject = targetLocalNameColumn;

    return;
}

public function getSelectableAttributes():Array
{
    return [sourceLocalNameColumn, targetLocalNameColumn];
}

public function getSelectableAttributeNames():Array
{
    return ['Source LocalName', 'Target LocalName'];
}

]]>
</mx:Script>
<ui:ControlPanel>
        <mx:VBox label="{lang('Attributes')}">
            <mx:HBox>
                <mx:Label text="{lang('Source KeyType')}"/>
                <ui:KeyTypesMenu id="sourceKeyTypeTextBox" creationComplete="linkBindableProperty(sourceKeyType, event.target, 'text', 500, true)"/>
            </mx:HBox>
            <ui:DynamicColumnComponent id="sourceLocalNameColumnComponent"/>
            <mx:HBox>
                <mx:Label text="{lang('Target KeyType')}"/>
                <ui:KeyTypesMenu id="targetKeyTypeTextBox" creationComplete="linkBindableProperty(targetKeyType, event.target, 'text', 500, true)"/>
            </mx:HBox>

            <ui:DynamicColumnComponent id="targetLocalNameColumnComponent"/>

            <mx:CheckBox id="enableSelectionCheckBox"
                label="{lang('Perform Mapping on Selection')}"
                creationComplete="linkBindableProperty(enableSelection, event.target, 'selected')"
            />
            <mx:CheckBox id="enableProbingCheckBox"
                label="{lang('Perform Mapping on Mouseover Linking')}"
                creationComplete="linkBindableProperty(enableProbing, event.target, 'selected')"
            />

            <mx:CheckBox id="isBidirectionalCheckBox" 
                label="{lang('Perform Bidirectional Mapping')}" 
                creationComplete="linkBindableProperty(isBidirectional, event.target, 'selected')"
            />
            <mx:CheckBox id="limitRecursionCheckBox"
                label="{lang('Limit to one step')}"
                creationComplete="linkBindableProperty(limitRecursion, event.target, 'selected')"
            />

        </mx:VBox>
        <!-- mx:VBox label="{lang('Advanced')}">
            <mx:CheckBox id="enableProbingCheckBox" label="{lang('Enable mouseover linking')}"/>
            <ui:Indent>
                <mx:CheckBox id="probeIncludesChildrenCheckBox" enabled="{enableProbingCheckBox.selected}" label="{lang('Linking includes all children.')}"/>
            </ui:Indent>
            <mx:CheckBox id="selectIncludesChildrenCheckBox" label="{lang('Include children in selections.')}"/>
            <mx:CheckBox id="expandToShowSelectedCheckBox" label="{lang('Expand to show selected children.')}" visible="false"/>
        </mx:VBox -->
        <editors:WindowSettingsEditor target="{this}"/>
    </ui:ControlPanel>
</ui:DraggablePanel>