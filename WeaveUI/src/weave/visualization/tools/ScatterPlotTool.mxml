<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/

-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
			   xmlns:mx="http://www.adobe.com/2006/mxml"
			   xmlns:ui="weave.ui.*" 
			   xmlns:settings="weave.ui.settings.*" xmlns:LinkableObjectList="weave.ui.LinkableObjectList.*"
			   xmlns:userControls="weave.ui.userControls.*"
			   layout="absolute">
	<mx:Script>
		<![CDATA[
			import mx.controls.TextArea;
			import mx.core.UIComponent;
			import mx.events.ChildExistenceChangedEvent;
			import mx.events.FlexEvent;
			import mx.events.IndexChangedEvent;
			import mx.events.ResizeEvent;
			import mx.events.SliderEvent;
			
			import weave.Weave;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IColumnWrapper;
			import weave.api.data.IKeyFilter;
			import weave.api.data.IKeySet;
			import weave.api.data.IPrimitiveColumn;
			import weave.api.data.IQualifiedKey;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.newLinkableChild;
			import weave.api.primitives.IBounds2D;
			import weave.api.registerLinkableChild;
			import weave.compiler.MathLib;
			import weave.core.LinkableBoolean;
			import weave.core.LinkableNumber;
			import weave.core.LinkableString;
			import weave.core.LinkableVariable;
			import weave.core.SessionManager;
			import weave.core.UIUtils;
			import weave.data.AttributeColumns.AlwaysDefinedColumn;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.FilteredColumn;
			import weave.primitives.Bounds2D;
			import weave.ui.AttributeSelectorPanel;
			import weave.ui.DraggablePanel;
			import weave.utils.BitmapText;
			import weave.utils.BitmapUtils;
			import weave.utils.ColumnUtils;
			import weave.utils.DebugUtils;
			import weave.utils.NumberUtils;
			import weave.utils.ProbeTextUtils;
			import weave.visualization.layers.PlotLayer;
			import weave.visualization.layers.SelectablePlotLayer;
			import weave.visualization.layers.SimpleInteractiveVisualization;
			import weave.visualization.plotters.AxisPlotter;
			import weave.visualization.plotters.CircleGlyphPlotter;
			import weave.visualization.plotters.ProbeLinePlotter;
			import weave.visualization.plotters.RegressionLinePlotter;
			import weave.visualization.plotters.ScatterPlotPlotter;
			import weave.visualization.plotters.styles.SolidFillStyle;
			
			public var _plotter:ScatterPlotPlotter = null;
			
			override protected function constructor():void
			{
				super.constructor();
				
				// lock dynamic objects into place
				_plotter = initDefaultPlotter(ScatterPlotPlotter) as ScatterPlotPlotter;
				visualization.enableProbeLine(true, true, labelFunctionY, labelFunctionX);
				
				initRegressionLayer() ;
				
				visualization.layers.childListCallbacks.addImmediateCallback(this, putRegressionLineOnTop);
				function putRegressionLineOnTop():void
				{
					var names:Array = visualization.layers.getNames();
					// move reg line after plot layer
					names.splice(names.indexOf(SimpleInteractiveVisualization.PLOT_LAYER_NAME) + 1, 0, names.splice(names.indexOf(REGRESSION_LAYER_NAME), 1));
					// set new name order
					visualization.layers.setNameOrder(names);
				}
				
				xAxisEnabled = true;
				yAxisEnabled = true;
				
				//BEGIN TEMPORARY SOLUTION
				visualization.marginLeft.addGroupedCallback(this, updateAxisLabels);
				visualization.marginBottom.addGroupedCallback(this, updateAxisLabels);
				getCallbackCollection(_plotter).addGroupedCallback(this, updateAxisLabels, true);
				// END TEMPORARY SOLUTION
				
				visualization.enableZoomAndPan.value = false;
				
				visualization.enableXAxisProbing = true;
				visualization.enableYAxisProbing = true;
				
				xAxisLayer.axisPlotter.setLabelFunction(labelFunctionX);
				yAxisLayer.axisPlotter.setLabelFunction(labelFunctionY);
				
				visualization.bottomMarginClickCallbacks.addImmediateCallback(this, AttributeSelectorPanel.openToolSelector, [this, _plotter.xColumn])
				visualization.leftMarginClickCallbacks.addImmediateCallback(this, AttributeSelectorPanel.openToolSelector, [this, _plotter.yColumn])
				
				visualization.enableAutoZoomXToNiceNumbers.value = true;
				visualization.enableAutoZoomYToNiceNumbers.value = true;
				
				getCallbackCollection(visualization.dataBounds).addGroupedCallback(this, invalidateAxes);
				getCallbackCollection(_plotter).addGroupedCallback(this, invalidateAxes, true);
				
				initColumnDefaults(_plotter.xColumn, _plotter.yColumn, colorColumn);
				
				toolTitle.addImmediateCallback(this, updateTitle);
				
				_plotter.xColumn.addGroupedCallback(this, updateProbeColumns);
				_plotter.yColumn.addGroupedCallback(this, updateProbeColumns);
				colorColumn.addGroupedCallback(this, updateProbeColumns);
				
				_plotter.enabledSizeBy.value = true;
				visualization.marginBottom.value = 80;
				visualization.marginTop.value    = 50;
				visualization.marginLeft.value   = 80;
				visualization.marginRight.value  = 50;
				
				initializeUndefinedLayers();				
			}
			
			override public function getSelectableAttributeNames():Array
			{
				return ["X", "Y", "Color", "Size"];
			}
			override public function getSelectableAttributes():Array
			{
				return [_plotter.xColumn, _plotter.yColumn, colorColumn, _plotter.radiusColumn];
			}
			
			private var _regressionLineLayer:PlotLayer ;
			private const REGRESSION_LAYER_NAME:String = "regressionLine";
			private var _regressionPlotter:RegressionLinePlotter = null;
			
			private function initRegressionLayer():void
			{
				_regressionLineLayer = visualization.layers.requestObject(REGRESSION_LAYER_NAME, PlotLayer, true);
				_regressionPlotter = _regressionLineLayer.getDynamicPlotter().requestLocalObject(RegressionLinePlotter,true);
				linkSessionState(_plotter.xColumn, _regressionPlotter.xColumn);
				linkSessionState(_plotter.yColumn, _regressionPlotter.yColumn);
				_regressionPlotter.drawLine.value = false;				
			}
			
			private function handleUpdateComplete(event:FlexEvent):void
			{
				plotLayer.getScreenBounds(_tempBounds);
				
				// TEMPORARY SOLUTION: undefined layer screenbounds need to be set after tool appears on stage
				// during creationComplete the plot's screenBounds are still empty, so this event needs to keep firing until they are not empty
				if(!_tempBounds.isEmpty())
				{
					setUndefinedLayerScreenBounds();
					// screenBounds have been set- remove event listener to avoid unnecessary updates
					removeEventListener(FlexEvent.UPDATE_COMPLETE, handleUpdateComplete);
				}
			}
			
			private var _undefinedXLayer:SelectablePlotLayer;
			private var _undefinedYLayer:SelectablePlotLayer;
			private var _undefinedXYLayer:SelectablePlotLayer;
			
			private var _undefinedXPlotter:ScatterPlotPlotter;
			private var _undefinedYPlotter:ScatterPlotPlotter;
			private var _undefinedXYPlotter:ScatterPlotPlotter;
									
			private const UNDEFINED_X_NAME:String = "undefinedX";
			private const UNDEFINED_Y_NAME:String = "undefinedY";
			private const UNDEFINED_XY_NAME:String = "undefinedXY";
			
			private const _tempBounds:Bounds2D = new Bounds2D();
			private const _screenBounds:Bounds2D = new Bounds2D();
			
			private function initializeUndefinedLayers():void
			{
				_undefinedXLayer = visualization.layers.requestObject(UNDEFINED_X_NAME,SelectablePlotLayer, true);
				_undefinedYLayer = visualization.layers.requestObject(UNDEFINED_Y_NAME, SelectablePlotLayer, true);
				_undefinedXYLayer = visualization.layers.requestObject(UNDEFINED_XY_NAME,SelectablePlotLayer, true);
				
				_undefinedXPlotter = _undefinedXLayer.getDynamicPlotter().requestLocalObject(ScatterPlotPlotter, true);
				_undefinedYPlotter = _undefinedYLayer.getDynamicPlotter().requestLocalObject(ScatterPlotPlotter, true);
				_undefinedXYPlotter = _undefinedXYLayer.getDynamicPlotter().requestLocalObject(ScatterPlotPlotter, true);
												
				for each(var plotter:ScatterPlotPlotter in [_undefinedXPlotter, _undefinedYPlotter, _undefinedXYPlotter])
				{
					linkSessionState(_plotter.colorColumn, plotter.colorColumn);
					linkSessionState(_plotter.alphaColumn, plotter.alphaColumn);
					linkSessionState(_plotter.radiusColumn, plotter.radiusColumn);
				} 	
				
				linkSessionState(_plotter.xColumn, _undefinedYPlotter.xColumn);
				linkSessionState(_plotter.yColumn, _undefinedXPlotter.yColumn);
				
				for each(var column:IAttributeColumn in getSelectableAttributes())
				{
					column.addGroupedCallback(this, setUndefinedPlotterKeySource, true);
				}
				
				visualization.marginTop.addGroupedCallback(this, setUndefinedLayerScreenBounds,true);
				visualization.marginBottom.addGroupedCallback(this, setUndefinedLayerScreenBounds, true);
				visualization.marginRight.addGroupedCallback(this, setUndefinedLayerScreenBounds, true);
				visualization.marginLeft.addGroupedCallback(this, setUndefinedLayerScreenBounds, true);
				
				for each(var layer:SelectablePlotLayer in [_undefinedXLayer, _undefinedYLayer, _undefinedXYLayer])
				{					
					layer.lockScreenBounds = true;
					layer.showMissingRecords(true);
					
					try 
					{
						// Remove background bitmaps to reduce the number of overlapping bitmaps on the visualization
						// This is a Flash Player limitation
						layer.plotLayer.removeChild(layer.plotLayer.getBackgroundBitmap());									
					} catch (e:Error) {} 				
				} 
				addEventListener(FlexEvent.UPDATE_COMPLETE, handleUpdateComplete);				
			}

			private const dataBounds:Bounds2D = new Bounds2D();
			
			private function setUndefinedPlotterKeySource():void
			{
				var recordKeys:Array = _plotter.keySet.keys;
				var missingXkeys:Array = [];
				var missingYkeys:Array = [];
				var missingXYkeys:Array = [];
				var missingX:Boolean;
				var missingY:Boolean;
				
				for each(var key:IQualifiedKey in recordKeys)	
				{
					missingX = !_plotter.xColumn.containsKey(key);
					missingY = !_plotter.yColumn.containsKey(key);
					
					if(missingX)
					{
						if(missingY)
							missingXYkeys.push(key);
						else 
							missingXkeys.push(key);
					}
					else if(missingY)
					{
						missingYkeys.push(key);
					}
				}
				_undefinedXPlotter.setCustomKeySource(missingXkeys);
				_undefinedYPlotter.setCustomKeySource(missingYkeys);
				_undefinedXYPlotter.setCustomKeySource(missingXYkeys);
			}
			
			private function setUndefinedLayerScreenBounds():void
			{				
				var left:Number = visualization.marginLeft.value;
				var top:Number = visualization.marginTop.value;
				var right:Number = visualization.width - visualization.marginRight.value;
				var bottom:Number = visualization.height - visualization.marginBottom.value;
				var width:Number = visualization.width;
				var height:Number = visualization.height;
				
				var topOffset:Number = top/2;
				var rightOffset:Number = visualization.marginRight.value/2;
				var graphics:Graphics = visualization.graphics;
				graphics.clear();
				graphics.lineStyle(1);
				
				graphics.moveTo(right+rightOffset, bottom);
				graphics.lineTo(right+rightOffset, top);				
				_tempBounds.setBounds(right, bottom, width, top);
				_undefinedXLayer.setScreenBounds(_tempBounds);					
				
				graphics.moveTo(left, top-topOffset);
				graphics.lineTo(right, top-topOffset);
				_tempBounds.setBounds(left, top, right, 0);
				_undefinedYLayer.setScreenBounds(_tempBounds);								
				
				graphics.moveTo(right+rightOffset, top-topOffset);
				graphics.lineTo(right+rightOffset+2, top-topOffset);
				_tempBounds.setBounds(right, top, width, 0);
				_undefinedXYLayer.setScreenBounds(_tempBounds);
				
				graphics.endFill();
			}
			
			// these variables are used to detect a change in size
			private var _prevUnscaledWidth:Number = 0;
			private var _prevUnscaledHeight:Number = 0;
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				// detect size change
				var sizeChanged:Boolean = _prevUnscaledWidth != unscaledWidth || _prevUnscaledHeight != unscaledHeight;
				_prevUnscaledWidth = unscaledWidth;
				_prevUnscaledHeight = unscaledHeight;
				
				super.updateDisplayList(unscaledWidth, unscaledHeight);
				if (sizeChanged)
					setUndefinedLayerScreenBounds();
			}
			
			public const includeToolAttributesInProbe:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(true), updateProbeColumns);
			
			private function updateProbeColumns():void
			{
				if(includeToolAttributesInProbe.value)
				{
					visualization.additionalProbeColumns = [];
					if (_plotter.xColumn.internalColumn)
						visualization.additionalProbeColumns.push(_plotter.xColumn.internalColumn);
					if (_plotter.yColumn.internalColumn)
						visualization.additionalProbeColumns.push(_plotter.yColumn.internalColumn);
					if (colorColumn.internalColumn)
						visualization.additionalProbeColumns.push(colorColumn.internalColumn);
					if (visualization.additionalProbeColumns.length == 0)
						visualization.additionalProbeColumns = null ;
				}
				else visualization.additionalProbeColumns = null;
			}
			
			private function invalidateAxes():void
			{
				updateTitle();
			}
			
			private function updateTitle():void
			{
				if (toolTitle.value == null)
					title = "Scatter Plot of " + ColumnUtils.getTitle(_plotter.xColumn) + " -vs- " + ColumnUtils.getTitle(_plotter.yColumn);
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();

				linkBindableProperty(_plotter.alphaColumn.defaultValue, alphaSlider, "value");
				linkBindableProperty(_plotter.enabledSizeBy, sizeByCheck, "selected");
				_plotter.minScreenRadius.addGroupedCallback(this, updateSizeSlider);
				_plotter.maxScreenRadius.addGroupedCallback(this, updateSizeSlider, true);
				getCallbackCollection(_regressionPlotter).addGroupedCallback(this, updateEquation, true);
				
				Weave.properties.axisFontFamily.addGroupedCallback(this,updateEquationStyle);
				Weave.properties.axisFontColor.addGroupedCallback(this,updateEquationStyle);
				Weave.properties.axisFontSize.addGroupedCallback(this,updateEquationStyle);
				Weave.properties.axisFontUnderline.addGroupedCallback(this, updateEquationStyle, true);
				
				setUndefinedLayerScreenBounds();
			}
			
			private function updateEquationStyle():void
			{
				equationText.setStyle("fontFamily", Weave.properties.axisFontFamily.value);
				equationText.setStyle("color", Weave.properties.axisFontColor.value);
				equationText.setStyle("fontSize", Weave.properties.axisFontSize.value);
				if (Weave.properties.axisFontUnderline.value)
					equationText.setStyle("textDecoration", "underline");
				else
					equationText.setStyle("textDecoration", "none");
			}
			
			private function updateEquation():void
			{
				if ( _regressionPlotter.drawLine.value )
				{
					var slope:Number = MathLib.roundSignificant(_regressionPlotter.getSlope(), 4);
					var intercept:Number = MathLib.roundSignificant(_regressionPlotter.getIntercept(), 4);
					lineEquation.text = "Equation: y = " + slope + "x"+ " + " + intercept;
					equationText.text = "slope = " + slope + "\nintercept = " + intercept;
					if (!contains(equationText))
						addChild(equationText);
				}
				else
				{ 
					lineEquation.text = "Equation: y = mx + b";
					if (contains(equationText))
					{
						try
						{
							removeChild(equationText);
						}
						catch (e:Error) {}
					}
				}
			}
			private function updateAxisLabels():void
			{
				visualization.setXAxisColumn(_plotter.xColumn);
				visualization.setYAxisColumn(_plotter.yColumn);
				
				if (_plotter == null)
					title = "Scatter Plot";
				/*				else
				title = "Scatter Plot of " + _plotter.xColumn.title + " -vs- " + _plotter.yColumn.title;
				*/					
				xAxisLayer.axisPlotter.setSideAxisName(
					ColumnUtils.getTitle(_plotter.xColumn), 
					0,   
					0, visualization.marginBottom.value - 3, 
					BitmapText.VERTICAL_ALIGN_BOTTOM
				);
				
				yAxisLayer.axisPlotter.setSideAxisName(
					ColumnUtils.getTitle(_plotter.yColumn), 
					-90, 
					-visualization.marginLeft.value, 0, 
					BitmapText.VERTICAL_ALIGN_TOP
				);
			}
			
			public function labelFunctionX(value:Number):String
			{
				// try to find an internal IPrimitiveColumn
				var column:IAttributeColumn = _plotter.xColumn;
				while (!(column is IPrimitiveColumn) && column is IColumnWrapper)
					column = (column as IColumnWrapper).internalColumn;
				if (column is IPrimitiveColumn)
					return (column as IPrimitiveColumn).deriveStringFromNumber(value);
				return null; // use default label
			}
			public function labelFunctionY(value:Number):String
			{
				// try to find an internal IPrimitiveColumn
				var column:IAttributeColumn = _plotter.yColumn;
				while (!(column is IPrimitiveColumn) && column is IColumnWrapper)
					column = (column as IColumnWrapper).internalColumn;
				if (column is IPrimitiveColumn)
					return (column as IPrimitiveColumn).deriveStringFromNumber(value);
				return null; // use default label
			}
			
			public function get colorColumn():DynamicColumn
			{
				var fc:FilteredColumn = Weave.root.getObject(Weave.DEFAULT_COLOR_DATA_COLUMN) as FilteredColumn;
				return fc.internalDynamicColumn;
			}
			
			private function updateSizeSlider():void
			{
				sizeBySlider.values = [_plotter.minScreenRadius.value, _plotter.maxScreenRadius.value];
				//this.closeable.value = false ;
			}
			
			private function alphaSliderFormatFunction(item:Number):String {
				//return int(100 - (item * 100)) + "% Transparent";
				return int((item * 100)) + "% Opaque" + "\n" + int(100 - (item * 100)) + "% Transparent";
			}
			
			
			private function handleSizeSliderChange(event:SliderEvent):void
			{
				if(event.thumbIndex == 0)
				{
					_plotter.minScreenRadius.value = event.value;
				}
				else
				{
					//_plotter.defaultScreenRadius.value = event.value;
					_plotter.maxScreenRadius.value = event.value;
				}
			}
			
			private function handleSizeByTextChange(event:Event):void
			{
				if (sizeByCheck.selected)
				{ 
					sizeByCheck.label = "Size by: " + ColumnUtils.getTitle(_plotter.radiusColumn);
					sizeByCheck.setStyle("textRollOverColor", "0x339900");
					sizeByCheck.setStyle("textSelectedColor", "0x339900");
					sizeByCheck.setStyle("Color", "0x339900");
					sizeBySlider.enabled = true;
				}
				else
				{
					sizeByCheck.label = "Size by: DISABLED";
					sizeByCheck.setStyle("textRollOverColor", "0xCC3300");
					sizeByCheck.setStyle("textSelectedColor", "0xCC3300");
					sizeByCheck.setStyle("Color", "0xCC3300");
					sizeBySlider.enabled = false;
				}
				
			}
			
			protected function Legend_clickHandler(event:MouseEvent):void
			{
				var sizeBinLegendTool:SizeBinLegendTool = Weave.root.requestObject(null, SizeBinLegendTool, false);
				sizeBinLegendTool.dynamicScatterPlotTool.globalName = Weave.root.getName(this);
			}
		]]>
	</mx:Script>
	<ui:AutoResizingTextArea id="equationText"
		 backgroundAlpha="0" editable="false" borderThickness="0" width="0"
				 bottom="0"
				 left="0"/>
	<ui:ControlPanel>
		<mx:VBox id="VizControlsPanel" label="Visualization Controls">
			<mx:Button id="Legend" label="legend" click="Legend_clickHandler(event)"/>
			<mx:HBox width="100%" 
					 borderStyle="inset" 
					 backgroundColor="0xFFFFFF" 
					 backgroundAlpha="0.7">
				<mx:Label text="Point Opacity:" 
						  color="0x000000" fontSize="12" 
						  toolTip="Use this control to see how transparent points are.  Moving left increases transparency (decreased opacity), moving to the right decreases transparency (increased opacity) up to fully opaque (no transparency)."/>
				
				<mx:HSlider id="alphaSlider" 
							width="100%" 
							alpha="1.0"
							dataTipFormatFunction="alphaSliderFormatFunction"
							minimum="0.1" maximum="1.0" value="1.0"
							liveDragging="true"/>
			</mx:HBox>
			<userControls:ControlsSubsetBox>
				<mx:CheckBox id="probeColumnsCheckBox" label="Include tool attributes in probe tooltip"
							 toolTip="Checking this box makes the x, y, color and size attributes visible in this tool's probe tooltip"
							 initialize="linkBindableProperty(includeToolAttributesInProbe,probeColumnsCheckBox,'selected')"/>
			</userControls:ControlsSubsetBox>
			<ui:DynamicColumnComponent visTool="{this}" label="x column" initialize="(event.target as DynamicColumnComponent).dynamicObject = _plotter.xColumn"/>
			<ui:DynamicColumnComponent visTool="{this}" label="y column" initialize="(event.target as DynamicColumnComponent).dynamicObject = _plotter.yColumn"/>
			<ui:DynamicColumnComponent visTool="{this}" label="color" initialize="(event.target as DynamicColumnComponent).dynamicObject = colorColumn"/>
			<ui:DynamicColumnComponent visTool="{this}" label="size" initialize="(event.target as DynamicColumnComponent).dynamicObject = _plotter.radiusColumn"/>
			<userControls:ControlsSubsetBox>
				<mx:HBox width="100%"> 
					<mx:CheckBox id="sizeByCheck" 
								 fontSize="12" 
								 label="Size by: ENABLED" 
								 color="{sizeByCheck.selected? 0x339900 : 0xCC3300}"
								 textRollOverColor="0x339900"
								 selected="true" 
								 click="handleSizeByTextChange(event)"/>
					<mx:Label id="sizeByColumnLabel" enabled="{sizeByCheck.selected}" color="0xffffff" width="100%"  fontSize="9"/>
				</mx:HBox>
				
				<mx:HBox width="100%"> 
					<mx:Label text="Point Size:" 
							  color="0x000000" 
							  fontSize="12"/>
					<mx:HSlider id="sizeBySlider"
								liveDragging="true"
								width="100%"
								thumbCount="2" 
								snapInterval="1"
								minimum="1" 
								maximum="60"
								tickInterval="5"
								change="handleSizeSliderChange(event)"
								mouseDown="event.stopImmediatePropagation();"/>
				</mx:HBox>
			</userControls:ControlsSubsetBox>
		</mx:VBox>
		<mx:VBox id="AdvancedPanel" label="Advanced">
			<userControls:ControlsSubsetBox>
				<mx:VBox>
					<mx:CheckBox id="regressionLineCheckBox" label="Enable regression line"
								 toolTip="Check this box to draw a regression line on the scatterplot"
								 initialize="linkBindableProperty(_regressionPlotter.drawLine,regressionLineCheckBox,'selected')"/>
					<mx:Label id="lineEquation" text="Equation: y = mx + b"/>
					<mx:HBox>
						<mx:Label text="Line Color" />
						<mx:ColorPicker id="regressionColorPicker" 
										initialize="linkBindableProperty(_regressionPlotter.lineStyle.color.defaultValue, regressionColorPicker, 'selectedColor')"
										enabled="{regressionLineCheckBox.selected}"/>
					</mx:HBox>
					<mx:HBox>
						<mx:Label text="Line Thickness" />
						<mx:HSlider id="regressionThicknessSlider"
									initialize="linkBindableProperty(_regressionPlotter.lineStyle.weight.defaultValue, regressionThicknessSlider, 'value')"
									enabled="{regressionLineCheckBox.selected}"
									minimum="1" maximum="10" snapInterval="1" liveDragging="true"
									tickInterval="1"/>
					</mx:HBox>
					<mx:HBox>
						<mx:Label text="Line Opacity" />
						<mx:HSlider id="regressionAlphaSlider"
									enabled="{regressionLineCheckBox.selected}"
									minimum="0.1" maximum="1" snapInterval=".1" liveDragging="true"
									tickInterval="0.1"
									initialize="linkBindableProperty(_regressionPlotter.lineStyle.alpha.defaultValue, regressionAlphaSlider, 'value')"/>
					</mx:HBox>
				</mx:VBox>
			</userControls:ControlsSubsetBox>
			<settings:GridLineSettings tool="{this}"/>
		</mx:VBox>
	</ui:ControlPanel>
</SimpleVisTool>
