<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/

-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
			   xmlns:mx="http://www.adobe.com/2006/mxml"
			   xmlns:ui="weave.ui.*" 
			   xmlns:settings="weave.ui.settings.*" xmlns:LinkableObjectList="weave.ui.LinkableObjectList.*"
			   xmlns:userControls="weave.ui.userControls.*"
			   layout="absolute">
	<mx:Script>
		<![CDATA[
			import mx.controls.TextArea;
			import mx.core.IToolTip;
			import mx.events.SliderEvent;
			import mx.managers.ToolTipManager;
			
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IKeySet;
			import weave.api.data.IQualifiedKey;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.primitives.IBounds2D;
			import weave.api.registerLinkableChild;
			import weave.api.ui.IVisTool;
			import weave.compiler.StandardLib;
			import weave.core.LinkableBoolean;
			import weave.core.LinkableNumber;
			import weave.data.AttributeColumns.ColorColumn;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.primitives.Bounds2D;
			import weave.ui.AttributeSelectorPanel;
			import weave.utils.BitmapText;
			import weave.utils.ColumnUtils;
			import weave.utils.LinkableTextFormat;
			import weave.utils.PlotterUtils;
			import weave.visualization.layers.LayerSettings;
			import weave.visualization.plotters.RegressionLinePlotter;
			import weave.visualization.plotters.ScatterPlotPlotter;

			WeaveAPI.registerImplementation(IVisTool, ScatterPlotTool, "Scatterplot");

			public var plotter:ScatterPlotPlotter = null;
			
			override protected function constructor():void
			{
				super.constructor();
				
				// lock dynamic objects into place
				plotter = initializePlotters(ScatterPlotPlotter, true);
				visualization.enableProbeLine(true, true);
				
				//BEGIN TEMPORARY SOLUTION
				visualization.plotManager.marginLeftNumber.addGroupedCallback(this, updateAxisLabels);
				visualization.plotManager.marginBottomNumber.addGroupedCallback(this, updateAxisLabels);
				getCallbackCollection(plotter).addGroupedCallback(this, updateAxisLabels, true);
				// END TEMPORARY SOLUTION
				
				visualization.enableZoomAndPan.value = false;
				
				xAxisPlotter.setLabelFunction(labelFunctionX);
				yAxisPlotter.setLabelFunction(labelFunctionY);
				
				var self:* = this;
				visualization.bottomMarginClickCallbacks.addImmediateCallback(this, function():void { AttributeSelectorPanel.openToolSelector(self, plotter.xColumn); })
				visualization.leftMarginClickCallbacks.addImmediateCallback(this, function():void { AttributeSelectorPanel.openToolSelector(self, plotter.yColumn); })
				
				visualization.enableAutoZoomXToNiceNumbers.value = true;
				visualization.enableAutoZoomYToNiceNumbers.value = true;
				
				getCallbackCollection(plotter).addGroupedCallback(this, handlePanelTitleChange, true);
				
				initColumnDefaults(plotter.xColumn, plotter.yColumn, colorColumn);
				
				plotter.xColumn.addGroupedCallback(this, updateProbeColumns);
				plotter.yColumn.addGroupedCallback(this, updateProbeColumns);
				colorColumn.addGroupedCallback(this, updateProbeColumns);
				
				visualization.plotManager.marginBottom.value = "80";
				visualization.plotManager.marginTop.value    = "30";
				visualization.plotManager.marginLeft.value   = "80";
				visualization.plotManager.marginRight.value  = "30";
				
				getCallbackCollection(plotter.circlePlotter).addGroupedCallback(this, handlePlotterChange, true);
				
				initializeUndefinedLayers();				
				initRegressionLayer();
			}
			
			override public function getSelectableAttributeNames():Array
			{
				return [lang("X"), lang("Y"), lang("Color"), lang("Size")];
			}
			override public function getSelectableAttributes():Array
			{
				return [plotter.xColumn, plotter.yColumn, colorColumn, plotter.radiusColumn];
			}
			
			private const REGRESSION_LAYER_NAME:String = "regressionLine";
			private var _regressionPlotter:RegressionLinePlotter = null;
			
			private function initRegressionLayer():void
			{
				_regressionPlotter = visualization.plotManager.plotters.requestObject(REGRESSION_LAYER_NAME, RegressionLinePlotter, true);
				linkSessionState(plotter.xColumn, _regressionPlotter.xColumn);
				linkSessionState(plotter.yColumn, _regressionPlotter.yColumn);
				_regressionPlotter.drawLine.value = false;				
			}
			
			
			/*BEGIN UNDEFINED LAYER CODE*/
					
			private var _undefinedXSettings:LayerSettings;
			private var _undefinedYSettings:LayerSettings;
			private var _undefinedXYSettings:LayerSettings;
			
			private var _undefinedXPlotter:ScatterPlotPlotter;
			private var _undefinedYPlotter:ScatterPlotPlotter;
			private var _undefinedXYPlotter:ScatterPlotPlotter;
			
			private const UNDEFINED_X_NAME:String = "undefinedX";
			private const UNDEFINED_Y_NAME:String = "undefinedY";
			private const UNDEFINED_XY_NAME:String = "undefinedXY";
			
			private var _tempPoint:Point = new Point();
			private const _tempBounds:Bounds2D = new Bounds2D();
			private const dataBounds:Bounds2D = new Bounds2D();
			private const _screenBounds:Bounds2D = new Bounds2D();						
			
			private function initializeUndefinedLayers():void
			{
				_undefinedXPlotter = visualization.plotManager.plotters.requestObject(UNDEFINED_X_NAME, ScatterPlotPlotter, true);
				_undefinedYPlotter = visualization.plotManager.plotters.requestObject(UNDEFINED_Y_NAME, ScatterPlotPlotter, true);
				_undefinedXYPlotter = visualization.plotManager.plotters.requestObject(UNDEFINED_XY_NAME, ScatterPlotPlotter, true);
				_undefinedXSettings = visualization.plotManager.getLayerSettings(UNDEFINED_X_NAME);
				_undefinedYSettings = visualization.plotManager.getLayerSettings(UNDEFINED_Y_NAME);
				_undefinedXYSettings = visualization.plotManager.getLayerSettings(UNDEFINED_XY_NAME);
				
				for each(var _plotter:ScatterPlotPlotter in [_undefinedXPlotter, _undefinedYPlotter, _undefinedXYPlotter])
				{
					linkSessionState(plotter.colorColumn, _plotter.colorColumn);
					linkSessionState(plotter.alphaColumn, _plotter.alphaColumn);
					linkSessionState(plotter.radiusColumn, _plotter.radiusColumn);					
					linkSessionState(plotter.defaultScreenRadius, _plotter.defaultScreenRadius);
					linkSessionState(plotter.enabledSizeBy, _plotter.enabledSizeBy);
					linkSessionState(plotter.maxScreenRadius, _plotter.maxScreenRadius);
					linkSessionState(plotter.minScreenRadius, _plotter.minScreenRadius);
				} 	
				
				linkSessionState(plotter.xColumn, _undefinedYPlotter.xColumn);
				linkSessionState(plotter.yColumn, _undefinedXPlotter.yColumn);
				
				for each(var column:IAttributeColumn in getSelectableAttributes())
				{
					column.addGroupedCallback(this, setUndefinedPlotterKeySource, true);
				}
				
				getCallbackCollection(visualization.plotManager.zoomBounds).addImmediateCallback(this, invalidateDisplayList);
				
				visualization.plotManager.marginTopNumber.addImmediateCallback(this, invalidateDisplayList);
				visualization.plotManager.marginBottomNumber.addImmediateCallback(this, invalidateDisplayList);
				visualization.plotManager.marginRightNumber.addImmediateCallback(this, invalidateDisplayList);
				visualization.plotManager.marginLeftNumber.addImmediateCallback(this, invalidateDisplayList);
				
				visualization.gridLineAlpha.addImmediateCallback(this, invalidateDisplayList);
				visualization.gridLineColor.addImmediateCallback(this, invalidateDisplayList);
				visualization.gridLineThickness.addImmediateCallback(this, invalidateDisplayList);
				
				for each (var settings:LayerSettings in [_undefinedXSettings, _undefinedYSettings, _undefinedXYSettings])
				{
					settings.hack_includeMissingRecordBounds = true;
					settings.hack_lockScreenBounds = new Bounds2D();
				}
				
				getCallbackCollection(visualization.plotManager.zoomBounds).addImmediateCallback(this, updateUndefinedPlotterBounds);
				
				addEventListener(MouseEvent.MOUSE_MOVE, displayToolTip);
				addEventListener(MouseEvent.MOUSE_OUT, destroyToolTip);
			}
			
			private function setUndefinedPlotterKeySource():void
			{
				var recordKeys:Array = plotter.keySet.keys;
				var missingXkeys:Array = [];
				var missingYkeys:Array = [];
				var missingXYkeys:Array = [];
				var missingX:Boolean;
				var missingY:Boolean;
				
				for each(var key:IQualifiedKey in recordKeys)	
				{
					missingX = !plotter.xColumn.containsKey(key);
					missingY = !plotter.yColumn.containsKey(key);
					
					if(missingX)
					{
						if(missingY)
							missingXYkeys.push(key);
						else 
							missingXkeys.push(key);
					}
					else if(missingY)
					{
						missingYkeys.push(key);
					}
				}
				_undefinedXPlotter.setCustomKeySource(missingXkeys);
				_undefinedYPlotter.setCustomKeySource(missingYkeys);
				_undefinedXYPlotter.setCustomKeySource(missingXYkeys);
			}
			
			private function updateUndefinedPlotterBounds():void
			{
				var b:IBounds2D = _tempBounds;
				visualization.plotManager.zoomBounds.getScreenBounds(b);
				if (b.isEmpty())
				{
					callLater(updateUndefinedPlotterBounds);
					return;
				}
				
				var graphics:Graphics = visualization.graphics;
				graphics.clear();
				graphics.lineStyle(visualization.gridLineThickness.value, visualization.gridLineColor.value, visualization.gridLineAlpha.value);
				
				var marginRight:Number = visualization.plotManager.marginRightNumber.value;
				
				// right margin; undefined x
				b = _undefinedXSettings.hack_lockScreenBounds;
				visualization.plotManager.zoomBounds.getScreenBounds(b);
				graphics.moveTo(b.getXMax() + marginRight / 2, b.getYMin());
				graphics.lineTo(b.getXMax() + marginRight / 2, b.getYMax());
				b.setXRange(b.getXMax() + marginRight * 1 / 4, b.getXMax() + marginRight * 3 / 4);
				
				// top margin; undefined y
				b = _undefinedYSettings.hack_lockScreenBounds;
				visualization.plotManager.zoomBounds.getScreenBounds(b);
				graphics.moveTo(b.getXMin(), b.getYMax() / 2);
				graphics.lineTo(b.getXMax(), b.getYMax() / 2);
				b.setYRange(b.getYMax() * 1 / 4, b.getYMax() * 3 / 4);
				
				// top-right corner; undefined x and y
				b = _undefinedXYSettings.hack_lockScreenBounds;
				visualization.plotManager.zoomBounds.getScreenBounds(b);
				b.setMinCoords(b.getXMax() + marginRight, 0);
			}
			
			
			private var _toolTip:IToolTip = null;
			private function displayToolTip(event:MouseEvent):void
			{
				destroyToolTip(event);
				_tempPoint = this.globalToContent(new Point(event.stageX, event.stageY));
								
				var probeExists:Boolean = mainLayerSettings.probeFilter.internalObject is IKeySet &&
					(mainLayerSettings.probeFilter.internalObject as IKeySet).keys.length > 0;

				var selectionExists:Boolean = mainLayerSettings.selectionFilter.internalObject is IKeySet &&
					(mainLayerSettings.selectionFilter.internalObject as IKeySet).keys.length > 0;
				
				if (probeExists || selectionExists)
					return;
				
				if (_undefinedXSettings.hack_lockScreenBounds.containsPoint(_tempPoint))
				{
					_tempPoint = this.contentToGlobal(_tempPoint);
					_toolTip = ToolTipManager.createToolTip(lang("undefined x"),_tempPoint.x, _tempPoint.y);	
					visualization.constrainToolTipsToStage(_toolTip);
					return;
				}
				if (_undefinedYSettings.hack_lockScreenBounds.containsPoint(_tempPoint))
				{
					_tempPoint = this.contentToGlobal(_tempPoint);
					_toolTip = ToolTipManager.createToolTip(lang("undefined y"),_tempPoint.x, _tempPoint.y);
					visualization.constrainToolTipsToStage(_toolTip);
					return;
				}
				if (_undefinedXYSettings.hack_lockScreenBounds.containsPoint(_tempPoint))
				{
					_tempPoint = this.contentToGlobal(_tempPoint);
					_toolTip = ToolTipManager.createToolTip(lang("undefined x,y"),_tempPoint.x, _tempPoint.y);
					visualization.constrainToolTipsToStage(_toolTip);
					return;
				}   
			}
			
			private function destroyToolTip(event:MouseEvent):void
			{
				if(_toolTip != null) 
				{
					ToolTipManager.destroyToolTip(_toolTip);
					_toolTip = null;
				}
			}
			/*END UNDEFINED LAYER CODE*/
			
			public const includeToolAttributesInProbe:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(true), updateProbeColumns);
			
			private function updateProbeColumns():void
			{
				if(includeToolAttributesInProbe.value)
				{
					visualization.additionalProbeColumns = [];
					if (plotter.xColumn.getInternalColumn())
						visualization.additionalProbeColumns.push(plotter.xColumn.getInternalColumn());
					if (plotter.yColumn.getInternalColumn())
						visualization.additionalProbeColumns.push(plotter.yColumn.getInternalColumn());
					if (colorColumn.getInternalColumn())
						visualization.additionalProbeColumns.push(colorColumn.getInternalColumn());
					if (visualization.additionalProbeColumns.length == 0)
						visualization.additionalProbeColumns = null ;
				}
				else visualization.additionalProbeColumns = null;
			}
			
			override protected function get defaultPanelTitle():String
			{
				return lang("Scatter Plot of {0} -vs- {1}", ColumnUtils.getTitle(plotter.xColumn), ColumnUtils.getTitle(plotter.yColumn));
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();

				plotter.colorColumn.addImmediateCallback(this, handleColorColumnChange, true);
				linkBindableProperty(plotter.alphaColumn.defaultValue, alphaSlider, "value");
				linkBindableProperty(plotter.enabledSizeBy, sizeByCheck, "selected");
				visualization.plotManager.marginLeftNumber.addGroupedCallback(this, updateEquation);
				visualization.plotManager.marginBottomNumber.addGroupedCallback(this, updateEquation);
				getCallbackCollection(_regressionPlotter).addGroupedCallback(this, updateEquation, true);
				showColorPicker.addGroupedCallback(this, updateColorPicker);
				overrideColor.addGroupedCallback(this, updateColorPicker);
				
				getCallbackCollection(LinkableTextFormat.defaultTextFormat).addGroupedCallback(this, updateEquationStyle, true);
			}
			
			private function updateEquationStyle():void
			{
				LinkableTextFormat.defaultTextFormat.copyToStyle(equationText);
			}
			
			private function updateEquation():void
			{
				if ( _regressionPlotter.drawLine.value )
				{
					if (equationText.parent != visCanvas)
						visCanvas.addChild(equationText);
					var slope:Number = StandardLib.roundSignificant(_regressionPlotter.getSlope(), 4);
					var intercept:Number = StandardLib.roundSignificant(_regressionPlotter.getIntercept(), 4);
					lineEquation.text = lang("Equation: y = {0}x + {1}", slope, intercept);
					equationText.text = lang("slope = {0}\nintercept = {0}", slope, intercept);
					equationText.validateNow();
					// severely hacked
					equationText.width = visualization.plotManager.marginLeftNumber.value * 2;
					equationText.height = Math.min(equationText.textHeight + 10, visualization.plotManager.marginBottomNumber.value);
				}
				else
				{ 
					if (equationText.parent)
						equationText.parent.removeChild(equationText);
					lineEquation.text = lang("Equation: y = mx + b");
				}
			}
			private function updateAxisLabels():void
			{
				visualization.bottomMarginColumn = plotter.xColumn;
				visualization.leftMarginColumn = plotter.yColumn;
				
				xAxisPlotter.setSideAxisName(
					ColumnUtils.getTitle(plotter.xColumn), 
					0,   
					0, visualization.plotManager.marginBottomNumber.value - 3, 
					BitmapText.VERTICAL_ALIGN_BOTTOM
				);
				
				yAxisPlotter.setSideAxisName(
					ColumnUtils.getTitle(plotter.yColumn), 
					-90, 
					-visualization.plotManager.marginLeftNumber.value, 0, 
					BitmapText.VERTICAL_ALIGN_TOP
				);
			}
			
			public function labelFunctionX(value:Number):String
			{
				return ColumnUtils.deriveStringFromNumber(plotter.xColumn, value);
			}
			public function labelFunctionY(value:Number):String
			{
				return ColumnUtils.deriveStringFromNumber(plotter.yColumn, value);
			}
			
			public function get colorColumn():DynamicColumn
			{
				return Weave.defaultColorDataColumn.internalDynamicColumn;
			}
			
			protected function createLegend():void
			{
				var sizeBinLegendTool:SizeBinLegendTool = Weave.root.requestObject(null, SizeBinLegendTool, false);
				sizeBinLegendTool.dynamicScatterPlotTool.globalName = Weave.root.getName(this);
			}
			
			private function updateColorPicker():void
			{
				if (!parent)
				{
					callLater(updateColorPicker);
					return;
				}
				
				if ( showColorPicker.value )
				{
					plotter.colorColumn.defaultValue.value = overrideColor.value;
					plotter.colorColumn.internalDynamicColumn.removeObject();
				}
				else
				{
					plotter.colorColumn.defaultValue.value = NaN;
					plotter.colorColumn.internalDynamicColumn.requestGlobalObject(_prevGlobalColorColumnName, ColorColumn, false);
				}
				scatterplotColorOverride.enabled = showColorPicker.value;
			}
			
			private var _prevGlobalColorColumnName:String = null;
			private function handleColorColumnChange():void
			{
				var newName:String = plotter.colorColumn.internalDynamicColumn.globalName;
				if (newName)
					_prevGlobalColorColumnName = newName;
			}
			
			public const showColorPicker:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(false));
			public const overrideColor:LinkableNumber = registerLinkableChild(this, new LinkableNumber());
				
			private function enableAbsHandler(event:Event):void
			{
				if( colorOverrideCheckbox.selected ) colorOverrideCheckbox.selected = false;
			}
			
			private function enableOverrideHandler(event:Event):void
			{
				if( absoluteValueColorEnabledCheckbox.selected ) absoluteValueColorEnabledCheckbox.selected = false;
			}
			
			
			private function handlePlotterChange():void
			{
				// when the plotter settings change, update the slider interface
				if (plotter.circlePlotter.absoluteValueColorEnabled.value)
				{
					sizeBySlider.thumbCount = 1;
					sizeBySlider.value = plotter.maxScreenRadius.value;
				}
				else if (plotter.enabledSizeBy.value)
				{
					sizeBySlider.thumbCount = 2;
					sizeBySlider.values = [plotter.minScreenRadius.value, plotter.maxScreenRadius.value];
				}
				else
				{
					sizeBySlider.thumbCount = 1;
					sizeBySlider.value = plotter.defaultScreenRadius.value;
				}
			}
			private function handleSizeSliderChange(event:SliderEvent):void
			{
				// when user interacts with slider, update session state
				if (plotter.circlePlotter.absoluteValueColorEnabled.value)
				{
					plotter.maxScreenRadius.value = event.value;
				}
				else if (plotter.enabledSizeBy.value)
				{
					if(event.thumbIndex == 0)
					{
						plotter.minScreenRadius.value = event.value;
					}
					else
					{
						plotter.maxScreenRadius.value = event.value;
					}
				}
				else
				{
					plotter.defaultScreenRadius.value = event.value;
				}
			}
		]]>
	</mx:Script>
	<mx:TextArea id="equationText"
		editable="false"
		verticalScrollPolicy="off"
		horizontalScrollPolicy="off"
		wordWrap="false"
		backgroundAlpha="0"
		borderThickness="0"
		bottom="0"
		left="0"/>
	<ui:ControlPanel>
		<mx:VBox id="VizControlsPanel" label="{lang('Visualization Controls')}">
			<ui:DynamicColumnComponent visTool="{this}" label="{lang('X')}" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = plotter.xColumn"/>
			<ui:DynamicColumnComponent visTool="{this}" label="{lang('Y')}" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = plotter.yColumn"/>
			<ui:DynamicColumnComponent visTool="{this}" label="{lang('Color')}" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = colorColumn" showLinkControl="false"/>
			<mx:CheckBox id="sizeByCheck" label="{lang('Size')}" fontWeight="bold" width="64" creationComplete="sizeSelector.addChildAt(sizeByCheck, 0);"/>
			<ui:DynamicColumnComponent id="sizeSelector" visTool="{this}" showLabel="false" creationComplete="(event.target as DynamicColumnComponent).dynamicObject = plotter.radiusColumn"/>
			<mx:HBox width="100%" verticalAlign="middle">
				
				<mx:Label text="{lang('Point Size:')}"/>
				<mx:HSlider id="sizeBySlider"
							liveDragging="true"
							width="100%"
							thumbCount="2"
							snapInterval="1"
							minimum="1" 
							maximum="60"
							tickInterval="5"
							change="handleSizeSliderChange(event)"
							mouseDown="event.stopImmediatePropagation();"/>
			</mx:HBox>
			<mx:HBox width="100%" verticalAlign="middle">
				<mx:Label text="{lang('Point Opacity:')}" 
						  toolTip="{lang('Use this control to see how transparent points are.  Moving left increases transparency (decreased opacity), moving to the right decreases transparency (increased opacity) up to fully opaque (no transparency).')}"/>
				
				<mx:HSlider id="alphaSlider" 
							width="100%" 
							alpha="1.0"
							dataTipFormatFunction="{PlotterUtils.alphaSliderFormatFunction}"
							minimum="0.1" maximum="1.0" value="1.0"
							liveDragging="true"/>
			</mx:HBox>
			<mx:CheckBox id="probeColumnsCheckBox" label="{lang('Include tool attributes in probe tooltip')}"
						 toolTip="{lang('Checking this box makes the x, y, color and size attributes visible in this tool\'s probe tooltip')}"
						 creationComplete="linkBindableProperty(includeToolAttributesInProbe,event.target,'selected')"/>
			<mx:Button label="{lang('Show legend')}" click="createLegend()"/>
		</mx:VBox>
		<mx:VBox id="AdvancedPanel" label="{lang('Advanced')}">
			<mx:VBox>
				<mx:CheckBox id="regressionLineCheckBox" label="{lang('Enable regression line')}"
							 toolTip="{lang('Check this box to draw a regression line on the scatterplot')}"
							 creationComplete="linkBindableProperty(_regressionPlotter.drawLine,event.target,'selected')"/>
				<ui:Indent>
					<mx:VBox>
						<mx:Label id="lineEquation" text="{lang('Equation: y = mx + b')}"/>
						<mx:HBox>
							<mx:Label text="{lang('Line Color')}" />
							<mx:ColorPicker id="regressionColorPicker" 
											creationComplete="linkBindableProperty(_regressionPlotter.lineStyle.color.defaultValue, event.target, 'selectedColor')"
											enabled="{regressionLineCheckBox.selected}"/>
						</mx:HBox>
						<mx:HBox>
							<mx:Label text="{lang('Line Thickness')}" />
							<mx:HSlider id="regressionThicknessSlider"
										creationComplete="linkBindableProperty(_regressionPlotter.lineStyle.weight.defaultValue, event.target, 'value')"
										enabled="{regressionLineCheckBox.selected}"
										minimum="1" maximum="10" snapInterval="1" liveDragging="true"
										tickInterval="1"/>
						</mx:HBox>
						<mx:HBox>
							<mx:Label text="{lang('Line Opacity')}" />
							<mx:HSlider id="regressionAlphaSlider"
										enabled="{regressionLineCheckBox.selected}"
										minimum="0.1" maximum="1" snapInterval=".1" liveDragging="true"
										tickInterval="0.1"
										creationComplete="linkBindableProperty(_regressionPlotter.lineStyle.alpha.defaultValue, event.target, 'value')"/>
						</mx:HBox>
					</mx:VBox>
				</ui:Indent>
			</mx:VBox>
			<mx:HBox>
				<mx:CheckBox id="colorOverrideCheckbox" label="{lang('Override Global Color')}" creationComplete="linkBindableProperty(showColorPicker,event.target,'selected')"
							 change="enableOverrideHandler(event)"/>
				<mx:ColorPicker id="scatterplotColorOverride" creationComplete="linkBindableProperty(overrideColor,event.target,'selectedColor')"/>
			</mx:HBox>
			<mx:VBox>
				<mx:HBox>
					<mx:CheckBox id="absoluteValueColorEnabledCheckbox" label="{lang('Color by positive or negative \'size\' column values')}"
								 creationComplete="linkBindableProperty(plotter.circlePlotter.absoluteValueColorEnabled, event.target, 'selected')"
								 change="enableAbsHandler(event)"/>
					<ui:HelpComponent>
						Select this option to highlight the differences in your 'size' column.
						* All values greater than 0 radius will be one color.
						* All values less than 0 radius will be another color.
						* If you have a size of 0 radius the circle will not be drawn.
						
						** If a size column is not specified, nothing will be drawn in this mode.
					</ui:HelpComponent>
				</mx:HBox>
				<ui:Indent>
					<mx:VBox>
						<mx:HBox>
							<mx:Label text="{lang('Color for values &lt; 0')}" />
							<mx:ColorPicker id="absoluteValueMinColorPicker" 
											creationComplete="linkBindableProperty(plotter.circlePlotter.absoluteValueColorMin, event.target, 'selectedColor')"
											enabled="{absoluteValueColorEnabledCheckbox.selected}" />
						</mx:HBox>
						<mx:HBox>
							<mx:Label text="{lang('Color for values &gt; 0')}" />
							<mx:ColorPicker id="absoluteValueMaxColorPicker" 
											creationComplete="linkBindableProperty(plotter.circlePlotter.absoluteValueColorMax, event.target, 'selectedColor')"
											enabled="{absoluteValueColorEnabledCheckbox.selected}" />
						</mx:HBox>
					</mx:VBox>
				</ui:Indent>
			</mx:VBox>
		</mx:VBox>
	</ui:ControlPanel>
</SimpleVisTool>
