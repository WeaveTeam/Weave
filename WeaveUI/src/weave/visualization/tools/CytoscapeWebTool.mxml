<SimpleVisTool xmlns="weave.visualization.tools.*" 
	     xmlns:mx="http://www.adobe.com/2006/mxml"
	     xmlns:ui="weave.ui.*" 
		 xmlns:userControls="weave.ui.userControls.*"
		 implements="weave.api.ui.IVisTool, weave.api.ui.ISelectableAttributes"
		 layout="vertical">
		<mx:VBox>

		<mx:Button id="reinitButton" label="Launch External Tool" click="reloadCytoscape()"/>
		<mx:Button id="raiseButton" label="Focus External Tool" click="raiseCytoscape()"/>

		</mx:VBox>
		
	
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
			
			import weave.Weave;
			import weave.api.data.ColumnMetadata;
			import weave.api.data.IKeySet;
			import weave.api.data.IQualifiedKey;
			import weave.api.linkBindableProperty;
			import weave.api.newDisposableChild;
			import weave.api.newLinkableChild;
			import weave.api.registerLinkableChild;
			import weave.api.reportError;
			import weave.api.ui.IVisTool;
			import weave.core.LinkableBoolean;
			import weave.core.LinkableNumber;
			import weave.core.LinkableString;
			import weave.core.SessionManager;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.KeySets.FilteredKeySet;
			import weave.data.KeySets.KeyFilter;
			import weave.data.KeySets.KeySet;

			// Node columns
			public const nodeColorColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleNodeColumnChange, true);
			public const nodeLabelColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleNodeColumnChange, true);
			public const nodeRadiusColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleNodeColumnChange, true);
			public const nodeParentColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleNodeColumnChange, true);
			
			
			// Edge columns
			public const edgeSourceColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleEdgeColumnChange, true);
			public const edgeTargetColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleEdgeColumnChange, true);
			public const edgeLabelColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleEdgeColumnChange, true);
			public const edgeThicknessColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleEdgeColumnChange, true);
			public const edgeColorColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleEdgeColumnChange, true);
			public const edgeWeightColumn:DynamicColumn = newLinkableChild(this, DynamicColumn, handleEdgeColumnChange, true);
			
			// General configuration
			public const layoutSeed:LinkableNumber = registerLinkableChild(this, new LinkableNumber(0), handleGraphLayoutChange);
			public const layoutType:LinkableString = registerLinkableChild(this, new LinkableString("ForceDirected"), handleGraphLayoutChange);
			private const layouts:Array = ["CompoundSpringEmbedder", "ForceDirected", "Circle", "Radial", "Tree"];

			private var subsetFilter:KeyFilter = Weave.defaultSubsetKeyFilter;
			private var _selection:KeySet = Weave.defaultSelectionKeySet;
			private var _probe:KeySet = Weave.defaultProbeKeySet;				

			WeaveAPI.ClassRegistry.registerImplementation(IVisTool, CytoscapeWebTool, "Cytoscape-Web Tool");
			private var _toolName:String;

			// Scratchspace for prepping the data to be sent to CytoscapeWeb
	
			private var _edgeKeyType:String;
			private var _nodeKeyType:String;
			private var _primitiveEdges:Object;
			private var _primitiveNodes:Object;
			private var _primitiveLayout:Object;
			public const _nodeFilteredKeys:FilteredKeySet = newDisposableChild(this, FilteredKeySet);
			public const _edgeFilteredKeys:FilteredKeySet = newDisposableChild(this, FilteredKeySet);

			public const _cytoscapeReady:LinkableBoolean = newDisposableChild(this, LinkableBoolean);

			override protected function inConstructor():void
			{
				super.inConstructor();
				_nodeFilteredKeys.keyFilter.targetPath = [Weave.DEFAULT_SUBSET_KEYFILTER];
				_edgeFilteredKeys.keyFilter.targetPath = [Weave.DEFAULT_SUBSET_KEYFILTER];
				_nodeFilteredKeys.setColumnKeySources([nodeLabelColumn, nodeColorColumn, nodeRadiusColumn]);
				_edgeFilteredKeys.setColumnKeySources([edgeSourceColumn, edgeTargetColumn, edgeThicknessColumn, edgeColorColumn, edgeWeightColumn]);
			}
			public function cytoscapeReady():void
			{
				_cytoscapeReady.value = true;
				_probe.addGroupedCallback(this, function ():void { WeaveAPI.StageUtils.startTask(this, handleProbeChange, WeaveAPI.TASK_PRIORITY_NORMAL); });
				_selection.addGroupedCallback(this, handleSelectionChange);
				_nodeFilteredKeys.addGroupedCallback(this, handleSubsetChange, true);
				_edgeFilteredKeys.addGroupedCallback(this, handleSubsetChange, true);
				if (_primitiveEdges != null && _primitiveNodes != null && _primitiveLayout != null)
				{
					sendNetworkToCytoscape();
				}
			}

			private function raiseCytoscape():void
			{
				if (!_cytoscapeReady.value)
					return;
				JavaScript.exec(
					{
						"catch": reportError,
						"toolname": _toolName
					},
					"this.external_tools[toolname].focus();"
				);
			}
			private function launchTimeout(event:TimerEvent):void
			{
				if (!_cytoscapeReady.value)
				{
					Alert.show(lang("This external tool failed to load. Try disabling your popup blocker for this site to prevent this from happening in the future."), _toolName + " " + lang("Error"), Alert.OK);
				}
			}
			private function reloadCytoscape():void
			{	
				_cytoscapeReady.value = false;
				launchCytoscape();
				handleGraphLayoutChange();
				handleEdgeColumnChange();
				handleNodeColumnChange();
			}
			private function launchCytoscape():void
			{
				var windowFeatures:String = "menubar=no,status=no,toolbar=no";
				var success:Boolean = JavaScript.exec(
					{
						"catch": reportError,
						"toolname": _toolName,
						"url": "CytoscapeWebPopout.html",
						"features": windowFeatures
					},
					"if (!this.external_tools)",
					"    this.external_tools = {};",
					"var popup = window.open(url, toolname, features);",
					"this.external_tools[toolname] = popup;",
					"return !!popup;"
				);
				if (!success)
					reportError("Popup blocked by web browser");

				var launchTimer:Timer = new Timer(5000, 1);
				launchTimer.addEventListener(TimerEvent.TIMER, launchTimeout);
				launchTimer.start();
			}


			private function handleProbeChange():Number
			{
				if (!_cytoscapeReady.value)
					return 1;
				JavaScript.exec(
					{
						"catch": reportError,
						"toolname": _toolName,
						"elements": _probe.keys
					},
					"this.external_tools[toolname].probe(elements);"
				);
				return 1;
			}
			private function handleSelectionChange():void
			{
				if (!_cytoscapeReady.value)
				{
					callLater(handleSelectionChange);
					return;
				}
				JavaScript.exec(
					{
						"catch": reportError,
						"toolname": _toolName,
						"elements": _selection.keys
					},
					"this.external_tools[toolname].select(elements);"
				);
			}
			public function handleSubsetChange():void
			{
				if (!_cytoscapeReady.value)
				{
					callLater(handleSubsetChange);
					return;
				}
				JavaScript.exec(
					{
						"catch": reportError,
						"toolname": _toolName,
						"elements": _nodeFilteredKeys.keys.concat(_edgeFilteredKeys.keys)
					},
					"this.external_tools[toolname].subset(elements);"
				);
			}

			private const _networkSchema:Object = {
					nodes: [{name: "label", type: "string", defValue: ""}, 
							{name: "keyType", type: "string"}, 
							{name: "localName", type: "string"},
							{name: "color", type: "string", defValue: ""},
							{name: "size", type: "number", defValue: 30},
							{name: "parent", type: "string", defValue: ""},
							{name: "shape", type:"string", defValue: "ELLIPSE"}],
					edges: [{name: "label", type: "string", defValue: ""},
							{name: "keyType", type: "string"}, 
							{name: "localName", type: "string"},
							{name: "color", type: "string", defValue: ""},
							{name: "width", type: "number", defValue: 5},
							{name: "weight", type: "number", defValue: 1}]
			};

			private const nodePropertyMappings:Object = {
				label:{column:nodeLabelColumn, type:String},
				color:{column:nodeColorColumn, type:String},
				size:{column:nodeRadiusColumn, type:Number},
				parent:{column:nodeParentColumn, type:String}
			};

			private const edgePropertyMappings:Object = {
				label:{column:edgeLabelColumn, type:String},
				source:{column:edgeSourceColumn, type:String, required:true},
				target:{column:edgeTargetColumn, type:String, required:true},
				color:{column:edgeColorColumn, type:String},
				width:{column:edgeThicknessColumn, type:Number},
				weight:{column:edgeWeightColumn, type:Number}
			};

			private function buildPrimitiveElements(keyset:IKeySet, mapping:Object):Array
			{
				var elements:Array = [];

				var keys:Array = keyset.keys;
				var keys_len:int = keys.length;
				var key_idx:int;

				for (key_idx = 0; key_idx < keys_len; key_idx++)
				{
					var element:Object = {};
					var key:IQualifiedKey;
					key = keys[key_idx];

					// Fixed element properties
					element.keyType = key.keyType;
					element.localName = key.localName;
					for (var prop:String in mapping)
					{
						element[prop] = mapping[prop].column.getValueFromKey(key, mapping[prop].type);

						// If the element is invalid (missing a required field),
						// set it to null so we know not to add it.
						if ((element[prop] == null) && (mapping[prop].required == true))
						{
							weaveTrace("qkey " + element.keyType + " " + element.localName + "discarded from graph output: Missing property" + prop +"\n");
							element = null;
							break;
						}
					}
					
					if (element != null)
					{
						elements.push(element);
					}
				}

				return elements;
			}

			private function handleNodeColumnChange():void
			{
				_nodeKeyType =  nodeColorColumn.getMetadata(ColumnMetadata.KEY_TYPE) ||
								nodeLabelColumn.getMetadata(ColumnMetadata.KEY_TYPE) ||
								nodeRadiusColumn.getMetadata(ColumnMetadata.KEY_TYPE);

				_primitiveNodes = buildPrimitiveElements(nodeLabelColumn, nodePropertyMappings);								

				sendNetworkToCytoscape();
			}

			private function handleEdgeColumnChange():void
			{
				/* If edgesource and edgetarget don't share a common keytype, don't bother rebuilding the edges; the result will be garbage. */
				var srcKeyType:String = edgeSourceColumn.getMetadata(ColumnMetadata.KEY_TYPE);
				var targetKeyType:String = edgeTargetColumn.getMetadata(ColumnMetadata.KEY_TYPE);
				if (srcKeyType != targetKeyType) return;
				
				_edgeKeyType = srcKeyType;
				_primitiveEdges = buildPrimitiveElements(edgeSourceColumn, edgePropertyMappings);

				sendNetworkToCytoscape();
			}

			private function sendNetworkToCytoscape():void
			{
				if (!_cytoscapeReady.value || _primitiveNodes == null || _primitiveEdges == null)
				{
					callLater(sendNetworkToCytoscape);
					return;
				}
				var parameters:Object = {};
				var element_to_key_types:Object = {nodes: _nodeKeyType, edges: _edgeKeyType};
				var primitiveElements:Array = _primitiveNodes.concat(_primitiveEdges);

				parameters.dataSchema = _networkSchema;
				parameters.layout = _primitiveLayout;
				JavaScript.exec(
					{
						"catch": reportError,
						"toolname": _toolName,
						"records": primitiveElements,
						"element_to_key_types": element_to_key_types,
						"parameters": parameters
					},
					"this.external_tools[toolname].raw_output;",
					"this.external_tools[toolname].load_data(records, element_to_key_types, null);",
					"this.external_tools[toolname].launch(parameters);"
				);
			}

			private function handleGraphLayoutChange():void
			{
				if (!_cytoscapeReady.value)
				{
					callLater(handleGraphLayoutChange);
					return;
				}
				_primitiveLayout = {
					name: layoutType.value, 
					options: {seed: layoutSeed.value, weightAttr:"weight"}
				};
				JavaScript.exec(
					{
						"catch": reportError,
						"toolname": _toolName,
						"layout": _primitiveLayout
					},
					'this.external_tools[toolname].vis.layout(layout);'
				);
			}

			override protected function childrenCreated():void
			{
				super.childrenCreated();

				nodeColorComponent.dynamicObject = nodeColorColumn;
				nodeParentComponent.dynamicObject = nodeParentColumn;
				nodeRadiusComponent.dynamicObject = nodeRadiusColumn;
				nodeLabelComponent.dynamicObject = nodeLabelColumn;
				edgeSourceComponent.dynamicObject = edgeSourceColumn;
				edgeTargetComponent.dynamicObject = edgeTargetColumn;
				edgeThicknessComponent.dynamicObject = edgeThicknessColumn;
				edgeLabelComponent.dynamicObject = edgeLabelColumn;
				edgeWeightComponent.dynamicObject = edgeWeightColumn;
				
				_toolName = WeaveAPI.CSVParser.createCSVRow((WeaveAPI.SessionManager as SessionManager).getPath(WeaveAPI.globalHashMap, this));
			}
			
			override public function getSelectableAttributes():Array
			{
				return [nodeLabelColumn, nodeColorColumn, nodeRadiusColumn, nodeParentColumn, edgeSourceColumn, edgeTargetColumn, edgeThicknessColumn, edgeLabelColumn, edgeWeightColumn];
			}
			override public function getSelectableAttributeNames():Array
			{
				return ["Node Labels", "Node Color", "Node Size", "Node Parent ID", "Edge Sources", "Edge Targets", "Edge Thickness", "Edge Labels", "Edge Weight"];
			}

		]]>
	</mx:Script>
		<ui:ControlPanel>
		<mx:VBox id="attributesPanel" label="{lang('Attributes for Graph')}">
			<ui:DynamicColumnComponent id="nodeLabelComponent"/>
			<ui:DynamicColumnComponent id="nodeColorComponent"/>
			<ui:DynamicColumnComponent id="nodeRadiusComponent"/>
			<ui:DynamicColumnComponent id="nodeParentComponent"/>
			<ui:DynamicColumnComponent id="edgeSourceComponent"/>
			<ui:DynamicColumnComponent id="edgeTargetComponent"/>
			<ui:DynamicColumnComponent id="edgeThicknessComponent"/>
			<ui:DynamicColumnComponent id="edgeLabelComponent"/>
			<ui:DynamicColumnComponent id="edgeWeightComponent"/>
		</mx:VBox>
		<mx:VBox id="VizControlsPanel" label="{lang('Visualization Controls')}">
		 	<mx:HBox>
		 		<mx:Label text="{lang('Layout Method')}"/>
		 		<mx:ComboBox id="layoutCombo" 
				creationComplete="linkBindableProperty(this.layoutType, event.target, 'selectedItem')"
				dataProvider="{this.layouts}"/>
			</mx:HBox>
			<mx:HBox>
				<mx:Label text="{lang('Radius Size')}"
						  toolTip="{lang('Use this option to control the default size of the each node in the graph.')}"/>
				<mx:NumericStepper id="numRadiusSize" minimum="1" maximum="100"/>
			</mx:HBox>

			 <mx:CheckBox id="undirectedCheckBox" label="{lang('Undirected edges.')}" 
				toolTip="{lang('Use this option if you want your graph data to be assumed as undirected.')}"/> 
		</mx:VBox>
	</ui:ControlPanel>
</SimpleVisTool>
