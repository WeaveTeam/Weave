<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
	     xmlns:mx="http://www.adobe.com/2006/mxml"
	     xmlns:ui="weave.ui.*" 
		 xmlns:userControls="weave.ui.userControls.*"
		 xmlns:settings="weave.ui.settings.*"
		 layout="absolute">
	<mx:Script>
		<![CDATA[
			import weave.api.WeaveAPI;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.registerLinkableChild;
			import weave.api.ui.IVisTool;
			import weave.api.unlinkSessionState;
			import weave.core.LinkableBoolean;
			import weave.data.AttributeColumns.BinnedColumn;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.FilteredColumn;
			import weave.ui.AttributeSelectorPanel;
			import weave.utils.BitmapText;
			import weave.utils.ColumnUtils;
			import weave.visualization.plotters.HistogramPlotter;
		
			WeaveAPI.registerImplementation(IVisTool, HistogramTool, "Histogram");
			
			private var _plotter:HistogramPlotter = null;
			public function get plotter():HistogramPlotter { return _plotter; }
			
			override protected function constructor():void
			{
				super.constructor();
				
				// lock dynamic objects into place
				_plotter = initializePlotters(HistogramPlotter, true);
				visualization.enableProbeLine(false, true);
				
				xAxisLayer.axisPlotter.forceTickCount.value = true;
				visualization.enableZoomAndPan.value = false;

				visualization.marginBottom.value = "80";
				visualization.marginTop.value    = "10";
				visualization.marginLeft.value   = "80";
				visualization.marginRight.value  = "10";
				
				_plotter.dynamicColorColumn.addGroupedCallback(this, handleDynamicColorColumnChange, true);

				visualization.marginLeftNumber.addGroupedCallback(this, updateAxisLabels);
				visualization.marginBottomNumber.addGroupedCallback(this, updateAxisLabels);
				getCallbackCollection(visualization.zoomBounds).addGroupedCallback(this, updateAxisLabels);
				getCallbackCollection(_plotter).addGroupedCallback(this, updateAxisLabels, true);
				
				visualization.bottomMarginClickCallbacks.addGroupedCallback(this, openAttributeSelector);
				
				var fc:FilteredColumn = _plotter.internalBinnedColumn.internalColumn as FilteredColumn;
				initColumnDefaults(fc.internalDynamicColumn);
				
				_plotter.dynamicColorColumn.addGroupedCallback(this, updateProbeColumns);
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();
			}
			
			override public function getSelectableAttributeNames():Array
			{
				return ["bin data"];
			}
			override public function getSelectableAttributes():Array
			{
				return [internalDataColumn];
			}
			
			public const includeToolAttributesInProbe:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(true), updateProbeColumns);

			private function openAttributeSelector():void
			{
				AttributeSelectorPanel.openToolSelector(this,internalDataColumn);
			}
						
			private function updateProbeColumns():void
			{
				visualization.additionalProbeColumns = [];
				if (includeToolAttributesInProbe.value)
				{
					if (_plotter.internalBinnedColumn && _plotter.internalBinnedColumn.internalColumn)
						visualization.additionalProbeColumns.push(_plotter.internalBinnedColumn.internalColumn);
				}
			}
			
			override protected function get defaultPanelTitle():String
			{
				return "Histogram of " + ColumnUtils.getTitle(_plotter.dynamicColorColumn);
			}

			private var _prevBinnedColumn:BinnedColumn = null;
			private function handleDynamicColorColumnChange():void
			{
				if (!parent)
					return callLater(handleDynamicColorColumnChange);
				
				if (_plotter.internalColorColumn)
					cRampEditor.colorRamp = _plotter.internalColorColumn.ramp;
				else
					cRampEditor.colorRamp = null;
				
				if (_prevBinnedColumn)
					unlinkSessionState(_prevBinnedColumn, binEditor.binnedColumn);
				
				if (_plotter.internalBinnedColumn)
					linkSessionState(_plotter.internalBinnedColumn, binEditor.binnedColumn);
				
				
				_prevBinnedColumn = _plotter.internalBinnedColumn;
				dataColumnComponent.dynamicObject = internalDataColumn;
			}
			
			public function get internalDataColumn():DynamicColumn
			{
				try
				{
					return (_plotter.internalBinnedColumn.internalColumn as FilteredColumn).internalDynamicColumn;
				}
				catch (e:Error) { } // do nothing if there is no data column
				return null;
			}
			
			private function updateAxisLabels():void
			{
				handlePanelTitleChange();
				
				visualization.bottomMarginColumn = _plotter.dynamicColorColumn;
				
				var binnedColumn:BinnedColumn = _plotter.internalBinnedColumn;
				if (binnedColumn)
				{
					xAxisLayer.axisPlotter.tickCountRequested.value = binnedColumn.numberOfBins;
					xAxisLayer.axisPlotter.tickMinValue.value = 0;
					xAxisLayer.axisPlotter.tickMaxValue.value = binnedColumn.numberOfBins - 1;	
					xAxisLayer.axisPlotter.setLabelFunction(binnedColumn.deriveStringFromNumber);
				}
				else
				{
					xAxisLayer.axisPlotter.setLabelFunction(null);
				}
				
				xAxisLayer.axisPlotter.setSideAxisName(ColumnUtils.getTitle(_plotter.dynamicColorColumn), 0, 0, visualization.marginBottomNumber.value - 3, BitmapText.VERTICAL_ALIGN_BOTTOM);
				yAxisLayer.axisPlotter.setSideAxisName("Number of Records", -90, -visualization.marginLeftNumber.value, 0, BitmapText.VERTICAL_ALIGN_TOP);
			}
		]]>
	</mx:Script>

	<ui:ControlPanel>
		<mx:VBox label="Visualization Controls">
			<mx:CheckBox id="probeColumnsCheckBox" label="Include tool attributes in probe tooltip"
						 toolTip="Checking this box makes the tool's binned attribute visible in this tool's probe tooltip"
						 creationComplete="linkBindableProperty(includeToolAttributesInProbe,event.target,'selected')"/>
			<ui:DynamicColumnComponent visTool="{this}" id="dataColumnComponent" label="bin data"/>
			<ui:BinningDefinitionEditor id="binEditor"/>
		</mx:VBox>
		<mx:VBox label="Advanced">
			<mx:CheckBox id="drawPartialCheckBox"
						 label="Draw partial histogram bars for selection and probe"
						 creationComplete="linkBindableProperty(_plotter.drawPartialBins, event.target, 'selected');"/>
			<ui:ColorRampEditor id="cRampEditor"/>
		</mx:VBox>
	</ui:ControlPanel>
</SimpleVisTool>
