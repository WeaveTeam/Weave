<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<SliderTool
	xmlns="weave.visualization.tools.*" 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:ui="weave.ui.*"
	xmlns:settings="weave.ui.settings.*"
	xmlns:LinkableObjectList="weave.ui.LinkableObjectList.*"
	verticalScrollPolicy="off"
	width="400" 
	title="{lang('Dimension Slider Tool')}"
	height="40">

<mx:Script>
		<![CDATA[
			import weave.api.WeaveAPI;
			import weave.api.ui.IVisTool;
			import weave.data.AttributeColumns.ProxyColumn;
			import weave.data.AttributeColumns.SecondaryKeyNumColumn;
			import weave.data.ColumnReferences.HierarchyColumnReference;

			WeaveAPI.registerImplementation(IVisTool, DimensionSliderTool, "Dimension Slider Tool");
			
	override protected function constructor():void
	{
		super.constructor();
	}
	override public function dispose():void
	{
		super.dispose();
		SecondaryKeyNumColumn.secondaryKeyFilter.value = null;
	}
	
	public static function get dimensionSliderValue():String
	{
		return SecondaryKeyNumColumn.secondaryKeyFilter.value;
	}
			
	//class data

	private var values:Array = [];
	
	//overrides of abstract SliderTool functions
	
	/**
	 * This function gets called after children have been created
	 */ 
	override protected function childrenCreated():void
	{
		super.childrenCreated();
		if (dimsLabel != null)
			dimsLabel.text = lang("Replace labels with values from a dimension with a secondary key:");
		
		SecondaryKeyNumColumn.secondaryKeyFilter.addGroupedCallback(this, handleSliderChange, true);
		checkBox_useGlobalMinMax.visible = true;
		checkBox_useGlobalMinMax.includeInLayout = true;
	}

	/**
	 * This function finds a list of attribute tags in the same attribute hierarchy
	 * that have the same @dataTableName and @name attributes, then returns a list of
	 * their @year values.
	 * @param ref A reference to an attribute tag in an AttributeHierarchy.
	 * @return An Array of @year values that the referenced column is defined for.
	 */
	override protected function getValuesFromColHier(ref:HierarchyColumnReference):Array
	{
		var dimensionColumn:ProxyColumn = WeaveAPI.AttributeColumnCache.getColumn(ref) as ProxyColumn;
		if (dimensionColumn)
		{
			var internalColumn:SecondaryKeyNumColumn = dimensionColumn.getInternalColumn() as SecondaryKeyNumColumn;
			if (internalColumn)
			{
				addToListOfDims(ref);
				return internalColumn.secondaryKeys;
			}
		}
		return [];
	}

	/**
	 * handleLabelChange
	 * This method is called every time there is a change in labels,
	 * so be sure to change labels only as often as is necesary.  
	 * We fill in the hslider labels from this.labelsLinkableString
	 * Since this.labelsLinkableString is a LinkableString it can be changed by
	 * the user by editing the sessionHistory.  
	 * */
	override public function handleLabelChange():void
	{
		//@TODO have values associated with the labels so that the labels can be
		//   changed and the values are still passed into the column request
		if (!parent)
			return callLater(handleLabelChange);
		var labelsString:String = labelsLinkableString.value;
		values = labelsString.split(',');
		if (values.length <= maxLabelCount.value)
			dimensionSlider.labels = values;
		else
		{
			dimensionSlider.labels = new Array();
			
			var interval:int = Math.ceil(values.length / maxLabelCount.value);
			for (var i:int = 0; i < values.length; ++i)
			{
				if (i % interval == 0)
					dimensionSlider.labels.push(values[i]);
				else
					dimensionSlider.labels.push('');
			}			
		}

		dimensionSlider.minimum = 0;
		dimensionSlider.maximum = values.length - 1;
		
		//set tick interval 
		dimensionSlider.tickInterval = 1;
	}	

	/** 
	 * change - this method is automatically called when the hslider
	 *  is changed.  Here we update all the visualization tools to 
	 *  the new value.  
	 * **/
	override public function handleSliderChange():void
	{
		SecondaryKeyNumColumn.secondaryKeyFilter.value = values[dimensionSlider.value];
	}
	
	
	/**
	 * This is specified in the HSlider above - called to get the tooltip that appears when 
	 * the user probes over the slider.  Item is the index of the value that the user is hovering 
	 * over.  
	 **/
	override protected function formatDataTip(item:Number):String 
	{
		return values[item];
	}
			
			
		]]>
	</mx:Script>
</SliderTool>

